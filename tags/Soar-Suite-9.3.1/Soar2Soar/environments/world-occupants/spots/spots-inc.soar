sp {spots*propose*spots-inc
   (state <s> ^name spots
              ^x <x>
              ^y <y>)
-->
   (<s> ^operator <op> + <)
   (<op> ^name spots-inc
         ^x <x>
         ^y <y>)
}

sp {apply*spots-inc*y
   (state <s> ^operator <op>
              ^params <params>
              ^next-y <n-y>
              ^prev-y <p-y>
              ^y <y>)
   (<params> ^stop-y {<> <n-y>})
   (<op> ^name spots-inc
         ^x <x>
         ^y <y>)
-->
   (<s> ^y <y> - <n-y>
        ^next-y <n-y> - (+ <n-y> 1)
        ^prev-y <p-y> - <y>)
}

sp {apply*spots-inc*x
   (state <s> ^operator <op>
              ^params <params>
              ^next-x <n-x>
              ^next-y <n-y>
              ^prev-x <p-x>
              ^prev-y <p-y>
              ^x <x>
              ^y <y>)
   (<params> ^stop-x {<> <n-x>}
             ^stop-y <n-y>
             ^start-y <s-y>)
   (<op> ^name spots-inc
         ^x <x>
         ^y <y>)
-->
   (<s> ^x <x> - <n-x>
        ^y <y> - <s-y>
        ^next-x <n-x> - (+ <n-x> 1)
        ^next-y <n-y> - (+ <s-y> 1)
        ^prev-x <p-x> - <x>
        ^prev-y <p-y> - (- <s-y> 1))
}

sp {apply*spots-inc*done*add
   (state <s> ^operator <op>
              ^task add
              ^params <params>
              ^next-x <n-x>
              ^next-y <n-y>
              ^prev-x <p-x>
              ^prev-y <p-y>
              ^x <x>
              ^y <y>)
   (<params> ^stop-x <n-x>
             ^stop-y <n-y>
             ^start-x <s-x>
             ^start-y <s-y>)
   (<op> ^name spots-inc
         ^x <x>
         ^y <y>)
-->
   (<s> ^task add - relations
        ^x <x> - <s-x>
        ^y <y> - <s-y>
        ^next-x <n-x> - (+ <s-x> 1)
        ^next-y <n-y> - (+ <s-y> 1)
        ^prev-x <p-x> - (- <s-x> 1)
        ^prev-y <p-y> - (- <s-y> 1))
}


sp {apply*spots-inc*done*relations
   (state <s> ^operator <op>
              ^task relations
              ^superstate <ss>
              ^params <params>
              ^next-x <n-x>
              ^next-y <n-y>
              ^x <x>
              ^y <y>)
   (<params> ^stop-x <n-x>
             ^stop-y <n-y>)
   (<op> ^name spots-inc
         ^x <x>
         ^y <y>)
-->
   (<ss> ^spots ready)
}