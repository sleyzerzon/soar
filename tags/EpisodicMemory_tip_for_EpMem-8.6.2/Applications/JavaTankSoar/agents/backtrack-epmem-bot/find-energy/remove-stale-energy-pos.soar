#IF my x position is different 
#   from the last energy position
#AND the last direction is not east or west
#THEN remove the stale position
sp {find-energy*propose*remove-stale-energy-pos-x
   (state <s> ^name find-energy
              ^io.input-link <il>
              ^top-state.last-energy-epmem <lee>)
   (<il> ^x <x>)
   (<lee> ^x <old-x> <> <x>
          ^direction <dir> <> east <> west)
          
-->
   (<s> ^operator <op> + =)
   (<op> ^name remove-stale-energy-pos)
}

#IF my y position is different 
#   from the last energy position
#AND the last direction is not north or south
#THEN remove the stale position
sp {find-energy*propose*remove-stale-energy-pos-y
   (state <s> ^name find-energy
              ^io.input-link <il>
              ^top-state.last-energy-epmem <lee>)
   (<il> ^y <y>)
   (<lee> ^y <old-y> <> <y>
          ^direction <dir> <> north <> south)
          
-->
   (<s> ^operator <op> + =)
   (<op> ^name remove-stale-energy-pos)
}
   
#Remove stale positions before
#move operators are selected because of them
sp {find-energy*compare*remove-stale-energy-pos
   (state <s> ^name find-energy
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name remove-stale-energy-pos)
   (<op2> ^name move)
-->
   (<s> ^operator <op1> > <op2>)
}

#Do the removal
sp {apply*remove-stale-energy-pos
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name remove-stale-energy-pos)
   (<ts> ^last-energy-epmem <lee>)
-->
   (<ts> ^last-energy-epmem <lee> -)
}

   

