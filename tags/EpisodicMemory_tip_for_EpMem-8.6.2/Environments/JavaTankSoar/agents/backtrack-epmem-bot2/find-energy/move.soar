
#If I'm not blocked in a particular direction, 
#propose moving in that direction
sp {find-energy*propose*move
   (state <s> ^name find-energy
              ^io.input-link <il>
              ^top-state.blocked.<abs-dir> no)
   (<il> ^x <x>
         ^y <y>)
-->
   (<s> ^operator <op> +)
   (<op> ^name move
         ^abs-dir <abs-dir>)
}

#IF a path has been found to energy charger
#THEN prefer moving along that path
sp {find-energy*compare*move*energy-path
   (state <s> ^name find-energy
              ^top-state.available-energy-path.abs-dir <dir>
              ^io.input-link <il>
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name move
          ^abs-dir <dir>)
   (<op2> -^abs-dir <dir>)
-->
   (<s> ^operator <op1> > <op2>)
}

#IF I'm am blocked on the left and right
#THEN prefer moving forward rather than turning around
sp {find-energy*compare*move*avoid-turnaround
   (state <s> ^name find-energy
              ^top-state.coord.forward <forward-dir>
              ^io.input-link.blocked <b>
              ^operator <op1> +
                        <op2> +)
   (<b> ^left yes
        ^right yes)
   (<op1> ^name move
          ^abs-dir <forward-dir>)
   (<op2> ^name move
         -^abs-dir <forward-dir>)
-->
   (<s> ^operator <op1> > <op2>)
}


#Move in the selected direction
sp {apply*move
   (state <s> ^operator <op>)
   (<op> ^name move
         ^abs-dir <abs-dir>)
-->
#   (write (crlf) |=================== Moving | <abs-dir> | =======================|)
}

