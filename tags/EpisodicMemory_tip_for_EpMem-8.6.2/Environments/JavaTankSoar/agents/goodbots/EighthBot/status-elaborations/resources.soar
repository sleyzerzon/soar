echo "\nLoading status-elaborations/resources.soar"

#####################################################
##  Create Status Category (insert dummy placeholder statement
######################################################
sp {elaborate*status*alive
   (state <s> ^name tanksoar)
-->
   (<s> ^status.alive yes)
}








################################################################
### Energy status - critical/low/high
################################################################
sp {elaborate*status*energy*critical
   (state <s> ^name tanksoar
              ^parameters.energy-critical <ec>
              ^io.input-link <il>
              ^status <st>)
   (<il> ^energy <= <ec>)
   -->
   (<st> ^energy-level critical)
}

sp {elaborate*status*energy*low
   (state <s> ^name tanksoar
              ^parameters <p>
              ^io.input-link <il>
              ^status <st>)
   (<p> ^energy-critical <ec>
        ^energy-low <el>)
   (<il> ^energy > <ec>
         ^energy <= <el>)
   -->
   (<st> ^energy-level low)}

sp {elaborate*status*energy*high
   (state <s> ^name tanksoar
              ^parameters.energy-low <el>
              ^io.input-link <il>
              ^status <st>)
   (<il> ^energy > <el>)
-->
   (<st> ^energy-level high)}



################################################################
### Health status - critical/low/high
################################################################
sp {elaborate*status*health*critical
   (state <s> ^name tanksoar
              ^parameters.health-critical <hc>
              ^io.input-link <il>
              ^status <st>)
   (<il> ^health <= <hc>)
   -->
   (<st> ^health-level critical)}

sp {elaborate*status*health*low
   (state <s> ^name tanksoar
              ^parameters <p>
              ^io.input-link <il>
              ^status <st>)
   (<p> ^health-critical <hc>
        ^health-low <hl>)
   (<il> ^health > <hc>
         ^health <= <hl>)
   -->
   (<st> ^health-level low)}

sp {elaborate*status*health*high
   (state <s> ^name tanksoar
              ^parameters.health-low <hl>
              ^io.input-link <il>
              ^status <st>)
   (<il> ^health > <hl>)
-->
   (<st> ^health-level high)}



################################################################
### Missile status - critical/low/high
################################################################
sp {elaborate*status*missiles*critical
   (state <s> ^name tanksoar
              ^parameters.missiles-critical <mc>
              ^io.input-link <il>
              ^status <st>)
   (<il> ^missiles <= <mc>)
   -->
   (<st> ^missiles-level critical)}

sp {elaborate*status*missiles*low
   (state <s> ^name tanksoar
              ^parameters <p>
              ^io.input-link.missiles <count>
              ^status <st>)
   (<p> ^missiles-critical < <count>
        ^missiles-low >= <count>)
   -->
   (<st> ^missiles-level low)}

sp {elaborate*status*missiles*high
   (state <s> ^name tanksoar
              ^parameters.missiles-low <ml>
              ^io.input-link <il>
              ^status <st>)
   (<il> ^missiles > <ml>)
-->
   (<st> ^missiles-level high)}





