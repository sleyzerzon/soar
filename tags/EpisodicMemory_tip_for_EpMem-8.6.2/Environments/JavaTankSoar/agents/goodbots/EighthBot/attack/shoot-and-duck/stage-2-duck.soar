echo "\nLoading attack/shoot-and-duck/stage-2-duck"


### duck behind a wall
sp {propose*shoot-and-duck*stage-two
   (state <s> ^name attack*shoot-and-duck
              ^io.input-link <il>
              ^status <st>
              ^string-to-integer <sti>)
   (<st> ^manuever.action duck
         ^line-of-fire <lf>
         ^enemy-sighted <es>)
   (<sti> ^<string> <dist>)   ## convert number to string for indexing
   (<es> ^distance <dist>
         ^direction forward
         ^position center)
   (<lf> -^<string>.<dir>.open)
   (<il> ^blocked.{ << left right >> <dir> } no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name shoot-and-duck-2-duck
        ^actions.move.direction <dir>)
}


### increment manuever state w/ o-support
sp {update*shoot-and-duck*stage-two
:o-support
(state <s> ^operator <o> 
           ^status <st>
           ^io.input-link.clock <ck>
           ^opposite-direction.<dir> <odir>)
(<o> ^name shoot-and-duck-2-duck
     ^actions.move.direction <dir>)
(<st> ^manuever <man2>)
(<man2> ^action duck
        ^delay <d>)
-->
   (<st> ^manuever <man>)
   (<man> ^action slide-back
          ^time (+ 1 <ck>)
          ^direction <odir>
          ^delay <d>)
}