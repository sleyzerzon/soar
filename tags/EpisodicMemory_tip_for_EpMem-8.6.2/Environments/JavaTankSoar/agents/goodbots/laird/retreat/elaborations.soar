echo "\nLoading retreat/elaborations.soar"

##################################
## Compute enemy direction from sound, radar, rwaves, or incoming
##################################

sp {elaborate*retreat*sound*direction
   (state <s> ^name retreat 
              ^sound.direction <direction>)
   -->
   (<s> ^direction <direction>)}

sp {elaborate*retreat*radar*front
   (state <s> ^name retreat 
              ^tank-detected yes)
   -->
   (<s> ^direction forward)}

sp {elaborate*retreat*incoming*direction
   (state <s> ^name << retreat wander chase >>
              ^io.input-link.incoming.<dir> yes)
   -->
   (<s> ^direction <dir>)}

sp {elaborate*retreat*rwaves*direction
   (state <s> ^name << retreat wander chase >>
              ^io.input-link.rwaves.<dir> yes)
   -->
   (<s> ^direction <dir>)}

sp {elaborate*retreat*radar*direction
   (state <s> ^name retreat 
              ^io.input-link.radar.tank.position { <dir> <> center })
   -->
   (<s> ^avoid-direction <dir>)}

