echo "\nLoading attack/move.soar "

###Propose Move Operator

## Slide over to atack and fire away - No obstacles

sp {attack*propose*move
   (state <s> ^name attack
              ^io.input-link <input>)
   (<input> ^blocked.<dir> no
            ^radar <r>)
   (<r> ^tank <t>
       -^tank.position center)
   (<t> ^position { << left right >> <dir> }
        ^distance { > 0 <d> })
 -{(<r> ^obstacle <ob>)          ;# no obstacle between tank and enemy
   (<ob> ^position <dir>
         ^distance < <d>)}
   -->
   (<s> ^operator <o> + =)
   (<o> ^name slide-to-attack
        ^actions <a>)
   (<a> ^fire.weapon missile
        ^move.direction <dir>)}

sp {attack*propose*stay-put
   (state <s> ^name attack
              ^io.input-link <input>)
   (<input> ^blocked.<dir> no
            ^blocked.forward no
            ^missiles > 0
            ^radar <r>)
   (<r> ^tank <t>
       -^tank.position center)
   (<t> ^position { << left right >> <dir> }
        ^distance 1)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name stay-put-fire
        ^actions.fire.weapon missile)}

## In front, but no place to slide to if he attacks
## Move forward
sp {attack*propose*move*closer
   (state <s> ^name attack
            - ^missile-number none
              ^io.input-link <il>)
   (<il> ^radar <r>
         ^blocked <b>)
   (<b> ^left yes ^right yes)
   (<r> ^tank <t>)
   (<t> ^position center
        ^distance { > 1 <d> })   ;# Don't do this at corner
  -{(<r> ^obstacle <ob>)          ;# no obstacle between tank and enemy
    (<ob> ^position center
         ^distance < <d>)}
   -->
   (<s> ^operator <o> + =, >)   
   (<o> ^name move-fire-to-opening
        ^actions <a>)
   (<a> ^fire.weapon missile
        ^move.direction forward)}

####### Try with and without this ############
## Can't slide over - move closer but fire (in case he moves in)


sp {attack*propose*move*backward*lots
   (state <s> ^name attack
            - ^missile-number none
              ^io.input-link <il>)
   (<il> ^radar <r>
         ^blocked.backward no)
   (<r> ^tank <t>
       -^tank.position center)
   (<t> ^position { << left right >> <dir> }
        ^distance { > 1 <d> })   ;# Don't do this at corner
   (<r> ^obstacle <ob>)          ;# obstacle between tank and enemy
   (<ob> ^position <dir>
         ^distance < <d>)
 -{ (<r> ^open <op>)          ;# open between tank and enemy
   (<op> ^position <dir>
         ^distance < <d>)}
   -->
   (<s> ^operator <o> + =)
   (<o> ^name move-backward-fire
        ^actions <a>)
   (<a> ^fire.weapon missile
        ^move.direction backward)}

sp {attack*propose*move*closer*lots
   (state <s> ^name attack
            - ^missile-number none
              ^io.input-link <il>)
   (<il> ^radar <r>)
   (<r> ^tank <t>
       -^tank.position center)
   (<t> ^position { << left right >> <dir> }
        ^distance { > 1 <d> })   ;# Don't do this at corner
   (<r> ^obstacle <ob>)          ;# obstacle between tank and enemy
   (<ob> ^position <dir>
         ^distance < <d>)
   (<r> ^open <op>)          ;# open between tank and enemy
   (<op> ^position <dir>
         ^distance { <> 0 < <d> })
   -->
   (<s> ^operator <o> + =)
   (<o> ^name move-forward-fire
        ^actions <a>)
   (<a> ^fire.weapon missile
        ^move.direction forward)}

sp {attack*propose*stay*lots
   (state <s> ^name attack
              ^missile-number mongo
              ^io.input-link <il>)
   (<il> ^radar <r>
         ^random < .3
         ^missiles <m>)
   (<r> ^tank <t>
       -^tank.position center)
   (<t> ^position { << left right >> <dir> }
        ^distance { > 1 <d> })   ;# Don't do this at corner
   (<r> ^obstacle <ob>)          ;# obstacle between tank and enemy
   (<ob> ^position <dir>
         ^distance < <d>)
   (<r> ^open <op>)          ;# open between tank and enemy
   (<op> ^distance 0
        -^position center)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name wait-forward-fire
        ^actions <a>)
   (<a> ^fire.weapon missile)}

sp {attack*propose*stay*lots*no-fire
   (state <s> ^name attack
              ^io.input-link <il>
-^operator.name wait)
   (<il> ^radar <r>
         ^missiles <m>
         ^clock <ck>)    ;# Only wait one turn
   (<r> ^tank <t>
       -^tank.position center)
   (<t> ^position { << left right >> <dir> }
        ^distance { > 1 <d> })   ;# Don't do this at corner
   (<r> ^obstacle <ob>)          ;# obstacle between tank and enemy
   (<ob> ^position <dir>
         ^distance < <d>)
   (<r> ^open <op>)          ;# open between tank and enemy
   (<op> ^distance 0
        -^position center)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name wait)}
