# proposed by generic operator proposal in propose-mission-operator

# note: this operator will only work at the end of a circular mission list 
# (i.e. all other steps are already marked status complete)


sp {apply*reset-mission*create-reset-pointer
   (state <s> ^operator <op>
              ^mission <m>)
   (<op> ^name reset-mission)
   (<m> ^current <c>
       -^resetting)
   (<c> ^next <n>)
-->
   (<m> ^resetting <n>)
}

sp {apply*reset-mission*reset-step
   (state <s> ^operator <op>
              ^mission <m>)
   (<op> ^name reset-mission)
   (<m> ^resetting <r>)
   (<r> ^status complete
        ^next <n>)
-->
   (<r> ^status complete -)
   (<m> ^resetting <r> - <n>)
}

# we don't mark this as status complete because we want the missions to repeat forever
# so just advance the current pointer manually
sp {apply*reset-mission*complete
   (state <s> ^operator <op>
              ^mission <m>)
   (<op> ^name reset-mission)
   (<m> ^current <c>
        ^resetting <c>)
   (<c> ^next <n>)
-->
   (<m> ^resetting <c> -
        ^current <c> - <n>)
}