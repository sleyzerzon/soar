
####
### STATE SUCCESS/GOAL DETECTION
###

sp {mac*detect*state*success
   (state <s> ^desired <d>
              ^<bank> <ls>)
   (<ls> ^missionaries <m>
         ^cannibals <c>)
   (<d> ^{ << right-bank left-bank >> <bank> } <dls>)
   (<dls> ^missionaries <m>
          ^cannibals <c>)
   -->
   (<s> ^success <d>)
   (write (crlf) |Success!|)}

###
### STATE FAILURE DETECTION
###

# this rule will cause a warning during loading in Soar; that warning can be ignored
sp {mac*evaluate*state*failure*more*cannibals
   (state <s> ^desired <d>
              ^<< right-bank left-bank >> <bank>)
   (<bank> ^missionaries { <n> > 0 }
           ^cannibals > <n>)
   -->
   (write (crlf) |Failure!|)
   (<s> ^failure <d>)}
