rl --set learning on
rl --set learning-policy q-learning
rl --set learning-rate 0.1
rl --set discount-rate 0.9
#rl --set eligibility-trace-decay-rate 0.1
#rl --set eligibility-trace-tolerance 0.000001

indifferent-selection --boltzmann
indifferent-selection --temperature 0.01
#indifferent-selection --auto-reduce on
#indifferent-selection --reduction-rate temperature exponential 0.9999

sp {propose*initialize*cartpole
    (state <s> ^superstate nil)
   -{(<s> ^name cartpole)}
-->
    (<s> ^operator <o> + >)
    (<o> ^name cartpole)
}
sp {apply*initialize*cartpole
    (state <s> ^operator.name cartpole)
-->
    (<s> ^name cartpole
         ^div <d>)
    (<d> ^name default
         ^x (/ 10 8)
         ^x-dot (/ 1 0.5)
         ^theta (/ 3.1415926 16)
         ^theta-dot (/ 3.141526 2))
}
#sp {elaborate*additional*cartpole
#    (state <s> ^superstate nil
#               ^name cartpole)
#-->
#    (<s> ^name cartpole
#         ^div <d>)
#    (<d> ^x (/ 10 8)
#         ^x-dot (/ 1 0.5)
#         ^theta (/ 3.1415926 16)
#         ^theta-dot (/ 3.141526 2))
#}

sp {cartpole*elaborate*index*x*gt
    (state <s> ^name cartpole
               ^io.input-link.x {<val> >= 0}
               ^div <d>)
    (<d> ^x <div>)
-->
    (<d> ^index-x (int (/ <val> <div>)))
}
sp {cartpole*elaborate*index*x*lt
    (state <s> ^name cartpole
               ^io.input-link.x {<val> < 0}
               ^div <d>)
    (<d> ^x <div>)
-->
    (<d> ^index-x (int (- (/ <val> <div>) 1)))
}

sp {cartpole*elaborate*index*x-dot*gt
    (state <s> ^name cartpole
               ^io.input-link.x-dot {<val> >= 0}
               ^div <d>)
    (<d> ^x-dot <div>)
-->
    (<d> ^index-x-dot (int (/ <val> <div>)))
}
sp {cartpole*elaborate*index*x-dot*lt
    (state <s> ^name cartpole
               ^io.input-link.x-dot {<val> < 0}
               ^div <d>)
    (<d> ^x-dot <div>)
-->
    (<d> ^index-x-dot (int (- (/ <val> <div>) 1)))
}

sp {cartpole*elaborate*index*theta*gt
    (state <s> ^name cartpole
               ^io.input-link.theta {<val> >= 0}
               ^div <d>)
    (<d> ^theta <div>)
-->
    (<d> ^index-theta (int (/ <val> <div>)))
}
sp {cartpole*elaborate*index*theta*lt
    (state <s> ^name cartpole
               ^io.input-link.theta {<val> < 0}
               ^div <d>)
    (<d> ^theta <div>)
-->
    (<d> ^index-theta (int (- (/ <val> <div>) 1)))
}

sp {cartpole*elaborate*index*theta-dot*gt
    (state <s> ^name cartpole
               ^io.input-link.theta-dot {<val> >= 0}
               ^div <d>)
    (<d> ^theta-dot <div>)
-->
    (<d> ^index-theta-dot (int (/ <val> <div>)))
}
sp {cartpole*elaborate*index*theta-dot*lt
    (state <s> ^name cartpole
               ^io.input-link.theta-dot {<val> < 0}
               ^div <d>)
    (<d> ^theta-dot <div>)
-->
    (<d> ^index-theta-dot (int (- (/ <val> <div>) 1)))
}

gp {cartpole*propose*move
    (state <s> ^name cartpole
               ^io.input-link <il>)
    (<il> ^step <c>)
-->
    (<s> ^operator <o> +)
    (<o> ^name move
         ^step <c>
         ^direction [left right])
}

sp {cartpole*reinforce*move
    :template
    (state <s> ^name cartpole
               ^div <d>
               ^operator <o> +)
    (<d> ^name <name>
         ^index-x <x>
         ^index-x-dot <x-dot>
         ^index-theta <theta>
         ^index-theta-dot <theta-dot>)
    (<o> ^name move
         ^direction <dir>)
-->
    (<s> ^operator <o> = 0)
}

sp {cartpole*apply*move
    (state <s> ^name cartpole
               ^operator <o>
               ^io.output-link <ol>)
    (<o> ^name move
         ^step <c>
         ^direction <d>)
-->
    (<ol> ^move <m>)
    (<m> ^step <c>
         ^direction <d>)
}

sp {cartpole*remove*old-move
    (state <s> ^name cartpole
               ^operator <o>
               ^io.output-link <ol>)
    (<o> ^name move
         ^step <c>)
    (<ol> ^move <m>)
    (<m> ^step <> <c>)
-->
    (<ol> ^move <m> -)
}

sp {cartpole*propose*halt
    (state <s> ^name cartpole
               ^io.input-link.state terminal)
-->
    (<s> ^operator <o> + >)
    (<o> ^name halt)
}

sp {cartpole*apply*halt
    (state <s> ^name cartpole
               ^operator <o>)
    (<o> ^name halt)
-->
    (halt)
}

sp {elaborate*reward
    (state <s> ^name cartpole
               ^io.input-link.state terminal
               ^reward-link <rl>)
-->
    (<rl> ^reward.value -1)
}
