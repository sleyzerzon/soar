#!/usr/bin/python
# Project: Soar <http://soar.googlecode.com>
# Author: Jonathan Voigt <voigtjr@gmail.com>
#
import os, sys

Import('env', 'msvs_projs')
clone = env.Clone()

libs = {
	('darwin', '32') : 'cppunit-osx',
	('darwin', '64') : 'cppunit-osx-64',
	('linux2', '32') : 'cppunit-linux',
	('linux2', '64') : 'cppunit-linux-64',
	('win32',  '32') : 'cppunit_dllvs9',
	('win32',  '64') : 'cppunit64_dllvs9'
}

lib = libs[(sys.platform, GetOption('platform'))]
libfile = env.File(os.path.join('lib', env['SHLIBPREFIX'] + lib + env['SHLIBSUFFIX']))
clone.Prepend(LIBS = lib, LIBPATH = ['lib'], CPPPATH = env.Dir('src').srcnode())
src = Glob('src/*.cpp')
t = env.Install('$OUT_DIR', [clone.Program('UnitTests', src), libfile])

proj = env.MSVSProject(
	target = '#unittests' + env['MSVSPROJECTSUFFIX'],
	srcs = [ str(s) for s in src ],
	buildtarget = t,
	variant = ['Debug'],
	auto_build_solution = 0,
)

msvs_projs.append(proj)
Return('t')
