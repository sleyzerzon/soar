
sp {build-building*propose*set-cCenter-loc
   (state <s> ^name build-building
              ^io.input-link.groups <gs>
             -^top-state.build-building.cCenter)
   (<gs> ^group <g>)
   (<g> ^type controlCenter
        ^owner 0
        ^x-pos <x>
        ^y-pos <y>)
        
-->
   (<s> ^operator <o> + > =)
   (<o> ^name set-cCenter-loc
        ^x <x>
        ^y <y>)
}

sp {build-building*apply*set-cCenter-loc
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<o> ^name set-cCenter-loc
        ^x <x>
        ^y <y>)
-->
   (<ts> ^build-building.cCenter <cc>)
   (<cc> ^x <x>
         ^y <y>)
}

# this should keep looking at friendly units until a cCenter is seen
sp {build-building*propose*find-cCenter
   (state <s> ^name build-building
             -^top-state.build-building.cCenter)
-->
   (<s> ^operator <o> + =)
   (<o> ^name visual-search
        ^search-type friendly)
}

sp {build-building*propose*find-build-location
   (state <s> ^name build-building
              ^top-state.build-building.cCenter <cc>
             -^top-state.build-building.location
              ^superstate.operator.building-dim <dim>)
   (<cc> ^x <x>
         ^y <y>)
        
-->
   (<s> ^operator <o> + > =)
   (<o> ^name find-build-location
        ^x <x>
        ^y <y>
        ^width (+ <dim> 31 40)) # keep a gap of 40 cCenter->building
}

sp {apply*find-build-location
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<o> ^name find-build-location
        ^x <x>
        ^y <y>
        ^width <w>)
-->
   (<ts> ^build-building.location <l>)
   (<l> ^x (- <x> <w>)
        ^y <y>)
}

sp {build-building*propose*location-found
   (state <s> ^name build-building
              ^top-state.find-location-near.result <l>)
   (<l> ^x <x>
        ^y <y>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name barracks-location-found
        ^x <x>
        ^y <y>)
}

sp {build-building*apply*location-found
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<ts> ^find-location-near <fnn>)
   (<o> ^name barracks-location-found
        ^x <x>
        ^y <y>)
-->
   (<ts> ^build-building.location <l>)
   (<l> ^x <x>
        ^y <y>)
   (<ts> ^find-location-near <fnn> -)
}
   

sp {build-building*propose*find-worker
   (state <s> ^name build-building
              ^top-state <ts>)
   (<ts> ^build-building.location.x <x>
         ^build-building.location.y <y>
        -^build-building.worker-id
        -^find-free-worker.done true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name find-free-worker
        ^x <x>
        ^y <y>)
}

sp {build-building*propose*worker-found
   (state <s> ^name build-building
              ^top-state.find-free-worker <f>)
   (<f> ^done true
        ^worker <id>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name bb-worker-found
        ^id <id>)
}

sp {build-building*apply*worker-found
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<ts> ^find-free-worker <ffw>)
   (<o> ^name bb-worker-found
        ^id <id>)
-->
   (<ts> ^build-building.worker-id <id>
         ^find-free-worker <ffw> -)
}

# worker was identified, but we lost him
# if this happens a lot, remember the location and look back there

#must be prefered to start-build
sp {build-building*propose*find-lost-worker
   (state <s> ^name build-building
              ^top-state <ts>
              ^io.input-link <il>)
   (<ts> ^build-building.worker-id <id>
         ^build-building.location.x <x>
         ^build-building.location.y <y>
        -^build-building.started-build true
        -^visual-search done)
   (<il> ^groups <gs>)
   -{(<gs> ^group <g>)
     (<g> ^type worker
          ^id <id>)}
-->
   (<s> ^operator <o> + > =)
   (<o> ^name visual-search
        ^worker-id <id>)
}

sp {build-building*propose*start-build
   (state <s> ^name build-building
              ^top-state <ts>
              ^superstate.operator.building-type <tp>)
   (<ts> ^build-building.worker-id <id>
         ^build-building.location.x <x>
         ^build-building.location.y <y>
        -^build-building.started-build true
        -^finished-command orts-build-building)
-->
    (<s> ^operator <o> +  =)
    (<o> ^name orts-build-building
         ^worker <id>
         ^x <x>
         ^y <y>
         ^building-type <tp>)
}

sp {build-building*propose*mark-started
   (state <s> ^name build-building
              ^top-state <ts>)
   (<ts> ^finished-command orts-build-building)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name build-building*mark-started)
}

sp {build-building*apply*mark-started
   (state <s> ^operator.name build-building*mark-started
              ^top-state <ts>)
   (<ts> ^finished-command orts-build-building
         ^build-building <bb>)
-->
   (<bb> ^started-build true)
   (<ts> ^finished-command orts-build-building -)
}