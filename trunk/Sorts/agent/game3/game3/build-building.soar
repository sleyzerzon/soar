sp {build-building*propose*set-cCenter-loc
   (state <s> ^name build-building
              ^io.input-link.groups <gs>
             -^top-state.build-building.cCenter)
   (<gs> ^group <g>)
   (<g> ^type controlCenter
        ^owner 0
        ^x-pos <x>
        ^y-pos <y>)
        
-->
   (<s> ^operator <o> + > =)
   (<o> ^name set-cCenter-loc
        ^x <x>
        ^y <y>)
}

sp {build-building*apply*set-cCenter-loc
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<o> ^name set-cCenter-loc
        ^x <x>
        ^y <y>)
-->
   (<ts> ^build-building.cCenter <cc>)
   (<cc> ^x <x>
         ^y <y>)
}

# this should keep looking at friendly units until a cCenter is seen
sp {build-building*propose*find-cCenter
   (state <s> ^name build-building
             -^top-state.build-building.cCenter)
-->
   (<s> ^operator <o> + =)
   (<o> ^name visual-search
        ^search-type friendly)
}

sp {build-building*propose*find-build-location
   (state <s> ^name build-building
              ^top-state.build-building.cCenter <cc>
             -^top-state.build-building.location
              ^superstate.operator.building-dim <dim>
              ^superstate.operator.building-type <t>
             -^top-state.finished-command orts-locate-building)
   (<cc> ^x <x>
         ^y <y>)
        
-->
   (<s> ^operator <o> + > =)
   (<o> ^name orts-locate-building
        ^x <x>
        ^y <y>
        ^distance (+ <dim> 31 40) # keep a gap of 40 cCenter->building
        ^building-type <t>) 
}


sp {build-building*propose*location-found
   (state <s> ^name build-building
              ^top-state.finished-command orts-locate-building
              ^io.input-link <il>)
   (<il> ^query-results <q>)
   (<q> ^query-name locate-building
        ^param0 <x>
        ^param1 <y>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name building-location-found
        ^x <x>
        ^y <y>)
}

sp {build-building*apply*location-found
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<ts> ^finished-command orts-locate-building)
   (<o> ^name building-location-found
        ^x <x>
        ^y <y>)
-->
   (<ts> ^build-building.location <l>)
   (<l> ^x <x>
        ^y <y>)
   (<ts> ^finished-command orts-locate-building -)
}
   

sp {build-building*propose*change-grouping-level
   (state <s> ^name build-building
              -^top-state.grouping-level individuals)
  -{(<gs> ^group <g>)
     (<g> ^type worker
          ^owner 0
          ^command_running 0
          ^num_members 1)}
-->
   (<s> ^operator <o> + > =)
   (<o> ^name set-grouping-individuals)
}

sp {build-building*propose*find-worker
   (state <s> ^name build-building
              ^top-state <ts>
              ^io.input-link <il>)
   (<ts> ^build-building.location.x <x>
         ^build-building.location.y <y>
        -^build-building.started-build true)
   (<il> ^groups <gs>)
   -{(<gs> ^group <g>)
     (<g> ^type worker
          ^owner 0
          ^command_running 0
          ^num_members 1)}
-->
   (<s> ^operator <o> + < =) # prefer changing group level
   (<o> ^name visual-search
        ^search-type friendly_workers)
}

sp {build-building*propose*start-build
   (state <s> ^name build-building
              ^top-state <ts>
              ^superstate.operator.building-type <tp>
              ^io.input-link <il>) 
   (<il> ^groups <gs>)
   (<gs> ^group <g>)
   (<g> ^type worker
        ^id <id>
        ^owner 0
        ^command_running 0
        ^num_members 1)
   (<ts> ^build-building.location.x <x>
         ^build-building.location.y <y>
        -^build-building.started-build true
        -^finished-command orts-build-building)
-->
    (<s> ^operator <o> +  =)
    (<o> ^name orts-build-building
         ^worker <id>
         ^x <x>
         ^y <y>
         ^building-type <tp>)
}

sp {build-building*propose*mark-started
   (state <s> ^name build-building
              ^top-state <ts>)
   (<ts> ^finished-command orts-build-building)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name build-building*mark-started)
}

sp {build-building*apply*mark-started
   (state <s> ^operator.name build-building*mark-started
              ^top-state <ts>)
   (<ts> ^finished-command orts-build-building
         ^build-building <bb>)
-->
   (<bb> ^started-build true)
   (<ts> ^finished-command orts-build-building -)
}