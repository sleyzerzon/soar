sp {scout*propose*send-position-message
   (state <s> ^name scout
              ^top-state.entities.entity <e>
              ^io.input-link.time.seconds <time>)
   (<e> ^id <id>
        ^next-update-message-time <= <time>
        ^data <d>)
   (<d> ^visible true
        ^friendly false)
-->
   (<s> ^operator <op> +)
   (<op> ^name send-position-message
         ^entity <e>
         ^time <time>)
}

sp {scout*compare*send-position-message
   (state <s> ^name scout
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name send-position-message)
   (<op2> ^name send-position-message)
-->
   (<s> ^operator <op1> = <op2>)
}


# message: entity-report position-update <id> <x> <y> <friendly>
sp {apply*send-position-message
   (state <s> ^operator <op>
              ^io.output-link <ol>)
   (<op> ^name send-position-message
         ^entity <e>
         ^time <time>)
   (<e> ^id <id>
        ^last-update-message-time <lmut>
        ^data <d>)
   (<d> ^friendly <friendly>
        ^current-absolute-location <al>)
   (<al> ^x <x> ^y <y>)
-->
   (<e> ^last-update-message-time <lmut> - <time>)
   (<ol> ^broadcast-message <w1>)
   (<w1> ^word entity-report
         ^next <w2>)
   (<w2> ^word position-update
         ^next <w3>)
   (<w3> ^word <id>
         ^next <w4>)
   (<w4> ^word <x>
         ^next <w5>)
   (<w5> ^word <y>
         ^next <w6>)
   (<w6> ^word <friendly>
         ^next nil)
}
