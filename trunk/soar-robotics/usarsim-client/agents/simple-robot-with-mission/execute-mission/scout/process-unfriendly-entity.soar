#TODO: if entity disappears, mark lost-contact-time and send message.  If don't see entity within some time after that, remove top-state structure
#      perhaps the entity should stay on the input-link with ^visible no for awhile (so agent doesn't need to track it itself)
#      PRO: less complex agent code CON: less flexible (environment code needs to be changed to alter timing, timing can't be based on knowledge)
#      can do both (so agent can continue to track it if it wants to)

sp {scout*propose*process-unfriendly-entity
   (state <s> ^name scout
              ^io.input-link.entities.entity <e>)
   (<e> ^friendly false)
-->
   (<s> ^operator <op> +)
   (<op> ^name process-unfriendly-entity
         ^entity <e>)
}

sp {scout*compare*process-unfriendly-entity
   (state <s> ^name scout
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name process-unfriendly-entity)
   (<op2> ^name process-unfriendly-entity)
-->
   (<s> ^operator <op1> = <op2>)
}

sp {apply*process-unfriendly-entity
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name process-unfriendly-entity
         ^entity.id <id>)
  -(<ts> ^entity.id <id>)
-->
   (<ts> ^entity <e-ts>)
   (<e-ts> ^id <id>
           ^last-message-update-time -1000) # init to negative time to force first message out right away
}
