# propose the action associated with each unblocked arc
sp {simple-robot*propose*action
   (state <s> ^name simple-robot
              ^io.input-link.ranges.range <r>
              ^io.input-link.time.microseconds <m>)
   (<r> ^blocked false
        ^action <a>
        ^ang-distance <d>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name action
         ^action <a>
         ^distance <d>)
}

# prefer actions associated with closer arcs
sp {simple-robot*compare*action
   (state <s> ^name simple-robot
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name action
          ^distance <d>)
   (<op2> ^name action
          ^distance > <d>)
-->
   (<s> ^operator <op1> > <op2>)
}

# if all arcs are blocked, propose turning left
sp {simple-robot*propose*turn-left
   (state <s> ^name simple-robot
              ^io.input-link.ranges <rs>
              ^io.input-link.time.microseconds <m>
              ^parameters.throttle <thr>)
  -(<rs> ^range.blocked false)
-->
   (<s> ^operator <op> + =)
   (<op> ^name action
         ^action <a>)
   (<a> ^rotate <params>)
   (<params> ^direction left
             ^throttle <thr>)
}

sp {apply*action
   (state <s> ^operator <op>
              ^io.output-link <ol>)
   (<op> ^name action
         ^action <a>)
   (<a> ^<action> <params>)
   (<params> ^direction <dir>
             ^throttle <thr>)
-->
   (<ol> ^<action> <acopy>)
   (<acopy> ^direction <dir>
            ^throttle <thr>)
}

sp {monitor*action
   (state <s> ^operator.action <a>)
   (<a> ^<action> <p>)
   (<p> ^direction <d>
        ^throttle <thr>)
-->
   (write (crlf) <action> | | <d> | at | <thr>)
}
