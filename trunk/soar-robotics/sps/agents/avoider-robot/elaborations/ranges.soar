sp {go-to-waypoint*elaborate*range*blocked
   (state <s> ^name simple-robot
              ^parameters.range-tolerance <rt>
              ^io.input-link.ranges.range <r>)
   (<r> ^distance <= <rt>)
-->
   (<r> ^blocked true)
}

sp {go-to-waypoint*elaborate*range*not-blocked
   (state <s> ^name simple-robot
              ^parameters.range-tolerance <rt>
              ^io.input-link.ranges.range <r>)
   (<r> ^distance > <rt>)
-->
   (<r> ^blocked false)
}

sp {go-to-waypoint*elaborate*range*move-forward
   (state <s> ^name simple-robot
              ^io.input-link.ranges.range <r>
              ^parameters.throttle <thr>
              ^io.input-link.self.pose.utime <utime>)
   (<r> ^id 0)
-->
   (<r> ^action <a>)
   (<a> ^move <m>)
   (<m> ^direction forward
        ^throttle <thr>
        ^utime <utime>)
}

sp {go-to-waypoint*elaborate*range*turn-left
   (state <s> ^name simple-robot
              ^io.input-link.ranges.range <r>
              ^parameters.throttle <thr>
              ^io.input-link.self.pose.utime <utime>)
   (<r> ^id > 0)
-->
   (<r> ^action <a>)
   (<a> ^rotate <m>)
   (<m> ^direction left
        ^throttle <thr>
        ^utime <utime>)
}

sp {go-to-waypoint*elaborate*range*rotate-right
   (state <s> ^name simple-robot
              ^io.input-link.ranges.range <r>
              ^parameters.throttle <thr>
              ^io.input-link.self.pose.utime <utime>)
   (<r> ^id < 0)
-->
   (<r> ^action <a>)
   (<a> ^rotate <m>)
   (<m> ^direction right
        ^throttle <thr>
        ^utime <utime>)
}

sp {go-to-waypoint*elaborate*range*dist-from-goal*0
   (state <s> ^name simple-robot
              ^io.input-link <il>)
   (<il> ^ranges.range <r>)
   (<r> ^start <= 0
        ^end >= 0)
-->
   (<r> ^ang-distance 0)
}

sp {go-to-waypoint*elaborate*range*dist-from-goal*left
   (state <s> ^name simple-robot
              ^io.input-link <il>)
   (<il> ^ranges.range <r>)
   (<r> ^start {<start> > 0})
-->
   (<r> ^ang-distance (- <start> 0) )
}

sp {go-to-waypoint*elaborate*range*dist-from-goal*right
   (state <s> ^name simple-robot
              ^io.input-link <il>)
   (<il> ^ranges.range <r>)
   (<r> ^end {<end> < 0})
-->
   (<r> ^ang-distance (- 0 <end>) )
}
