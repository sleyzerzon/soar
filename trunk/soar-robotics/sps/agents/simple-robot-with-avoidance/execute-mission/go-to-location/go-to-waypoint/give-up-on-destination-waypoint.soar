sp {go-to-waypoint*propose*give-up-on-destination-waypoint
   (state <s> ^name go-to-waypoint
              ^top-state.destination-waypoint.give-up true)
-->
   (<s> ^operator <op> +, =, >)
   (<op> ^name give-up-on-destination-waypoint
         ^destination-waypoint <mt>)
}

# if the give-up is marked as immediate, then make this better
# (giving up will be marked as immediate if the agent receives a message
#  that says its destination is unreachable from its source)
sp {go-to-waypoint*compare*give-up-on-destination-waypoint-better-than-update-progress
   (state <s> ^name go-to-waypoint
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name give-up-on-destination-waypoint
          ^destination-waypoint.give-up immediate)
   (<op2> ^name record-progress)
-->
   (<s> ^operator <op1> > <op2>)
}

# don't let low-level movement continue once we've decided to quit
sp {apply*give-up-on-destination-waypoint*stop
   (state <s> ^operator <op>
              ^io.output-link <ol>)
   (<op> ^name give-up-on-destination-waypoint)
-->
   (<ol> ^stop <x>)
}

# eliminate current destination waypoint
sp {apply*give-up-on-destination-waypoint
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name give-up-on-destination-waypoint
         ^destination-waypoint <mt>)
   (<ts> ^destination-waypoint <mt>)
-->
   (<ts> ^destination-waypoint <mt> -
         ^abandoned-waypoint <mt>)
}

# message: update-map remove-link <id1> <id2>
# (assumings links are two-way)
sp {apply*give-up-on-destination*update-map
   (state <s> ^operator <op>
              ^io.output-link <ol>)
   (<op> ^name give-up-on-destination-waypoint
         ^destination-waypoint <dw>)
   (<dw> ^source <source>
         ^waypoint <dest>)
   (<source> ^next <dest>
             ^id <id1>)
   (<dest> ^next <source>
           ^id <id2>)
-->
   (<source> ^next <dest> -)
   (<dest> ^next <source> -)
   (<ol> ^broadcast-message <w1>)
   (<w1> ^word update-map
         ^next <w2>)
   (<w2> ^word remove-link
         ^next <w3>)
   (<w3> ^word <id1>
         ^next <w4>)
   (<w4> ^word <id2>
         ^next nil)
   #(interrupt)
}
