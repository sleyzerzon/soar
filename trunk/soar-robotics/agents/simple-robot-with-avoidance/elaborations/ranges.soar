sp {go-to-waypoint*elaborate*range*blocked
   (state <s> ^name simple-robot
              ^parameters.range-tolerance <rt>
              ^io.input-link.ranges.range <r>)
   (<r> ^distance <= <rt>)
-->
   (<r> ^blocked true)
}

sp {go-to-waypoint*elaborate*range*not-blocked
   (state <s> ^name simple-robot
              ^parameters.range-tolerance <rt>
              ^io.input-link.ranges.range <r>)
   (<r> ^distance > <rt>)
-->
   (<r> ^blocked false)
}

sp {go-to-waypoint*elaborate*range*move-forward
   (state <s> ^name simple-robot
              ^io.input-link.ranges.range <r>)
   (<r> ^id 0)
-->
   (<r> ^action <a>)
   (<a> ^move <m>)
   (<m> ^direction forward
        ^throttle 1.0)
}

sp {go-to-waypoint*elaborate*range*turn-left
   (state <s> ^name simple-robot
              ^io.input-link.ranges.range <r>)
   (<r> ^id < 0)
-->
   (<r> ^action <a>)
   (<a> ^rotate <m>)
   (<m> ^direction left
        ^throttle 1.0)
}

sp {go-to-waypoint*elaborate*range*rotate-right
   (state <s> ^name simple-robot
              ^io.input-link.ranges.range <r>)
   (<r> ^id > 0)
-->
   (<r> ^action <a>)
   (<a> ^rotate <m>)
   (<m> ^direction right
        ^throttle 1.0)
}

sp {go-to-waypoint*elaborate*range*dist-from-goal*0
   (state <s> ^name simple-robot          
              ^io.input-link <il>)
   (<il> ^self.waypoints.waypoint <w>
         ^ranges.range <r>)
   (<w> ^id destination
        ^relative-bearing <rb>)
   (<r> ^start <= <rb>
        ^end >= <rb>)
-->
   (<r> ^distance 0)
}

sp {go-to-waypoint*elaborate*range*dist-from-goal*left
   (state <s> ^name simple-robot          
              ^io.input-link <il>)
   (<il> ^self.waypoints.waypoint <w>
         ^ranges.range <r>)
   (<w> ^id destination
        ^relative-bearing <rb>)
   (<r> ^start {<start> > <rb>})
-->
   (<r> ^distance (- <start> <rb>) )
}

sp {go-to-waypoint*elaborate*range*dist-from-goal*right
   (state <s> ^name simple-robot          
              ^io.input-link <il>)
   (<il> ^self.waypoints.waypoint <w>
         ^ranges.range <r>)
   (<w> ^id destination
        ^relative-bearing <rb>)
   (<r> ^end {<end> < <rb>})
-->
   (<r> ^distance (- <rb> <end>) )
}