sp {go-to-location*propose*go-to-waypoint
   (state <s> ^name go-to-location
              ^desired
              ^waypoints.at.next <w>)
            # -^top-state.move-to) # not already executing a move-to command
-->
   (<s> ^operator <op> +)
   (<op> ^name go-to-waypoint
         ^destination <w>
         ^track-progress true) #create a move-to structure on state
}

# 1) create waypoint structure on input-link
# 2) rotate-to to the desired heading 
# 3) create structures to track movement
# 4) move/rotate to the desired location (in subgoal)

# note that we don't have to explicitly remove any old "destination" waypoints as adding a new one will do a replace
sp {apply*go-to-waypoint*add-input-waypoint
   (state <s> ^operator <op>
              ^io.output-link <ol>)
   (<op> ^name go-to-waypoint
         ^destination <w>)
   (<w> ^x <x> ^y <y>)
-->
   (<ol> ^add-waypoint <aw>)
   (<aw> ^id destination
         ^x <x> ^y <y>)
}

sp {elaborate*operator*go-to-waypoint*waypoint
   (state <s> ^io.input-link.self.waypoints.waypoint <w>
              ^operator <o>)
   (<o> ^name go-to-waypoint
        ^destination <d>)
   (<w> ^id destination ^x <x> ^y <y>)
   (<d> ^x <x> ^y <y>)
-->
   (<o> ^waypoint <w>)
}

sp {apply*go-to-waypoint*create-move-to
   (state <s> ^operator <op>
              ^io.input-link <il>
              ^waypoints.at <at>
              ^top-state <ts>)
  -(<ts> ^move-to)
   (<il> ^time.seconds <time>
         ^self.waypoints.waypoint <w>)
   (<op> ^name go-to-waypoint
         ^move-now true
         ^track-progress true
         ^destination <d>
         ^waypoint <w>)
   (<w> ^id destination
        ^distance <dist>)
-->
   (<ts> ^move-to <mt>)
   (<mt> ^destination <d>
         ^source <at>
          ^closest <dist>
          ^time <time>)
}

sp {apply*go-to-waypoint*remove-move-to
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<ts> ^move-to <mt>)
   (<mt> ^destination <> <w>)
   (<op> ^name go-to-waypoint
         ^move-now true
         ^destination <w>)
-->
   (<ts> ^move-to <mt> -)
}

sp {monitor*go-to-waypoint
   (state <s> ^operator <o>)
   (<o> ^name go-to-waypoint
        ^destination <d>)
   (<d> ^x <x> ^y <y>)
-->
   (write (crlf) |Going to (| <x> |,| <y> |)|)
}
