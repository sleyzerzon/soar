# if we're close enough to a waypoint, then mark that we are "at" the waypoint
sp {go-to-location*propose*detect-waypoint
   (state <s> ^name << go-to-location localize >>
              ^waypoints <ws>
             -^top-state.current-waypoint <w>)
   (<ws> ^near-waypoint <w>)
-->
   (<s> ^operator <o> +, =, >)
 #  (interrupt)
   (<o> ^name detect-waypoint
        ^waypoint <w>)}

sp {go-to-location*apply*detect-waypoint
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<o> ^name detect-waypoint
        ^waypoint <w>)
-->
   (<ts> ^current-waypoint <w>)
}

sp {go-to-location*apply*detect-waypoint*remove
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<ts> ^current-waypoint <w>)
   (<o> ^name detect-waypoint
        ^waypoint <> <w>)
-->
   (<ts> ^current-waypoint <w> -)
}

sp {apply*detect-waypoint*arrived-at-destination*remove-destination-waypoint
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<ts> ^destination-waypoint <mt>)
   (<mt> ^waypoint <w>)
   (<op> ^name detect-waypoint
         ^waypoint <w>)
-->
   (<ts> ^destination-waypoint <mt> -
         ^last-completed-waypoint <mt>)
}