## If don't have any blocks in wm that need to be picked up - do a query to see if any exist
##

sp {get-block*propose*retrieve-blocks
   (state <s> ^name get-block
             -^retrieved-blocks yes
              ^top-state.parameters.objects-held-in smem
              ^current-location.cleaned yes
             -^top-state.blocks.block)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name retrieve-blocks)
}
## Test to see if already know block exists
sp {apply*retrieve-block*smem*query
   (state <s> ^name get-block
              ^smem.command <cmd>
              ^top-state.parameters.objects-held-in smem
              ^operator <op>)
   (<op> ^name retrieve-blocks)
-->
   (<cmd> ^query <q>)
   (<q> ^class block
        ^in-storage-room no)
}

sp {apply*retrieve-block*smem*retrieved
   (state <s> ^name get-block
              ^top-state.parameters.objects-held-in smem
              ^top-state.blocks <blocks>
              ^smem <smem>
              ^operator <op>)
   (<op> ^name retrieve-blocks)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^retrieved <cp>
          ^success <q>)
   (<q> ^class block)
-->
  #(write (crlf) |Retrieved object | <cp> )
   (<s> ^retrieved-blocks yes)
   (<blocks> ^block <cp>)}

sp {apply*record-block*return
   (state <s> ^operator <op>
              ^top-state.parameters.objects-held-in smem
              ^smem <smem>)
   (<op> ^name retrieve-blocks)
   (<smem> ^result.failure <q>
           ^command <cmd>)
   (<cmd> ^query <q>)
   (<q> ^class block)
-->
   (<s> ^retrieved-blocks yes)
}


#################################
## If don't have any blocks in wm that need to be picked up - do a query to see if any exist
##

sp {get-block*propose*retrieve-blocks*epmem
   (state <s> ^name get-block
             -^retrieved-blocks yes
              ^top-state.parameters.objects-held-in epmem
              ^current-location.cleaned yes
             -^top-state.blocks.block.in-storage-room no)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name retrieve-blocks)
}
## Test to see if already know block exists
sp {apply*retrieve-block*epmem*query*epmem
   (state <s> ^name get-block
              ^epmem.command <cmd>
              ^top-state.parameters.objects-held-in epmem
              ^operator <op>)
   (<op> ^name retrieve-blocks)
-->
   (<cmd> ^query <q>)
   (<q> ^blocks.block <block>)
   (<block> ^in-storage-room no)
}

sp {apply*retrieve-block*smem*query*not-in-storage-room
   (state <s> ^name get-block
              ^top-state.blocks.block <block>
              ^epmem.command.neg-query <ng>
              ^top-state.parameters.objects-held-in epmem
              ^operator <op>)
   (<block> ^in-storage-room yes
            ^id <id>)
   (<op> ^name retrieve-blocks)
-->
   (<ng> ^blocks.block <block1>)
   (<block1> ^id <id>
             ^in-storage-room no)
}

### (R915 ^cue-size 7 ^graph-match 0 ^match-cardinality -2 ^match-score -1.995
  ##     ^memory-id 516 ^normalized-match-score -1.995 ^present-id 517
    ##   ^retrieved R917 ^status success)
### Mark failure at top until see more blocks
##don't copy if already have these blocks!


sp {apply*retrieve-blocks*retrieve*epmem
   (state <s> ^name get-block
              ^top-state.parameters.objects-held-in epmem
              ^top-state.blocks <blocks>
              ^epmem <epmem>
              ^operator <op>)
   (<op> ^name retrieve-blocks)
   (<epmem> ^command <cmd>
            ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^retrieved <cp>
          ^success <q>)
   (<cp> ^blocks.block <block>)
-->
  #(write (crlf) |Retrieved object | <cp> )
   (<s> ^retrieved-blocks yes)
   (<blocks> ^block <block>)
   (<cmd> ^query <q> -)}

sp {apply*record-block*return*epmem
   (state <s> ^operator <op>
              ^top-state.parameters.objects-held-in epmem
              ^epmem <epmem>)
   (<op> ^name retrieve-blocks)
   (<epmem> ^result.failure <q>
            ^command <com>)
   (<com> ^query <q>)
-->
   (<s> ^retrieved-blocks yes)
   (<com> ^query <q> -)
}