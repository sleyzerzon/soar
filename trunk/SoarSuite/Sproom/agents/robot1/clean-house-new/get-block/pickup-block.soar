sp {get-block*propose*pickup-block
   (state <s> ^name get-block
             -^current-location.storage-area yes
              ^io.input-link <input>)
   (<input> ^self <self>
            ^objects.object <obj1>)
  -(<self> ^carry)
   (<obj1> ^type <> player
           ^id <oid>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name pickup-block
         ^id <oid>)
}

sp {get-block-hierarchical-pickup*compare*pickup-block
   (state <s> ^name get-block
              ^io.input-link.objects.object <obj1> { <> <obj1> <obj2> }
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name pickup-block
          ^id <id1>)
   (<op2> ^name pickup-block
          ^id <id2>)
   (<obj1> ^id <id1> 
           ^distance <range>)
   (<obj2> ^id <id2>
           ^distance > <range>)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {get-block*compare*go-to*prefer-over-go-to-seen-block
   (state <s> ^name get-block
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name pickup-block )
   (<op2> ^name find-block-in-area)
-->
   (<s> ^operator <op1> > <op2>)
}