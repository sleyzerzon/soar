#!/usr/bin/python
import glob
import shutil
import SoarSCons

Import('env')
env = env.Copy()

SoarSCons.SetJavaPaths(env, 'Core/ClientSMLSWIG/Java/sml.jar:SoarLibrary/bin/swt.jar')

javacStep = env.Java(target = 'classes', source = '.')

copySources = [
	       'JarDescription.jardesc',
	       'SoarDebugger.plist',
	       'debugger.html',
	       'default-text.dlf',
	       'default-layout.dlf',
	      ]
copySources.extend(glob.glob('images/*'))
copySources.extend(glob.glob('altimages/*'))

copyTargets = map(lambda x: 'classes/' + x, copySources)

copyStep = env.Command(copyTargets, copySources, SoarSCons.DoCopy)
jarStep = env.Command('#Tools/SoarJavaDebugger/SoarJavaDebugger.jar', [copySources, 'classes'], 'jar cfm Tools/SoarJavaDebugger/SoarJavaDebugger.jar Tools/SoarJavaDebugger/JarManifest -C Tools/SoarJavaDebugger/classes .')

env.Depends(jarStep, copyStep)
env.Depends(copyStep, javacStep)

env.Install('#SoarLibrary/bin', '#Tools/SoarJavaDebugger/SoarJavaDebugger.jar')

env.Clean('classes', 'classes')
