# in the selection space, if there are two available operators, this can happen:
# - operator A tried, depth-exceeded
# - operator B tried, failure
# tie resolves: operator A is the only acceptable one
# - operator A tried again, depth-exceeded again

# This is inefficient, and messes up the prune-alternate-paths code 
# (since it stores the locations visited the first time A was tried, and sees them again when it retries)
# Avoid the problem by annotating the operator the first time it is depth-exceeded


sp {exceed-depth*mark-original-operator
   (state <s> ^operator.name exceed-depth
              ^tried-tied-operator <o>
              ^look-ahead-operator <o>)
   (<o> ^duplicate-of <d>)
-->
   (<d> ^is-depth-exceeded true)
}

sp {game*shortcut*depth-exceeded
   (state <s> ^name game
              ^operator <o>
              ^desired <d>)
   (<o> ^name <any>
        ^is-depth-exceeded true)
-->
   (<s> ^depth-exceeded <d>)
}

sp {increment-ced*clear-de-shortcut
   (state <s> ^operator.name increment-current-evaluation-depth
              ^item <i>)
   (<i> ^is-depth-exceeded <ide>)
-->
   (<i> ^is-depth-exceeded <ide> -)
}