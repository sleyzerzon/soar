### Mark top-selection-space
sp {selection*elaboration*top-selection
   (state <s> ^name selection
             -^superstate.superstate.name selection)
-->
   (<s> ^top-selection true)
}
sp {selection*elaboration*not-top-selection
   (state <s> ^name selection
              ^superstate.superstate.name selection)
-->
   (<s> ^top-selection false)
}


# The remaining-operators value must be initialized to the number of tied operators.
# Two ways of doing this are supported-- either using an instance counter, or by using a version of Soar
# that provides item-count directly. Both are supported, although using the instance counter results in chunking
# over local negations.

sp {selection*elaborate*instance-counter
   (state <s> ^name selection
             -^item-count <x>) # only use this if Soar isn't providing item-count directly
-->
   (<s> ^instance-counter <ic>)
}

sp {selection*elaborate*add-to-ic*two-attribute
   (state <s> ^name selection
              ^item <it>
             -^evaluate-reject <it>
              ^instance-counter <ic>)
   (<it> ^name <n>
         ^p1 <p1>
         ^p2 <p2>) # this prevents chunks that specify only that N operators of a given name are present,
# instead the chunks need to be a bit overspecific, since there is a huge number of matches otherwise
-->
   (<ic> ^include <it>)
}

sp {selection*elaborate*add-to-ic*one-attribute
   (state <s> ^name selection
              ^item <it>
             -^evaluate-reject <it>
              ^instance-counter <ic>)
   (<it> ^name <n>
         ^p1 <p1>
        -^p2 <p2>) # this prevents chunks that specify only that N operators of a given name are present,
# instead the chunks need to be a bit overspecific, since there is a huge number of matches otherwise
-->
   (<ic> ^include <it>)
}

sp {selection*elaborate*add-to-ic*no-substructure
   (state <s> ^name selection
              ^item <it>
             -^evaluate-reject <it>
              ^instance-counter <ic>)
   (<it> ^name <n>
         -^p1 <p1>
         -^p2 <p2>)
-->
   (<ic> ^include <it>)
}

sp {selection*elaborate*tied-operator-count
   (state <s> ^name selection
              ^instance-counter <ic>)
   (<ic> ^up-to-date true
         ^total <t>)
-->
   (<s> ^tied-operator-count <t>) # tied-operator-count is equivalent to item-count
}


sp {selection*compare*same-symbolic-evaluations-are-indifferent
   :default
   (state <s> ^name selection 
              ^superstate <ss>
              ^evaluation <e1>  { <> <e1> <e2> })
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x> 
         ^symbolic-value { <> partial-success <> depth-exceeded <v> }
         ^desired <d>)
   (<e2> ^superoperator <y> 
         ^symbolic-value <v>
         ^desired <d>)
   -->
   (<ss> ^operator <x> = <y>)}  


sp {selection*monitor*depth
   (state <s> ^name selection
              ^current-evaluation-depth <ced>
              ^remaining-operators <ro>)
-->
   (write (crlf) |Depth: | <ced> |, | <ro>)
}
