sp {cluster-attack-tanks*elaborate*set-cluster-radius-first
   (state <s> ^name cluster-attack-tanks
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name set-cluster-radius)
   (<o2> ^name <> set-cluster-radius)
-->
   (<s> ^operator <o1> > <o2>)}

sp {cluster-attack-tanks*elaborate*free-tanks-first
   (state <s> ^name cluster-attack-tanks
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name free-tanks)
   (<o2> ^name move-tanks)
-->
   (<s> ^operator <o1> > <o2>)}

sp {cluster-attack-tanks*elaborate*tanks-clustered
   (state <s> ^name cluster-attack-tanks
              ^my-pid <mpid>
              ^cluster-radius <cr>
              ^io.input-link <in>)
   (<in> ^groups <gs>
         ^vision-state.grouping-radius <cr>)
   (<gs> ^group <g>)
   (<g> ^owner <mpid>
        ^type tank)
  -{(<gs> ^group { <> <g> <g1> })
    (<g1> ^owner <mpid>
          ^type tank)}
-->
   (<s> ^tanks-clustered t)}
   