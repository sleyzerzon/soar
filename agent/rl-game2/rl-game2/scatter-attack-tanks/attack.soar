sp {scatter-attack-tanks*propose*attack
   (state <s> ^name scatter-attack-tanks
              ^my-pid <mpid>
              ^enemy-pid <epid>
              ^io.input-link.groups <gs>)
   (<gs> ^group <my-tanks>
         ^group <enemy-tanks>)
   (<my-tanks> ^owner <mpid>
               ^type tank
               ^id <mtid>)
   (<enemy-tanks> ^owner <epid>
                  ^type tank
                  ^id <etid>)
   (<s> ^distance-measure <dm>)
   (<dm> ^my-tank-id <mtid>
         ^enemy-tank-id <etid>
         ^distance <d>)
  -{(<gs> ^group <closer-target>)
    (<closer-target> ^owner <epid>
                     ^type tank
                     ^id <ctid>)
    (<s> ^distance-measure <dm1>)
    (<dm1> ^my-tank-id <mtid>
           ^enemy-tank-id <ctid>
           ^distance < <d>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name attack
        ^my-tanks <my-tanks>
        ^enemy-tanks <enemy-tanks>)}

sp {scatter-attack-tanks*apply*attack
   (state <s> ^name scatter-attack-tanks
              ^io.output-link.sorts <cmd>
              ^operator <o>)
   (<o> ^name attack
        ^my-tanks <my-tanks>
        ^enemy-tanks <enemy-tanks>)
   (<my-tanks> ^id <mtid>)
   (<enemy-tanks> ^id <etid>)
-->
   (<cmd> ^attack <a>)
   (<a> ^group0 <mtid>
        ^group1 <etid>)}