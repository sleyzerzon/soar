sp {apply*enable-owner-grouping
   (state <s> ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name enable-owner-grouping
        ^task-id <tid>)
-->
   (<ol> ^command <c>)
   (<c> ^name enable-owner-grouping
        ^task-id <tid>)
}

sp {apply*look-at-feature
   (state <s> ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name look-at-feature
        ^feature <feature>
        ^sector <sector>
        ^task-id <tid>)
-->
   (<ol> ^command <lf>)
   (<lf> ^feature <feature>
         ^sector <sector>
         ^name look-feature
         ^task-id <tid>)
}
sp {apply*move-to-feature
   (state <s> ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name move-to-feature
        ^feature <feature>
        ^sector <sector>
        ^task-id <tid>)
-->
   (<ol> ^command <lf>)
   (<lf> ^feature <feature>
         ^sector <sector>
         ^name move-feature
         ^task-id <tid>)
}

sp {apply*change-view-width
   (state <s> ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name change-view-width
        ^width <w>
        ^task-id <tid>)
-->
   (<ol> ^command <vw>)
   (<vw> ^value <w>
         ^name view-width
         ^task-id <tid>)
}

# these should never apply if one is pending! make sure the proposals reflect this

sp {apply*set-grouping-individuals
   (state <s> ^operator <o>
              ^io.output-link <ol>
              ^top-state <ts>)
   (<o> ^name set-grouping-individuals
        ^task-id <tid>)
   (<ts> ^grouping-level {<gl> <> change-pending})
-->
   (<ts> ^grouping-level change-pending
         ^pending-grouping-level individuals)
   (<ts> ^grouping-level <gl> -)
   (<ol> ^command <g>)
   (<g> ^value 1
        ^name grouping-radius
        ^task-id <tid>)
}

sp {apply*set-grouping-small-groups
   (state <s> ^operator <o>
              ^io.output-link <ol>
              ^top-state <ts>)
   (<o> ^name set-grouping-small-groups
        ^task-id <tid>)
   (<ts> ^grouping-level {<gl> <> change-pending})
-->
   (<ts> ^grouping-level change-pending
         ^pending-grouping-level small-groups)
   (<ts> ^grouping-level <gl> -)
   (<ol> ^command <g>)
   (<g> ^value 32
        ^name grouping-radius
        ^task-id <tid>)
}

sp {apply*set-grouping-large-groups
   (state <s> ^operator <o>
              ^io.output-link <ol>
              ^top-state <ts>)
   (<o> ^name set-grouping-large-groups
        ^task-id <tid>)
   (<ts> ^grouping-level {<gl> <> change-pending})
-->
   (<ts> ^grouping-level change-pending
         ^pending-grouping-level large-groups)
   (<ts> ^grouping-level <gl> -)
   (<ol> ^command <g>)
   (<g> ^value 64
        ^name grouping-radius
        ^task-id <tid>)
}

sp {apply*set-grouping-huge-groups
   (state <s> ^operator <o>
              ^io.output-link <ol>
              ^top-state <ts>)
   (<o> ^name set-grouping-huge-groups
        ^task-id <tid>)
   (<ts> ^grouping-level {<gl> <> change-pending})
-->
   (<ts> ^grouping-level <gl> -)
   (<ts> ^grouping-level change-pending
         ^pending-grouping-level huge-groups)
   (<ol> ^command <g>)
   (<g> ^value 9999
        ^name grouping-radius
        ^task-id <tid>)
}

sp {propose*clean-grouping-command
   (state <s> ^io.output-link <ol>
              ^top-state <ts>
              ^task-id <tid>)
   (<ol> ^command <c>)
   (<c> ^name grouping-radius
        ^status complete
        ^task-id <tid>)
   (<ts> ^grouping-level change-pending
         ^pending-grouping-level <pgl>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name clean-grouping-command)
}

sp {apply*clean-grouping-command
   (state <s> ^operator <o>
              ^io.output-link <ol>
              ^top-state <ts>)
   (<o> ^name clean-grouping-command)
   (<ol> ^command <c>)
   (<c> ^name grouping-radius
        ^status complete)
   (<ts> ^grouping-level change-pending
         ^pending-grouping-level <pgl>)
-->
   (<ol> ^command <c> -)
   (<ts> ^grouping-level change-pending -)
   (<ts> ^pending-grouping-level <pgl> -)
   (<ts> ^grouping-level <pgl>)
}

sp {apply*look-at-location
   (state <s> ^operator <o>
              ^io.output-link <ol>
              ^top-state <ts>)
   (<o> ^name look-at-location
        ^x <x>
        ^y <y>
        ^task-id <tid>)
-->
   (<ol> ^command <g>)
   (<g> ^name look-location
        ^x <x>
        ^y <y>
        ^task-id <tid>)
}

