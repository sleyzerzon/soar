# PARAMETERS

# areas-held-in
# default-storage-area-id
# delete-old-areas
# look-ahead-planning
# method-ecological-doors
# method-ecological-entry
# mission
# objects-held-in
# search-control-go-to-gateway
# tasks-held-in

# TASKS

smem --add {
   (<task01> ^type task
             ^object <object01>
             ^destination <dest01>)
   (<object01> ^color green
               ^type block)
   (<dest01> ^id 3)
}

sp {interrupt
   (state <s> ^operator.name record-area-wm
              ^io.input-link.area-description.id 15)
-->
   #(interrupt)
}

sp {delivery*elaborate*tasks
   (state <s> ^name delivery
              ^tasks <tasks>)
-->
   (<tasks> ^task <task01>)
   (<task01> ^type task
             ^object <object01>
             ^destination <dest01>)
   (<object01> ^color green
               ^type block)
   (<dest01> ^id 3)
}

# DELIVERY PROBLEM SPACE

sp {propose*init-agent
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-agent)
}

sp {apply*init-agent
   (state <s> ^operator.name init-agent)
-->
   (<s> ^name delivery
        ^tasks <tasks>
        ^areas <areas>
        ^objects <objects>)
}

sp {delivery*elaborate*parameters
   (state <s> ^name delivery)
-->
   (<s> ^parameters <params>)
   (<params> ^distance-tolerance 1.0
             ^heading-tolerance 1.5
             ^linear-velocity 0.7
             ^storage-delta-x 1
             ^storage-delta-y 1)
}

sp {delivery*elaborate*constants
   (state <s> ^name delivery
              ^parameters <params>)
   (<params> ^distance-tolerance <distance-tol>)
-->
   (<s> ^constants <constants>)
   (<constants> ^gateway-deltas <deltas>
                ^direction-opposites <opposites>
                ^operator-preferences <preferences>)
   (<deltas> ^north <delta-north>
             ^south <delta-south>
             ^east <delta-east>
             ^west <delta-west>)
   (<delta-north> ^x 0.0
                  ^y (+ 0.5 <distance-tol>))
   (<delta-south> ^x 0.0
                  ^y (- -0.5 <distance-tol>))
   (<delta-east> ^x (+ 0.5 <distance-tol>)
                 ^y 0.0)
   (<delta-west> ^x (- -0.5 <distance-tol>)
                 ^y 0.0)
   (<opposites> ^north south
                ^south north
                ^east west
                ^west east)
   (<preferences> ^record 1
                  ^task 2
                  ^movement 3)
}

# ELABORATIONS

# default

sp {elaborate*topstate*topstate
   (state <s> ^superstate nil)
-->
   (<s> ^topstate <s>)
}

sp {elaborate*state*inherited*defaults
   (state <s> ^superstate.{<attr> << io topstate >>} <val>)
-->
   (<s> ^<attr> <val>)
}

sp {elaborate*state*state-name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

# domain-specific inherited attributes

sp {elaborate*state*inherited*roomsworld
   (state <s> ^superstate.{<attr> << parameters constants areas objects tasks >>} <val>)

-->
   (<s> ^<attr> <val>)
}

# navigation

sp {elaborate*state*current-area
   (state <s> ^io.input-link.area-description.id <id>
              ^areas.area <area>)
   (<area> ^id <id>
           ^stored yes)
-->
   (<s> ^current-area <area>)
}

sp {elaboroate*state*waypoint
   (state <s> ^name <name>
              ^io.input-link.waypoints.waypoint <waypoint>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
   (<waypoint> ^id <name>
               ^x <x>
               ^y <y>)
-->
   (<destination> ^waypoint <waypoint>)
}

sp {elaborate*state*arrived
   (state <s> ^name <name>
              ^parameters.distance-tolerance <distance-tol>
              ^destination.waypoint.distance {<= <distance-tol>})
-->
   (<s> ^arrived yes)
}

# PREFERENCES

sp {elaborate*operator*record
   (state <s> ^operator <o> +)
   (<o> ^name << record-area-wm >>)
-->
   (<o> ^type record)
}

sp {elaborate*operator*task
   (state <s> ^operator <o> +)
   (<o> ^name << perform-pick-up-task pick-up-object put-down-object >>)
-->
   (<o> ^type task)
}

sp {elaborate*operator*movement
   (state <s> ^operator <o> +)
   (<o> ^name << patrol go-to-adjacent-area go-to-coord turn-to-coord drive-to-coord >>)
-->
   (<o> ^type movement)
}

sp {delivery*compare*operator-types
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^constants.operator-preferences <preferences>)
   (<o1> ^type <o1-type>)
   (<o2> ^type <o2-type>)
   (<preferences> ^<o1-type> <o1-rank>
                  ^<o2-type> {<o2-rank> > <o1-rank>})
-->
   (<s> ^operator <o1> > <o2>)
}

# OPERATORS

# perform-pick-up-task

sp {delivery*propose*perform-pick-up-task
   (state <s> ^name delivery
              ^io.input-link.objects.object <obj>
              ^tasks.task <task>)
   (<task> ^object <desc>
          -^object-id)
   (<obj> ^id <id>)
   # check that the object has not been forgotten (FIXME use forgotten metadata)
   # check that the object is right for the task
  -{ (<desc> ^<attr> <val>)
    -(<obj> ^<attr> <val>) }
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-pick-up-task
        ^task <task>
        ^object <obj>)
}

sp {delivery*apply*perform-pick-up-task
   (state <s> ^name delivery
              ^operator <op>)
   (<op> ^name perform-pick-up-task
         ^task <task>
         ^object.id <id>)
   (<task> -^object-id)
-->
   (<task> ^object-id <id>)
}

sp {perform-pick-up-task*propose*pick-up-object
   (state <s> ^name perform-pick-up-task
              ^superstate.operator <so>)
   (<so> ^task <task>
         ^object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name pick-up-object
        ^object <obj>)
}


# perform-put-down-task

sp {delivery*propose*perform-put-down-task
   (state <s> ^name delivery
              ^io.input-link.self.carry.object <obj>
              ^top-state.current-area <area>
              ^tasks.task <task>)
   (<task> ^object-id <obj-id>
           ^destination <dest>)
   (<obj> ^id <obj-id>)
   # check that the destination has not been forgotten (FIXME use forgotten metadata)
   # check that we are in the right location
  -{ (<dest> ^<attr> <val>)
    -(<area> ^<attr> <val>) }
-->
   (<s> ^operator <op> +)
   (<op> ^name perform-put-down-task
         ^object <obj>)
}

sp {perform-put-down-task*propose*put-down-object
   (state <s> ^name perform-put-down-task
              ^superstate.operator.object <obj>)
-->
   (<s> ^operator <op> +)
   (<op> ^name put-down-object
         ^object <obj>)
}

# record-area-wm

sp {delivery*propose*record-area-wm
   (state <s> ^name delivery
             -^current-area
              ^parameters.areas-held-in wm)
-->
   (<s> ^operator <o> +)
   (<o> ^name record-area-wm)
}

sp {record-area-wm*propose*create-neighbor-area
   (state <s> ^name record-area-wm
              ^io.input-link.area-description <description>
             -^areas.area.id <to>)
   (<description> ^id <id>
                  ^gateway.to {<to> <> <id>})
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-neighbor-area
        ^area-id <to>)
}

sp {record-area-wm*apply*create-neighbor-area
   (state <s> ^name record-area-wm
              ^operator <o>
              ^areas <areas>)
   (<o> ^name create-neighbor-area
        ^area-id <id>)
-->
   (<areas> ^area.id <id>)
}

sp {record-area-wm*propose*copy-curent-area
   (state <s> ^name record-area-wm
              ^io.input-link.area-description <description>
              ^areas <areas>)
   (<description> ^id <id>)
  -{ (<description> ^gateway.to {<to> <> <id>})
    -(<areas> ^area.id <to>) }
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-current-area)
}

sp {record-area-wm*apply*copy-current-area*create-area
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description.id <id>
              ^areas <areas>)
  -(<areas> ^area.id <id>)
-->
   (<areas> ^area <area>)
   (<area> ^id <id>)
}

sp {record-area-wm*apply*copy-current-area*link-area
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^areas.area <area>
              ^io.input-link.area-description <description>)
   (<area> ^id <id>)
   (<description> ^id <id>
                  ^type <type>
                  ^wall <north-wall>
                  ^wall <south-wall>
                  ^wall <east-wall>
                  ^wall <west-wall>)
   (<north-wall> ^direction north
                 ^y <north-y>)
   (<south-wall> ^direction south
                 ^y <south-y>)
   (<east-wall> ^direction east
                ^x <east-x>)
   (<west-wall> ^direction west
                ^x <west-x>)
-->
   (<s> ^area <area>)
   (<area> ^type <type>
           ^class area
           ^center-x (/ (+ <east-x> <west-x>) 2)
           ^center-y (/ (+ <north-y> <south-y>) 2)
           ^height (- <north-y> <south-y>)
           ^width (- <east-x> <west-x>))
}

sp {record-area-wm*apply*copy-current-area*gateway
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description.gateway <cgateway>
              ^areas.area <to-area>
              ^area <area>
              ^constants.direction-opposites.<direction> <opposite>)
   (<area> ^id <id>
          -^<direction>.id <gid>)
   (<to-area> ^id <to>)
   (<cgateway> ^id <gid>
               ^to {<to> <> <id>}
               ^direction <direction>
               ^x <x>
               ^y <y>)
-->
   (<area> ^<direction> <gateway>)
   (<to-area> ^<opposite> <gateway>)
   (<gateway> ^id <gid>
              ^class coord
              ^type gateway
              ^to <area>
              ^to <to-area>
              ^x <x>
              ^y <y>)
}

sp {record-area-wm*apply*copy-current-area*done
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description <description>
              ^area <area>)
   (<area> ^id <id>)
  -{ (<description> ^gateway <gateway>)
     (<gateway> ^to {<to> <> <id>}
                ^direction <direction>)
    -(<area> ^<direction>.to.id <to>) }
-->
   (<area> ^stored yes)
}

# pick-up-object

sp {pick-up-object*elaborate*destination
   (state <s> ^name pick-up-object
              ^superstate.operator.object <obj>)
   (<obj> ^x <x>
          ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {pick-up-object*propose*go-to-object
   (state <s> ^name pick-up-object
              ^destination.waypoint <waypoint>
             -^arrived)
   (<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x <x>
        ^y <y>)
}

sp {pick-up-object*propose*get-object
   (state <s> ^name pick-up-object
              ^arrived)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-object
        ^actions.stop <stop>)
}

sp {pick-up-object*apply*get-object
   (state <s> ^name pick-up-object
              ^operator.name get-object
              ^superstate.operator.object.id <id>
              ^io.output-link <ol>
              ^arrived)
-->
   (<ol> ^get-object.id <id>)
}

# put-down-object

sp {put-down-object*elaborate*destination
   (state <s> ^name put-down-object
              ^parameters <params>
              ^current-area <area>)
   (<params> ^storage-delta-x <dx>
             ^storage-delta-y <dy>)
   (<area> ^center-x <x>
           ^center-y <y>
           ^height <height>
           ^width <width>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x (+ (- <x> (/ <width> 2)) <dx>)
                  ^y (+ (- <y> (/ <height> 2)) <dy>))
}

sp {put-down-object*propose*go-to-storage-coord
   (state <s> ^name put-down-object
              ^destination.waypoint <waypoint>
             -^arrived)
   (<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x <x>
        ^y <y>)
}

sp {put-down-object*propose*drop-object
   (state <s> ^name put-down-object
              ^arrived)
-->
   (<s> ^operator <o> +)
   (<o> ^name drop-object
        ^actions.stop <stop>)
}

sp {put-down-object*apply*drop-object
   (state <s> ^name put-down-object
              ^operator.name get-object
              ^superstate.operator.object.id <id>
              ^io.output-link <ol>
              ^arrived)
-->
   (<ol> ^drop-object.id <id>)
}

# patrol

sp {delivery*propose*patrol
   (state <s> ^name delivery)
-->
   (<s> ^operator <o> +)
   (<o> ^name patrol
        ^area <a01> <a02> <a03> <a04> <a05> <a06> <a07> <a08> <a09> <a10> <a11> <a12>)
   (<a01> ^next <a02>
          ^id 0)
   (<a02> ^next <a03>
          ^id 5)
   (<a03> ^next <a04>
          ^id 10)
   (<a04> ^next <a05>
          ^id 15)
   (<a05> ^next <a06>
          ^id 20)
   (<a06> ^next <a07>
          ^id 25)
   (<a07> ^next <a08>
          ^id 30)
   (<a08> ^next <a09>
          ^id 35)
   (<a09> ^next <a10>
          ^id 40)
   (<a10> ^next <a11>
          ^id 45)
   (<a11> ^next <a12>
          ^id 50)
   (<a12> ^next <a01>
          ^id 55)
} 

sp {patrol*propose*go-to-adjacent-area
   (state <s> ^name patrol
              ^superstate.operator.area <area>
              ^current-area.id <id>)
   (<area> ^id <id>
           ^next <next-area>)
   (<next-area> ^id <next-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-adjacent-area
        ^area-id <next-id>)
}

# go-to-adjacent-area

sp {go-to-adjacent-area*elaborate*destination
   # TODO this can cause multiple destinations to be specified
   # if there are two gateways to the same room
   (state <s> ^name go-to-adjacent-area
              ^superstate.operator.area-id <next-id>
              ^current-area.<< north south east west >> <gateway>)
   (<gateway> ^to.id <next-id>
              ^x <x>
              ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {go-to-adjacent-area*propose*go-to-gateway
   (state <s> ^name go-to-adjacent-area
              ^destination.waypoint <waypoint>
             -^arrived)
   (<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x <x>
        ^y <y>)
}

sp {go-to-adjacent-area*propose*enter-area
   (state <s> ^name go-to-adjacent-area
              ^superstate.operator.area-id <next-id>
              ^arrived yes
              ^current-area.{<direction> << north south east west >>}.to.id <next-id>
              ^destination.waypoint <waypoint>
              ^constants.gateway-deltas.<direction> <deltas>)
   (<waypoint> ^x <x>
               ^y <y>)
   (<deltas> ^x <delta-x>
             ^y <delta-y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x (+ <x> <delta-x>)
        ^y (+ <y> <delta-y>))
}

# go-to-coord

sp {go-to-coord*elaborate*destination
   (state <s> ^name go-to-coord
              ^superstate.operator <so>)
   (<so> ^x <x>
         ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {go-to-coord*propose*turn-to-waypoint
   (state <s> ^name go-to-coord
              ^parameters.heading-tolerance <heading-tol>
              ^destination.waypoint <waypoint>)
   (<waypoint> ^yaw <yaw>
               ^abs-relative-bearing {> <heading-tol>})
-->
   (<s> ^operator <o> +)
   (<o> ^name turn-to-waypoint
        ^actions.set-heading.yaw <yaw>)
}

sp {go-to-coord*propose*drive-to-waypoint
   (state <s> ^name go-to-coord
              ^parameters <params>
              ^destination.waypoint <waypoint>)
   (<params> ^distance-tolerance <distance-tol>
             ^linear-velocity <velocity>
             ^heading-tolerance <heading-tol>)
   (<waypoint> ^abs-relative-bearing {<= <heading-tol>}
               ^distance {<remaining> > <distance-tol>})
-->
   (<s> ^operator <o> +)
   (<o> ^name drive-to-waypoint
        ^actions.set-linear-velocity.linear-velocity <velocity>)
}

# waypoints

sp {propose*add-waypoint
   (state <s> ^name <name>
             -^io.input-link.waypoints.waypoint.id <name>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-waypoint
        ^actions.add-waypoint <add-waypoint>)
   (<add-waypoint> ^id <name>
                   ^x <x>
                   ^y <y>)
}

sp {propose*remove-waypoint
   (state <s> ^name <name>
              ^io.input-link.waypoints.waypoint <waypoint>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
   (<waypoint> ^id <name>)
  -(<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name remove-waypoint
        ^actions.remove-waypoint.id <name>)
}

# output commands

sp {apply*operator*create-output
   (state <s> ^operator.actions.<attr> <val>
              ^io.output-link <ol>)
-->
   (<ol> ^<attr> <val>)
}

sp {apply*operator*remove-output
   (state <s> ^superstate nil
              ^operator <o>
              ^io.output-link <ol>)
   (<ol> ^<attr> <val>)
   (<val> ^status << interrupted complete error >>)
-->
   (<ol> ^<attr> <val> -)
}

# wait

sp {propose*wait
   (state <s> ^attribute state
              ^choices none
             -^operator.name wait)
-->
   (<s> ^operator <o> +)
   (<o> ^name wait)
}
