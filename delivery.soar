# PARAMETERS

# areas-held-in
# default-storage-area-id
# delete-old-areas
# look-ahead-planning
# method-ecological-doors
# method-ecological-entry
# mission
# objects-held-in
# search-control-go-to-gateway
# tasks-held-in

# TASKS

smem --add {
   (<task01> ^type task
             ^object <object01>
             ^destination <dest01>)
   (<object01> ^color green
               ^type block)
   (<dest01> ^id 35)
}

sp {interrupt
   (state <s> ^operator.name record-area-wm
              ^io.input-link.area-description.id 15)
-->
   #(interrupt)
}

# DELIVERY PROBLEM SPACE

sp {propose*init-agent
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-agent)
}

sp {apply*init-agent
   (state <s> ^operator.name init-agent)
-->
   (<s> ^name delivery
        ^areas <areas>
        ^ltis <ltis>
        ^objects <objects>
        ^tasks <tasks>)
}

sp {delivery*elaborate*parameters
   (state <s> ^name delivery)
-->
   (<s> ^parameters <params>)
   (<params> ^distance-tolerance 1.0
             ^heading-tolerance 1.5
             ^linear-velocity 0.7
             ^storage-delta-x 1
             ^storage-delta-y 1)
}

sp {delivery*elaborate*constants
   (state <s> ^name delivery
              ^parameters <params>)
   (<params> ^distance-tolerance <distance-tol>)
-->
   (<s> ^constants <constants>)
   (<constants> ^gateway-deltas <deltas>
                ^direction-opposites <opposites>
                ^operator-preferences <preferences>)
   (<deltas> ^north <delta-north>
             ^south <delta-south>
             ^east <delta-east>
             ^west <delta-west>)
   (<delta-north> ^x 0.0
                  ^y (+ 0.5 <distance-tol>))
   (<delta-south> ^x 0.0
                  ^y (- -0.5 <distance-tol>))
   (<delta-east> ^x (+ 0.5 <distance-tol>)
                 ^y 0.0)
   (<delta-west> ^x (- -0.5 <distance-tol>)
                 ^y 0.0)
   (<opposites> ^north south
                ^south north
                ^east west
                ^west east)
   (<preferences> ^initialization 10
                  ^record 20
                  ^retrieve 25
                  ^task 30
                  ^movement 40)
}

# ELABORATIONS

# default

sp {elaborate*topstate*topstate
   (state <s> ^superstate nil)
-->
   (<s> ^topstate <s>)
}

sp {elaborate*state*inherited*defaults
   (state <s> ^superstate.{<attr> << io topstate >>} <val>)
-->
   (<s> ^<attr> <val>)
}

sp {elaborate*state*state-name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

# domain-specific inherited attributes

sp {elaborate*state*inherited*roomsworld
   (state <s> ^superstate.{<attr> << parameters constants areas objects tasks >>} <val>)

-->
   (<s> ^<attr> <val>)
}

# memory

# TODO these rules will not work on LTIs with cyclic references
# or, for that matter, when we don't need the entire LTI tree

sp {elaborate*state*forgettables-base
   (state <s> ^superstate nil
              ^ltis <ltis>
              ^tasks.task <task>)
-->
   (<ltis> ^lti <task>)
}

sp {elaborate*state*forgettables-recurse
   (state <s> ^superstate nil
              ^ltis <ltis>)
   (<ltis> ^lti.<attr> {<ident> <=> <s>})
-->
   (<ltis> ^lti <ident>)
}

sp {elaborate*state*forgettables-status-base
   (state <s> ^superstate nil
              ^ltis.lti <lti>)
   # there is at least one non-identifier child
   # and no identifier children
   (<lti> ^{<attr> <> forgotten}
         -^<ident-attr> {<ident> <=> <s>})
-->
   (<lti> ^forgotten no)
}

sp {elaborate*state*forgettables-status-recurse
   (state <s> ^superstate nil
              ^ltis.lti <lti>)
   # there is an identifier child
   # and all identifier children have ^forgotten no
   (<lti> ^<attr> {<=> <s>})
  -{ (<lti> ^<ident-attr> {<ident> <=> <s>})
    -(<ident> ^forgotten no) }
-->
   (<lti> ^forgotten no)
}

# navigation

sp {elaborate*state*current-area
   (state <s> ^io.input-link.area-description.id <id>
              ^areas.area <area>)
   (<area> ^id <id>
           ^stored yes)
-->
   (<s> ^current-area <area>)
}

sp {elaboroate*state*waypoint
   (state <s> ^name <name>
              ^io.input-link.waypoints.waypoint <waypoint>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
   (<waypoint> ^id <name>
               ^x <x>
               ^y <y>)
-->
   (<destination> ^waypoint <waypoint>)
}

sp {elaborate*state*arrived
   (state <s> ^name <name>
              ^parameters.distance-tolerance <distance-tol>
              ^destination.waypoint.distance {<= <distance-tol>})
-->
   (<s> ^arrived yes)
}

# PREFERENCES

sp {elaborate*operator*initialization
   (state <s> ^operator <o> +)
   (<o> ^name << initialize-tasks >>)
-->
   (<o> ^type initialization)
}

sp {elaborate*operator*record
   (state <s> ^operator <o> +)
   (<o> ^name << record-area-wm mark-object-picked-up mark-task-completed >>)
-->
   (<o> ^type record)
}

sp {elaborate*operator*retrieve
   (state <s> ^operator <o> +)
   (<o> ^name << recursive-retrieve >>)
-->
   (<o> ^type retrieve)
}

sp {elaborate*operator*task
   (state <s> ^operator <o> +)
   (<o> ^name << perform-pick-up-task pick-up-object put-down-object >>)
-->
   (<o> ^type task)
}

sp {elaborate*operator*movement
   (state <s> ^operator <o> +)
   (<o> ^name << patrol go-to-adjacent-area go-to-coord turn-to-coord drive-to-coord >>)
-->
   (<o> ^type movement)
}

sp {delivery*compare*operator-types
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^constants.operator-preferences <preferences>)
   (<o1> ^type <o1-type>)
   (<o2> ^type <o2-type>)
   (<preferences> ^<o1-type> <o1-rank>
                  ^<o2-type> {<o2-rank> > <o1-rank>})
-->
   (<s> ^operator <o1> > <o2>)
}

# OPERATORS

# initialize-tasks

sp {delivery*propose*initialize-tasks
   (state <s> ^name delivery
             -^tasks-initialized
              ^tasks)
-->
   (<s> ^operator <op> +)
   (<op> ^name initialize-tasks)
}

sp {delivery*apply*initialize-tasks*first-query
   (state <s> ^operator.name initialize-tasks
              ^smem.command <scmd>)
-->
   (<scmd> ^query.type task)
}

sp {delivery*apply*initialize-tasks*next-query
   (state <s> ^operator.name initialize-tasks
              ^smem <smem>
              ^tasks <tasks>)
   (<smem> ^command <scmd>
           ^result <sres>)
   (<sres> ^success
           ^retrieved <task>)
-->
   (<tasks> ^task <task>)
   (<scmd> ^prohibit <task>)
}

sp {delivery*apply*initialize-tasks*done
   (state <s> ^operator.name initialize-tasks
              ^smem <smem>)
   (<smem> ^command <scmd>
           ^result.failure)
   (<scmd> ^{<cmd> << query prohibit >>} <arg>)
-->
   (<scmd> ^<cmd> <arg> -)
   (<s> ^tasks-initialized yes)
}

# store-task TODO

# recursive-retrieve

sp {delivery*propose*recursive-retrieve*ecological-doors
   (state <s> ^name delivery
              ^parameters <params>
              ^current-area.type door
              ^tasks.task <task>)
   (<params> ^tasks-held-in smem
             ^method-ecological-doors yes)
   (<task> -^forgotten no)
-->
   (<s> ^operator <o> +)
   (<o> ^name recursive-retrieve
        ^lti <task>)
}

sp {delivery*propose*recursive-retrieve*ecological-entry
   # TODO this rule will fire anytime the agent is in a room
   # need to be more discriminatory than this
   (state <s> ^name delivery
              ^parameters <params>
              ^current-area.type room
              ^tasks.task <task>)
   (<params> ^tasks-held-in smem
             ^method-ecological-entry yes)
   (<task> -^forgotten no)
-->
   (<s> ^operator <op>)
   (<op> ^name recursive-retrieve
         ^task <task>)
}

sp {recursive-retrieve*elaborate*deeper
   (state <s> ^name recursive-retrieve
              ^to-be-retrieved <to-be-retrieved>
              ^depth <depth>)
   (<to-be-retrieved> ^{<attr> <=> 0}
                     -^<depth>)
-->
   (<s> ^we-need-to-go-deeper (+ <attr> 1))
}

sp {recursive-retrieve*propose*initialize
   (state <s> ^name recursive-retrieve
             -^to-be-retrieved
             -^retrieved
              ^quiescence t)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialize)
}

sp {recursive-retrieve*apply*initialize*default-depth
   (state <s> ^name recursive-retrieve
              ^operator.name initialize
             -^superstate.operator.depth)
-->
   (<s> ^depth 9999)
}

sp {recursive-retrieve*apply*initialize*copy-depth
   (state <s> ^name recursive-retrieve
              ^operator.name initialize
              ^superstate.operator.depth <depth>)
-->
   (<s> ^depth <depth>)
}

sp {recursive-retrieve*apply*initialize*copy-path
   (state <s> ^name recursive-retrieve
              ^operator.name initialize
              ^superstate.operator <so>)
   (<so> ^lti <lti>
         ^path <path>)
-->
   (<s> ^mapping.node <node>)
   (<node> ^path <path>
           ^retrieved <lti>)
}

sp {recursive-retrieve*apply*initialize*create-sets
   (state <s> ^name recursive-retrieve
              ^operator.name initialize
              ^superstate.operator.lti <lti>
              ^depth <depth>)
-->
   (<s> ^to-be-retrieved.<depth> <lti>
        ^retrieved <retrieved>)
}

sp {recursive-retrieve*propose*retrieve
   (state <s> ^name recursive-retrieve
              ^depth {<depth> > 0}
              ^to-be-retrieved.<depth> <lti>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name retrieve
         ^lti <lti>)
}

sp {recursive-retrieve*apply*retrieve*retrieve
   (state <s> ^name recursive-retrieve
              ^operator <op>
              ^smem.command <scmd>
             -^retrieved.lti <lti>)
   (<op> ^name retrieve
         ^lti <lti>)
-->
   (<scmd> ^retrieve <lti>)
}

sp {recursive-retrieve*apply*retrieve*copy-children*no-path
   (state <s> ^name recursive-retrieve
              ^operator <op>
              ^smem.result.retrieved <lti>
              ^depth <depth>
              ^to-be-retrieved <to-be-retrieved>
              ^retrieved <retrieved>
             -^mapping)
   (<op> ^name retrieve
         ^lti <lti>)
   (<lti> ^<attr> {<val> <=> <s>})
   (<retrieved> -^lti <val>)
-->
   (<to-be-retrieved> ^(- <depth> 1) <val>)
}

sp {recursive-retrieve*apply*retrieve*copy-children*path
   (state <s> ^name recursive-retrieve
              ^operator <op>
              ^smem.result.retrieved <lti>
              ^depth <depth>
              ^to-be-retrieved <to-be-retrieved>
              ^retrieved <retrieved>
              ^mapping <mapping>)
   (<op> ^name retrieve
         ^lti <lti>)
   (<lti> ^<attr> {<val> <=> <s>})
   (<retrieved> -^lti <val>)
   (<mapping> ^node <node>)
   (<node> ^path.<attr> <ident>
           ^retrieved <lti>)
-->
   (<to-be-retrieved> ^(- <depth> 1) <val>)
   (<mapping> ^node <new-node>)
   (<new-node> ^path <ident>
               ^retrieved <val>)
}

sp {recursive-retrieve*apply*retrieve*remove-command
   (state <s> ^name recursive-retrieve
              ^operator <op>
              ^smem <smem>
              ^depth <depth>
              ^to-be-retrieved <to-be-retrieved>
              ^retrieved <retrieved>)
   (<op> ^name retrieve
         ^lti <lti>)
   (<smem> ^command <scmd>
           ^result.<< success failure bad-cmd >>)
   (<scmd> ^retrieve <lti>)
   (<to-be-retrieved> ^<depth> <lti>)
-->
   (<scmd> ^retrieve <lti> -)
   (<to-be-retrieved> ^<depth> <lti> -)
   (<retrieved> ^lti <lti>)
}

sp {recursive-retrieve*propose*go-deeper
   (state <s> ^name recursive-retrieve
              ^retrieved
              ^we-need-to-go-deeper <depth>
              ^depth <depth>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-deeper)
}

sp {recursive-retrieve*apply*go-deeper
   (state <s> ^name recursive-retrieve
              ^operator.name go-deeper
              ^depth <depth>)
-->
   (<s> ^depth <depth> -
        ^depth (- <depth> 1))
}

# perform-pick-up-task

sp {delivery*propose*perform-pick-up-task
   (state <s> ^name delivery
              ^io.input-link.objects.object <obj>
              ^tasks.task <task>)
   (<task> ^object <desc>)
   # check that the object has not been forgotten
   (<desc> ^forgotten no
          -^id)
   # check that the object is right for the task
  -{ (<desc> ^{<attr> <> forgotten} <val>)
    -(<obj> ^<attr> <val>) }
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-pick-up-task
        ^task <task>
        ^object <obj>)
}

sp {perform-pick-up-task*propose*pick-up-object
   (state <s> ^name perform-pick-up-task
              ^superstate.operator <so>)
   (<so> ^task <task>
         ^object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name pick-up-object
        ^object <obj>)
}

# mark-object-picked-up

sp {delivery*propose*mark-object-picked-up
   (state <s> ^name delivery
              ^tasks <tasks>
              ^io.input-link.self.carry.object <obj>)
   (<obj> ^id <id>)
  -(<tasks> ^task.object.id <id>)
   (<tasks> ^task <task>)
   (<task> ^object <desc>)
   # check that the object has not been forgotten
   (<desc> ^forgotten no
          -^id)
   # check that the object is right for the task
  -{ (<desc> ^{<attr> <> forgotten} <val>)
    -(<obj> ^<attr> <val>) }
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-object-picked-up
        ^task <task>
        ^object <obj>)
}

sp {delivery*apply*mark-object-picked-up
   (state <s> ^name delivery
              ^operator <o>)
   (<o> ^name mark-object-picked-up
        ^task.object <desc>
        ^object.id <id>)
-->
   (<desc> ^id <id>)
}

# FIXME need to store this back to smem too

# perform-put-down-task

sp {delivery*propose*perform-put-down-task
   (state <s> ^name delivery
              ^io.input-link.self.carry.object <obj>
              ^current-area <area>
              ^tasks.task <task>)
   (<task> ^object.id <id>
           ^destination <dest>)
   (<obj> ^id <id>)
   # check that the destination has not been forgotten
   (<dest> ^forgotten no)
   # check that we are in the right location
  -{ (<dest> ^{<attr> <> forgotten} <val>)
    -(<area> ^<attr> <val>) }
-->
   (<s> ^operator <op> +)
   (<op> ^name perform-put-down-task
         ^task <task>
         ^object <obj>)
}

sp {perform-put-down-task*propose*put-down-object
   (state <s> ^name perform-put-down-task
              ^superstate.operator.object <obj>)
-->
   (<s> ^operator <op> +)
   (<op> ^name put-down-object
         ^object <obj>)
}

# mark-task-completed

sp {delivery*propose*mark-task-completed
   (state <s> ^name delivery
              ^tasks.task <task>
              ^io.input-link.objects.object <obj>
              ^current-area <area>)
   # check that the task has not been forgotten
   (<task> ^object <desc>
           ^destination <dest>
           ^forgotten no)
   # check that the object is right for the task
  -{ (<desc> ^{<attr> <> forgotten} <val>)
    -(<obj> ^<attr> <val>) }
   # check that we are in the right location
  -{ (<dest> ^{<attr> <> forgotten} <val>)
    -(<area> ^<attr> <val>) }
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-task-completed
        ^task <task>)
}

sp {delivery*apply*mark-task-completed
   (state <s> ^name delivery
              ^operator <o>
              ^tasks <tasks>)
   (<tasks> ^task <task>)
   (<o> ^name mark-task-completed
        ^task <task>)
-->
   (<tasks> ^task <task> -)
}

# record-area-wm

sp {delivery*propose*record-area-wm
   (state <s> ^name delivery
             -^current-area
              ^parameters.areas-held-in wm)
-->
   (<s> ^operator <o> +)
   (<o> ^name record-area-wm)
}

sp {record-area-wm*propose*create-neighbor-area
   (state <s> ^name record-area-wm
              ^io.input-link.area-description <description>
             -^areas.area.id <to>)
   (<description> ^id <id>
                  ^gateway.to {<to> <> <id>})
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-neighbor-area
        ^area-id <to>)
}

sp {record-area-wm*apply*create-neighbor-area
   (state <s> ^name record-area-wm
              ^operator <o>
              ^areas <areas>)
   (<o> ^name create-neighbor-area
        ^area-id <id>)
-->
   (<areas> ^area.id <id>)
}

sp {record-area-wm*propose*copy-curent-area
   (state <s> ^name record-area-wm
              ^io.input-link.area-description <description>
              ^areas <areas>)
   (<description> ^id <id>)
  -{ (<description> ^gateway.to {<to> <> <id>})
    -(<areas> ^area.id <to>) }
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-current-area)
}

sp {record-area-wm*apply*copy-current-area*create-area
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description.id <id>
              ^areas <areas>)
  -(<areas> ^area.id <id>)
-->
   (<areas> ^area <area>)
   (<area> ^id <id>)
}

sp {record-area-wm*apply*copy-current-area*link-area
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^areas.area <area>
              ^io.input-link.area-description <description>)
   (<area> ^id <id>)
   (<description> ^id <id>
                  ^type <type>
                  ^wall <north-wall>
                  ^wall <south-wall>
                  ^wall <east-wall>
                  ^wall <west-wall>)
   (<north-wall> ^direction north
                 ^y <north-y>)
   (<south-wall> ^direction south
                 ^y <south-y>)
   (<east-wall> ^direction east
                ^x <east-x>)
   (<west-wall> ^direction west
                ^x <west-x>)
-->
   (<s> ^area <area>)
   (<area> ^type <type>
           ^class area
           ^center-x (/ (+ <east-x> <west-x>) 2)
           ^center-y (/ (+ <north-y> <south-y>) 2)
           ^width (- <east-x> <west-x>)
           ^height (- <north-y> <south-y>))
}

sp {record-area-wm*apply*copy-current-area*gateway
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description.gateway <cgateway>
              ^areas.area <to-area>
              ^area <area>
              ^constants.direction-opposites.<direction> <opposite>)
   (<area> ^id <id>
          -^<direction>.id <gid>)
   (<to-area> ^id <to>)
   (<cgateway> ^id <gid>
               ^to {<to> <> <id>}
               ^direction <direction>
               ^x <x>
               ^y <y>)
-->
   (<area> ^<direction> <gateway>)
   (<to-area> ^<opposite> <gateway>)
   (<gateway> ^id <gid>
              ^class coord
              ^type gateway
              ^to <area>
              ^to <to-area>
              ^x <x>
              ^y <y>)
}

sp {record-area-wm*apply*copy-current-area*done
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description <description>
              ^area <area>)
   (<area> ^id <id>)
  -{ (<description> ^gateway <gateway>)
     (<gateway> ^to {<to> <> <id>}
                ^direction <direction>)
    -(<area> ^<direction>.to.id <to>) }
-->
   (<area> ^stored yes)
}

# pick-up-object

sp {pick-up-object*elaborate*destination
   (state <s> ^name pick-up-object
              ^superstate.operator.object <obj>)
   (<obj> ^x <x>
          ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {pick-up-object*propose*go-to-object
   (state <s> ^name pick-up-object
              ^destination.waypoint <waypoint>
             -^arrived)
   (<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x <x>
        ^y <y>)
}

sp {pick-up-object*propose*get-object
   (state <s> ^name pick-up-object
              ^superstate.operator.object.id <id>
              ^arrived)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-object
        ^actions <actions>)
   (<actions> ^stop <stop>
              ^get-object.id <id>)
}

# put-down-object

sp {put-down-object*elaborate*destination
   (state <s> ^name put-down-object
              ^parameters <params>
              ^current-area <area>)
   (<params> ^storage-delta-x <dx>
             ^storage-delta-y <dy>)
   (<area> ^center-x <x>
           ^center-y <y>
           ^width <width>
           ^height <height>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x (+ (- <x> (/ <width> 2)) <dx>)
                  ^y (+ (- <y> (/ <height> 2)) <dy>))
}

sp {put-down-object*propose*go-to-storage-coord
   (state <s> ^name put-down-object
              ^destination.waypoint <waypoint>
             -^arrived)
   (<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x <x>
        ^y <y>)
}

sp {put-down-object*propose*drop-object
   (state <s> ^name put-down-object
              ^superstate.operator.object.id <id>
              ^arrived)
-->
   (<s> ^operator <o> +)
   (<o> ^name drop-object
        ^actions <actions>)
   (<actions> ^stop <stop>
              ^drop-object.id <id>)
}

# patrol

sp {delivery*propose*patrol
   (state <s> ^name delivery)
-->
   (<s> ^operator <o> +)
   (<o> ^name patrol
        ^area <a01> <a02> <a03> <a04> <a05> <a06> <a07> <a08> <a09> <a10> <a11> <a12>)
   (<a01> ^next <a02>
          ^id 0)
   (<a02> ^next <a03>
          ^id 5)
   (<a03> ^next <a04>
          ^id 10)
   (<a04> ^next <a05>
          ^id 15)
   (<a05> ^next <a06>
          ^id 20)
   (<a06> ^next <a07>
          ^id 25)
   (<a07> ^next <a08>
          ^id 30)
   (<a08> ^next <a09>
          ^id 35)
   (<a09> ^next <a10>
          ^id 40)
   (<a10> ^next <a11>
          ^id 45)
   (<a11> ^next <a12>
          ^id 50)
   (<a12> ^next <a01>
          ^id 55)
} 

sp {patrol*propose*go-to-adjacent-area
   (state <s> ^name patrol
              ^superstate.operator.area <area>
              ^current-area.id <id>)
   (<area> ^id <id>
           ^next <next-area>)
   (<next-area> ^id <next-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-adjacent-area
        ^area-id <next-id>)
}

# go-to-adjacent-area

sp {go-to-adjacent-area*elaborate*destination
   # TODO this can cause multiple destinations to be specified
   # if there are two gateways to the same room
   (state <s> ^name go-to-adjacent-area
              ^superstate.operator.area-id <next-id>
              ^current-area.<< north south east west >> <gateway>)
   (<gateway> ^to.id <next-id>
              ^x <x>
              ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {go-to-adjacent-area*propose*go-to-gateway
   (state <s> ^name go-to-adjacent-area
              ^destination.waypoint <waypoint>
             -^arrived)
   (<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x <x>
        ^y <y>)
}

sp {go-to-adjacent-area*propose*enter-area
   (state <s> ^name go-to-adjacent-area
              ^superstate.operator.area-id <next-id>
              ^arrived yes
              ^current-area.{<direction> << north south east west >>}.to.id <next-id>
              ^destination.waypoint <waypoint>
              ^constants.gateway-deltas.<direction> <deltas>)
   (<waypoint> ^x <x>
               ^y <y>)
   (<deltas> ^x <delta-x>
             ^y <delta-y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x (+ <x> <delta-x>)
        ^y (+ <y> <delta-y>))
}

# go-to-coord

sp {go-to-coord*elaborate*destination
   (state <s> ^name go-to-coord
              ^superstate.operator <so>)
   (<so> ^x <x>
         ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {go-to-coord*propose*turn-to-waypoint
   (state <s> ^name go-to-coord
              ^parameters.heading-tolerance <heading-tol>
              ^destination.waypoint <waypoint>)
   (<waypoint> ^yaw <yaw>
               ^abs-relative-bearing {> <heading-tol>})
-->
   (<s> ^operator <o> +)
   (<o> ^name turn-to-waypoint
        ^actions.set-heading.yaw <yaw>)
}

sp {go-to-coord*propose*drive-to-waypoint
   (state <s> ^name go-to-coord
              ^parameters <params>
              ^destination.waypoint <waypoint>)
   (<params> ^distance-tolerance <distance-tol>
             ^linear-velocity <velocity>
             ^heading-tolerance <heading-tol>)
   (<waypoint> ^abs-relative-bearing {<= <heading-tol>}
               ^distance {<remaining> > <distance-tol>})
-->
   (<s> ^operator <o> +)
   (<o> ^name drive-to-waypoint
        ^actions.set-linear-velocity.linear-velocity <velocity>)
}

# waypoints

sp {propose*add-waypoint
   (state <s> ^name <name>
             -^io.input-link.waypoints.waypoint.id <name>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-waypoint
        ^actions.add-waypoint <add-waypoint>)
   (<add-waypoint> ^id <name>
                   ^x <x>
                   ^y <y>)
}

sp {propose*remove-waypoint
   (state <s> ^name <name>
              ^io.input-link.waypoints.waypoint <waypoint>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
   (<waypoint> ^id <name>)
  -(<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name remove-waypoint
        ^actions.remove-waypoint.id <name>)
}

# output commands

sp {apply*operator*create-output
   (state <s> ^operator.actions.<attr> <val>
              ^io.output-link <ol>
              ^quiescence t)
-->
   (<ol> ^<attr> <val>)
}

sp {apply*operator*remove-output
   (state <s> ^superstate nil
              ^operator <o>
              ^io.output-link <ol>)
   (<ol> ^<attr> <val>)
   (<val> ^status << interrupted complete error >>)
-->
   (<ol> ^<attr> <val> -)
}

# wait

sp {propose*wait
   (state <s> ^attribute state
              ^choices none
             -^operator.name wait)
-->
   (<s> ^operator <o> +)
   (<o> ^name wait)
}
