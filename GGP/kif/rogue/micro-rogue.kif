;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TL-Rogue GDL Rule Set
; Tom Hinrichs (Northwestern U.)
; In collabortion with David Aha (NRL)
; 2007 June 12
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TL Level ? 
; Scenario: Name ??
; - Source Problem: ??
; - Target Problem: ??
; Debugger: "http://games.stanford.edu:5999/director/debugrawgame?"
;  
; Environment: micro-Rogue (a simplification of Rogue)
; 
; Description: A hero ("@") must traverse 1 or more levels of a
; 2-dimensional "dungeon" in search of the Amulet of DARPA ("!"), which
; he must bring to the dungeon's exit at level 1. The dungeon is 
; populated by monsters with varying capabilities,
; which can attack the hero. To combat them, the hero can
; read scrolls, quaff potions, and use weapons and armor, all found
; while traversing the dungeon. The hero carries a sack of up to n items, 
; where "n" is the sack's capacity. The hero, and all monsters,
; have an initial (maximum) health value. Unlike monsters, the hero 
; regains 1 point of health for each turn he is not attacked. 
; 
; Initial state: Hero has an empty sack, the amulet is on level 1,
; as are a variety of objects and monsters.
;
; Goal state: Hero is at dungeon exit, with the amulet in the sack.
;
; Visualizing Maps:
; - <0,0> is top-left room square (".") below
; - <7,7> is the lower-right square
; - Y axis increases from top to bottom (0 indexing)
; - X axis increases from left to right (0 indexing)
; - Characters/objects can be in any room or corridor location,
;   and no two characters can be in the same location
; 
; Map Example and Legend:
;    01234567     + = Corner
;   +----+---+    - = Horizontal wall
;  0|pB..#.w.|    | = Vertical wall
;  1|.w..|s..|    . = A room location 
;  2|a...|...|    # = A corridor location
;  3|@..S|*.K|    @ = hero 
;  4+----+-#-+    a = armor, p = potion, s = scroll,
;  5|..!.B..p|    w = weapon, ! = amulet, * = gold
;  6|........|    < = Staircase (down), > = Staircase (Up)
;  7|H......>|    K = kestral
;   +--------+    H = hobgoblin
;                 B = bat
;                 S = snake
;
;
; Hero commands/actions: 
; - Rest() 
;   - Hero does not move or do anything else
; - Move(dir): N, NE, E, SE, S, SW, W, NW, Up, Down
;   - All but Up and Down can cause attacks on an adjacent monster
; - Drop(item): Drop item currently in the sack
;   - Can only have one object per location on its floor
; - Read(scroll): Read scroll currently in the sack
; - Quaff(potion): Quaff potion currently in the sack
; - Throw(arrow,dir): Throws arrow in quiver currently in sack (direction dir)
;   - Arrows travel instantaneously and as far as possible
; - Wear(armor): Wears armor that is currently in the sack
; - Remove(): Removes armor currently being worn; remains in sack
; - Wield(weapon): Wields weapon that is currently in the sack
; 
; Properties of objects:
; - All objects (armor, amulet, potions, scrolls, weapons)
;   - Are picked up automatically when moved on, if room in sack
;   - burden=1
;   - Can be dropped
;   - Gold: Does not count towards burden
; - Armor: Defense values in [1,3]
;   - None: 1         ; No defense against monsterattacks
;   - Ring mail: 2    ; First monster attack does not decrease health
;   - Banded mail: 3  ; First 2 monster attacks do not decrease health
;   - Counts towards burden while worn
; - Potions
;   - Speed (Doubles speed for 3 turns)
;   - Strength (Increases attack value by 1 for 3 turns)
;   - Invisibility (note: cannot implement blindness)
;   - Deleted after use
; - Scrolls:
;   - Restore health (to maximum value)
;   - Armor enhance (Increases armor strength by 1, to max 3)
;   - Sleep (forces "rest" command for 2 turns)
;   - Deleted after use
; - Weapon: Offensive value in [1,3]
;   - None: 1 (melee only)
;   - Mace: 2 (melee only)
;   - Sword: 3 (melee only)
;   - Bow: 1 (when throwing arrow)
;   - Counts towards burden while wielded
;
; Simplifications from the game of Rogue:
; - No fog of war (all rooms are lit), doors, traps, food, wands, or rings
; - No levels of experience
; - Can only throw an arrow when wielding a bow
;   - Decrements number of arrows in a quiver
;   - Assume arrow always hit a target if it is thrown towards target
;   - Assume arrow has unlimited range (as in Rogue)
;   - Assume arrow otherwise disappears
; - Sack
;   - Max capacity: 5 
;   - Items wielded or worn continue to count towards carrying burden
;   - A set of bow and arrows takes 1 item location in a sack
; - Monsters: Name (Character) [Initial health, Damage when attacking, Base speed]
;   - Bat (B) [3, 1, 1]
;     - Asleep unless/until attacked
;   - Hobgoblin (H) [5, 2, 1] 
;     - Toughest monster in game
;   - Kestral (K) [2, 1, 2]
;     - Moves at twice speed unless hitting
;   - Snake (S) [4, 1, 0.5]
;     - Moves at half speed
; 
; Notes (things you'd notice in a detailed read of this GDL rule set)
;   0. Turn-taking: The hero and the monsters rotate turns; they
;      do not take place simultaneously. 
;   1. The hero cannot share a location with a monster. 
;   2. When in the same room or corridor as a monster that is not asleep,
;      the monster will advance towards the hero. Likewise, they
;      will advance towards an adjacent hero that is running away.
;   3. To use different dungeons per "run", we could use separate GDL rule sets,
;      differing only in the "Init" clauses, for each new dungeon.
;
; Recent updates (e.g., to fix bugs):
;   - Replaced plus and lessThan with the appropriate built-in arithmetic
;   - Eliminated diagonal movement and reduced map to 8x8
;     This should make the game more tractable for this years experiments
;     without significantly reducing functionality.
;
; Known bugs:
;   - None yet
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GDL State Machine Model Components: 
;   1. Role: Players of the game
;   2. Init: Facts true in the initial state (initial state of fluents)
;   3. Transitions: Rules that can be used to determine what is and will 
;                   be true in the world
;   4. Legal: Legal moves for a <player,state> pair
;   5. Goal: Value of a (final) state for a player
;   6. Terminal: Terminal state
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 1. Role
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Meaning: This is a one-player game
(role hero)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 2. Init 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Step: <stepnumber>
; Meaning: Player starts at step/state 1. Game has finite length.
(init (step 1))

; Object locations: <object_name x y>
(init (location amulet 2 5))
(init (location armor1 0 2))
(init (location weapon1 1 1))
(init (location weapon2 6 0))

(init (location potion1 0 0))
(init (location potion2 7 5))
(init (location scroll1 5 1))
(init (location exit 7 7))           ; staircaseup
(init (location gold 5 3))

; Character locations: <character x y>
(init (location hero 0 3))
(init (location bat1 1 0))
(init (location bat2 4 5))
(init (location hobgoblin1 0 7))
(init (location kestral1 7 3))
(init (location snake1 3 3))

; Health: <character initial_health>
(init (health hero 6))
(init (health bat1 3))   
(init (health bat2 3))
(init (health kestral1 2))
(init (health hobgoblin1 5))
(init (health snake1 4))
(init (asleep bat1 0))
(init (asleep bat2 0))

; Burden: <burdenamount>
; Meaning: Initially the sack is empty. 
(init (burden 0))

; Quiversize <quiver> <size>
(init (quiverSize 5))

; Parity counters for half & quarter speed movement:
(init odd)
(init (slomo-time 1))
;----------------------------------------------------------------------
; Type Predicates
;----------------------------------------------------------------------

; Room: <topLeft_x topLeft_y bottomRight_x bottomRight_y>
(room 0 0 3 3)    ; Room 1 (Top left)
(room 5 0 7 3)    ; Room 2 (RHS)
(room 0 5 7 7)    ; Room 3 (Lower left)

; Corridor location: <x y>
(corridor 4 0) ; Horizontal corridor/doorway betweeen R1 and R2

(corridor 6 4) ; Vertical corridor/doorway between R2 and R3


; Meaning: Specific monsters (and their types) in *this* dungeon
(monster bat1 bat)   
(monster bat2 bat)   
(monster hobgoblin1 hobgoblin)
(monster kestral1 kestral)  
(monster snake1 snake)
(monster snake2 snake)

; Meaning: Items in this dungeon (identified here, defined later)
; note: the bow includes the quiver and arrows
(armor armor1 bandedmail)
(weapon weapon1 mace)
(weapon weapon2 bow)
(potion potion1 speed)
(potion potion2 invisibility) 
(scroll scroll1 armorEnhance)

; An item is anything you can pick up.
(item armor1)
(item weapon1)
(item weapon2)
(item potion1)
(item potion2)
(item scroll1)
(item amulet)
(item gold)


;----------------------------------------------------------------------
; Constants
;----------------------------------------------------------------------

; Meaning: Sack can contain at most this number of items
(maxBurden 5)

; Meaning: Maximum health a hero can ever attain
(maxHealth hero 6)

; Meaning: The maximum defined time-step (t = 50)
(maxTime 50)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 3. Transitions: Rules used to reason about states
;    - Specifies the dynamics of fluents
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;----------------------------------------------------------------------
; 3.1 Location updates
;----------------------------------------------------------------------

; Meaning: Updates hero location 
(<= (next (location hero ?x ?y))
	(nextHeroLocation ?x ?y))                 ; Action 

; Meaning: Updates monster location
(<= (next (location ?monster ?x ?y))
	(monster ?monster ?monster-type)
	(true (health ?monster ?health))
	(> ?health 0)                             ; built-in
	(nextMonsterLocation ?monster ?x ?y))     ; Action 

; Meaning: The exit does not move
(<= (next (location exit ?x ?y))
	(true (location exit ?x ?y)))

; Meaning: The staircase does not move
(<= (next (location staircasedown ?x ?y))
	(true (location staircasedown ?x ?y)))

; Meaning: Items remain on the ground until they are picked up
(<= (next (location ?item ?x ?y))
	(item ?item)
	(true (location ?item ?x ?y))
	(not (pickedUp ?item)))        ; Object-interaction

; Meaning: Dropped objects are placed at the hero's current location
(<= (next (location ?item ?x ?y))
	(does hero (drop ?item))
	(true (location hero ?x ?y)))

;----------------------------------------------------------------------
; 3.2 Health updates for hero
;     - If the hero is attacked, his health is decreased by difference
;       of 1 + (monster's attack value - hero defense value)
;     - Regains health if not attacked! (Rate is described below)
;     - Health goes to max if hero reads restoreHealth scroll
;----------------------------------------------------------------------

; Meaning: Hero incurs damage
; Note: totalMonsterDamage maxes out at hero's current health, so
; we shouldn't need to check for negative health here.
(<= (next (health hero ?hpNew))
	(totalMonsterDamage ?hitPointsLost)
	(> ?hitPointsLost 0)
	(true (health hero ?hpOld))
	(- ?hpOld ?hitPointsLost ?hpNew))


; Meaning: Hero heals 1 HP when not attacked.
; - We'll probably slow this recovery rate in a later version
(<= (next (health hero ?hpNew))
	(totalMonsterDamage 0)
	(not (healthRestoredByScroll))
	(true (health hero ?hpOld))
	(maxHealth hero ?maxHealth)
	(> ?maxHealth ?hpOld)
	(+ ?hpOld 1 ?hpNew))

; Meaning: Hero already at maxHealth and not attacked
(<= (next (health hero ?maxHealth))
	(totalMonsterDamage 0)
	(maxHealth hero ?maxHealth)
	(true (health hero ?maxHealth)))

; Meaning: Hero read a restoreHealth scroll to regain maximum health
(<= (next (health hero ?maxHealth))
	(totalMonsterDamage 0)
	(healthRestoredByScroll)
	(maxHealth hero ?maxHealth))

;----------------------------------------------------------------------
; 3.3 Health updates for monsters
;     - If attacked by hero, health decreases by attack value of hero's 
;       wielded weapon!
;     - Simplification: Only arrows can be thrown, and only when hero
;       wields a bow
;----------------------------------------------------------------------

; Meaning: Hero kills the monster (melee)
(<= (next (health ?monster 0))
	(monster ?monster ?monster-type)
	(monsterAttacked ?monster)
	(true (health ?monster ?hpOld))
	(heroOffense ?wDamage)
	(>= ?wDamage ?hpOld))

; Meaning: Hero hurts the monster (melee)
(<= (next (health ?monster ?hpNew))
	(monster ?monster ?monster-type)
	(monsterAttacked ?monster)
	(true (health ?monster ?hpOld))
	(heroOffense ?wDamage)
	(> ?hpOld ?wDamage)
	(- ?hpOld ?wDamage ?hpNew))

; Meaning: Hero kills the monster (by throwing an arrow)
; Hardcoding the damage value of arrows: 2
(<= (next (health ?monster 0))
	(monster ?monster ?monster-type)
	(hitByArrow ?monster)
	(true (health ?monster ?hpOld))
	(>= 2 ?hpOld))

; Meaning: Hero hurts the monster (by throwing an arrow)
(<= (next (health ?monster ?hpNew))
	(monster ?monster ?monster-type)
	(true (health ?monster ?hpOld))
	(> ?hpOld 2)
	(hitByArrow ?monster)
	(- ?hpOld 2 ?hpNew))

(<= (next(health ?monster ?hp))
	(monster ?monster ?monster-type)
	(not (monsterAttacked ?monster))
	(not (hitByArrow ?monster))
	(true (health ?monster ?hp)))

;----------------------------------------------------------------------
; 3.4 Burden-related updates
;     - Sack, armor, and weaponry
;----------------------------------------------------------------------

;--------------------------------------------------
; 3.4.1: Burden maintenance relations
;--------------------------------------------------

; Meaning: Items remain in hero's sack when picked up
(<= (next (carrying ?item))
	(item ?item)
	(pickedUp ?item))                  ; Object-interaction

; Meaning: Items in sack stay in sack unless dropped
(<= (next (carrying ?item))
	(true (carrying ?item))
	(not (decrementsBurden ?item)))

; Meaning: Maintain size of burden in sack (being carried)
(<= (next (burden ?burden))
	(not (burdenChanged))
	(true (burden ?burden)))

; Meaning: Increment burden by picking up an item
; Note: we don't have to check maxBurden here, because it's checked in pickedUp.
(<= (next (burden ?newBurden))
	(pickedUp ?item)                   ; Object-interaction
	(distinct ?item gold)
	(true (burden ?oldBurden))
	(succ ?oldBurden ?newBurden))      ; Arithmetic

; Meaning: Decrement burden by dropping an item or using a scroll or potion
(<= (next (burden ?newBurden))
	(decrementsBurden ?item)
	(true (burden ?oldBurden))
	(succ ?newBurden ?oldBurden))      ; Arithmetic


;--------------------------------------------------
; 3.4.2 Quiver size (i.e., # arrows in it)
;--------------------------------------------------

; Meaning: Quiver size doesn't change when no arrows are thrown.
; Note: we don't have to check here for wielding the bow or 
; carrying it.  If that's not true, then hero can't legally 
; throw an arrow.
; Also: don't reify the quiver apart from the bow.  It's a 
; package, like a clip of ammo in a gun.
(<= (next (quiverSize ?oldSize))
	(not arrowThrown)
	(true (quiverSize ?oldSize)))

; Meaning: Quiver size decreases when throwing arrow w/ a bow
; - Ensures quiver contains at least one arrow
(<= (next (quiverSize ?newSize))
	(does hero (throw arrow ?dir))
	(true (quiverSize ?oldSize))
	(- ?oldSize 1 ?newSize))           ; Arithmetic


;--------------------------------------------------
; 3.4.3: Armor-wearing relations
;--------------------------------------------------

; Meaning: Hero continues wearing armor until it is removed (into sack)
(<= (next (wearing ?item))
	(true (wearing ?item))
	(not (does hero (remove ?item))))

; Meaning: Hero is wearing item that is in sack and newly worn
(<= (next (wearing ?item))
	(does hero (wear ?item)))

;--------------------------------------------------
; 3.4.4: Weapon-wielding relations
;--------------------------------------------------

; Wielding: means you have a weapon from sack in your hand
; Meaning: Continue wielding a weapon wielded unless switching to another
(<= (next (wielding ?weapon))
	(true (wielding ?weapon))
	(not (changeWeapon ?weapon))
	(not (does hero (drop ?weapon))))

; Meaning: Wielding a new weapon
(<= (next (wielding ?weapon))
	(does hero (wield ?weapon))) ; don't have to check for carrying here.

;--------------------------------------------------
; 3.4.5: Sleep relations
;--------------------------------------------------

(<= (next (asleep hero 2))
	(does hero (read ?scroll))
	(scroll ?scroll sleepInduce))

(<= (next (asleep hero ?n))
	(true (asleep hero ?old-n))
	(distinct ?old-n 1)
	(- ?old-n 1 ?n))

(<= (next (asleep ?monster ?n))
	(monster ?monster ?monster-type)
	(true (asleep ?monster ?n))
	(true (health ?monster ?hp))
	(distinct ?hp 0)
	(not (provoked ?monster))) ; in this case, attacked or hit with an arrow

;--------------------------------------------------
; 3.4.6: Visibility
;--------------------------------------------------

;;; Meaning: hero is invisible to monsters for the next 5 turns
(<= (next (invisible hero 5))
	(does hero (quaff ?potion))
	(potion ?potion invisibility))

(<= (next (invisible hero ?n))
    (true (invisible hero ?old-n))
    (distinct ?old-n 1)
    (- ?old-n 1 ?n))

;--------------------------------------------------
; 3.4.7: Enhanced Speed, Strength, Armor
;--------------------------------------------------

; Meaning: Hero's attack strength is enhanced by 1 for 3 turns
(<= (next (enhancedSpeed hero 3))
    (does hero (quaff ?potion))
    (potion ?potion speed))

(<= (next (enhancedSpeed hero ?n))
    (true (enhancedSpeed hero ?old-n))
    (distinct ?old-n 1)
    (- ?old-n 1 ?n))

(<= (next (enhancedStrength hero 3))
    (does hero (quaff ?potion))
    (potion ?potion strength))

(<= (next (enhancedStrength hero ?n))
    (true (enhancedStrength hero ?old-n))
    (distinct ?old-n 1)
    (- ?old-n 1 ?n))

(<= (next (enhancedArmor hero 2))
	(does hero (read ?scroll)) 
	(scroll ?scroll armorEnhance)
	(true (wearing ?armor))  ; You have to be wearing armor first
	(armor ?armor ?armor-type)
	(armorstrength ?armor-type ?amt)
	(>= 3 ?amt))

(<= (next (enhancedArmor hero ?n))
	(true (enhancedArmor hero ?old-n))
	(distinct ?old-n 1)
	(- ?old-n 1 ?n))

;---------------------------------------------------------------------------
;;; 3.5 Standard GDL support rule
;---------------------------------------------------------------------------

; Meaning: Time advances in one step increments
(<= (next (step ?next))
	(true (step ?current))
	(succ ?current ?next))          ; Arithmetic

; Parity counter for 1/2 speed movement
(<= (next odd)
	(true even))
(<= (next even)
	(true odd))

; Meaning: slow-motion or "matrix" time counter for sluggish snakes
(<= (next (slomo-time 1))
	(true (slomo-time 4)))
(<= (next (slomo-time 2))
	(true (slomo-time 1)))
(<= (next (slomo-time 3))
	(true (slomo-time 2)))
(<= (next (slomo-time 4))
	(true (slomo-time 3)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 4. Legal: Specifies legal moves for players
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Meaning: The hero can rest (and do nothing) at any time
(legal hero (rest))

; Meaning: The hero may move anywhere within rooms and corridors
; - BUT: Moving to a monster's location is an attack, not a move!
(<= (legal hero (move ?dir))
	(not (currentlyAsleep hero))
	(true (location hero ?xOld ?yOld))
	(direction ?dir)                        ; Generic spatial
	(nCell 1 ?dir ?xOld ?yOld ?xNew ?yNew)  ; Movement
	(traversible ?xNew ?yNew))              ; Generic spatial

; Meaning: Hero can drop an item (being carried)
(<= (legal hero (drop ?item))
	(not (currentlyAsleep hero))
	(true (carrying ?item)))  

; Meaning: Hero can read a scroll (being carried)
(<= (legal hero (read ?scroll))
	(not (currentlyAsleep hero))
	(true (carrying ?scroll))
	(scroll ?scroll ?scrollType))            ; Init

; Meaning: Hero can quaff a potion (being carried)
(<= (legal hero (quaff ?potion))
    (not (currentlyAsleep hero))
	(true (carrying ?potion))
	(potion ?potion ?potionType))            ; Init

; Meaning: Hero can throw an arrow (if carrying a non-empty quiver)
(<= (legal hero (throw arrow ?dir))
	(not (currentlyAsleep hero))
	(true (wielding ?weapon))
	(weapon ?weapon bow)                    ; Init
	(true (quiverSize ?numarrows))
	(> ?numarrows 0)
	(direction ?dir))                       ; Generic spatial

; Meaning: Hero can wear armor (being carried)
(<= (legal hero (wear ?item))
    (not (currentlyAsleep hero))
	(true (carrying ?item))       
	(not (armored hero))  
	(armor ?item ?itemType))

; Meaning: Hero can remove armor (currently being worn)
; - It remains in the sack
(<= (legal hero (remove ?item))
	(not (currentlyAsleep hero))
	(true (wearing ?item)))

; Meaning: Hero can wield a weapon (being carried)
(<= (legal hero (wield ?weapon))
	(not (currentlyAsleep hero))
	(true (carrying ?weapon))
	(weapon ?weapon ?weapon-Type))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 5. Goal: Value of a (final) state for a player
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Meaning: Hero receives 100 points if he escapes with the amulet
(<= (goal hero 100)
	(not (true (health hero 0)))    ; Endgame
	(true (carrying amulet))    
	atExit)                         ; Endgame

; Meaning: 75 points if hero has obtained the amulet but not exited
(<= (goal hero 75)
	(not (true (health hero 0)))    ; Endgame
	(true (carrying amulet))      
	(not atExit))                   ; Endgame

; Meaning: 50 points if hero has obtained gold but not exited
(<= (goal hero 75)
	(not (true (health hero 0)))    ; Endgame
	(not (true (carrying amulet)))
	(true (carrying gold))
	(not atExit))                   ; Endgame

; Meaning: The hero receives 0 points if he is killed
(<= (goal hero 0)
	(true (health hero 0)))         ; Endgame

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 6. Terminal: Terminal state
;    - specifies the conditions under which the game has ended
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Meaning: The game is over if the hero is killed
(<= terminal
	(true (health hero 0)))         ; Endgame

; Meaning: The game is over if time runs out
(<= terminal
	(true (step ?t))
	(maxTime ?t))                   ; Endgame

; Meaning: The game is over if the hero has escaped with the amulet
(<= terminal
	(true (carrying amulet))      
	atExit)                         ; Endgame

;=============================================================================
; Supporting (Low-Level) Relations
;=============================================================================


;----------------------------------------------------------------------
; Battle relations
;----------------------------------------------------------------------

; Meaning: The amount of damage incurred by monster attacks (on possibly
; two fronts).  This should return 0 if hero is not attacked and should
; max out at hero's current health.
(<= (totalMonsterDamage 0)
	(nextHeroLocation ?x ?y)
    (not (attackedBySomeMonster ?x ?y)))

(<= (totalMonsterDamage ?damage)
	(nextHeroLocation ?x ?y)
    (monster ?monster ?monster-type)
    (attackedByMonster ?monster ?x ?y)
    (monsterStrength ?monster-type ?strength)
    (totalMonsterDamage1 ?monster ?strength ?x ?y ?damage))

(<= (totalMonsterDamage1 ?monster ?strength ?x ?y ?damage)
	(not (attackedBy2ndMonster ?monster ?x ?y))
	(amountOfMonsterDamage ?strength ?d1)
	(true (health hero ?hp))
	(min ?d1 ?hp ?damage))

(<= (totalMonsterDamage1 ?monster1 ?strength ?x ?y ?damage)
	(totalMonsterDamage2 ?monster1 ?strength ?x ?y ?damage))

; Meaning: combined damage from two monsters:
(<= (totalMonsterDamage2 ?monster1 ?strength1 ?x ?y ?damage)
	(monster ?monster2 ?monster-type)
	(distinct ?monster2 ?monster1)
	(attackedByMonster ?monster2 ?x ?y)
	(monsterStrength ?monster-type ?strength2)
	(+ ?strength1 ?strength2 ?strength)
	(amountOfMonsterDamage ?strength ?combined-damage)
	(true (health hero ?hp))
	(min ?combined-damage ?hp ?damage))

; Meaning: There exists some monster that attacks hero at ?x ?y
(<= (attackedBySomeMonster ?x ?y)
    (monster ?monster ?monster-type)
    (attackedByMonster ?monster ?x ?y))

; Meaning: Monster attacks hero when provoked and within one spot of 
; hero's next location
(<= (attackedByMonster ?monster ?x2 ?y2)
	(provoked ?monster)                            ; Generic monster
	(true (location ?monster ?x1 ?y1))
	(adjacent ?x1 ?y1 ?x2 ?y2))                    ; Generic spatial

; Meaning: Monster1 and monster2 both attack simultaneously.
(<= (attackedBy2ndMonster ?monster1 ?x ?y)
	(monster ?monster2 ?monster-type)
	(distinct ?monster2 ?monster1)
	(attackedByMonster ?monster2 ?x ?y))

; Meaning: No damage if monster's attack is less than defense
; Offensive-strength is the combined strength of attacking monsters.
; - Open to debate
(<= (amountOfMonsterDamage ?offensive-strength 0)
	(heroDefense ?defense)                          ; Battle
	(>= ?defense ?offensive-strength))              ; Arithmetic

; Meaning: Attacks whose value is equal to or greater than armor value
; - Open to debate (i.e., Attack value is 1 + (offense - defense))
(<= (amountOfMonsterDamage ?offensive-strength ?hitPointsLost)
	(heroDefense ?defense)                          ; Battle
	(-  ?offensive-strength ?defense ?diff)         ; Arithmetic
	(+ ?diff 1 ?hitPointsLost))                     ; Arithmetic

; Meaning: The current defensive strength of armor worn (if any)
(<= (heroDefense 1)
	(not (armored hero)))

(<= (heroDefense ?defense)
	(not (currentlyEnhancedArmor hero))
	(true (wearing ?item))
	(armor ?item ?armor-type)
	(armorstrength ?armor-type ?defense))

(<= (heroDefense ?defense)
    (int ?n)         ; ?n is how many turns it will remain in effect (0 - 3)
	(true (enhancedArmor hero ?n))
	(true (wearing ?item))
	(armor ?item ?armor-type)
	(armorstrength ?armor-type ?base-defense)
	(plus ?base-defense 1 ?defense))

; Meaning: The current offense strength of weapons wielded (if any)
(<= (heroOffense 1)
	(not (armed hero)))

(<= (heroOffense ?offense)
	(not (currentlyEnhancedStrength hero))
	(true (wielding ?weapon))
	(weapon ?weapon ?weapon-type)                  ; Init
	(weaponStrength ?weapon-type ?offense))        ; Generic weapon

(<= (heroOffense ?offense)
	(currentlyEnhancedStrength hero)
	(true (wielding ?weapon))
	(weapon ?weapon ?weapon-type)                  ; Init
	(weaponStrength ?weapon-type ?wDamage))        ; Generic weapon


; Meaning: Hero attacks the monster
(<= (monsterAttacked ?monster)
	(true (location hero ?x1 ?y1))
	(true (location ?monster ?x2 ?y2))
	(monster ?monster ?monster-type)               ; Init
	(adjacent ?x1 ?y1 ?x2 ?y2)                     ; Generic spatial
	(does hero (move ?dir))
	(nCell 1 ?dir ?x1 ?y1 ?x2 ?y2))                ; Movement

;----------------------------------------------------------------------
; Movement relations
;----------------------------------------------------------------------

;;; Recursively moves one step at a time 
; Meaning: Exhausted our travel distance
(<= (nStepsTowards 0 ?startx ?starty ?destx ?desty ?startx ?starty))

; Meaning: Reached our destination already.  Shouldn't happen if
; destination is occupied by hero!
(<= (nStepsTowards ?n ?x ?y ?x ?y ?x ?y)) 

; Meaning: Recursively move n steps towards our destination
(<= (nStepsTowards ?n ?startx ?starty ?destx ?desty ?finalx ?finaly)
	(> ?n 0)                                        ; Arithmetic
	(singleUnobstructedStep ?startx ?starty ?destx ?desty ?x2 ?y2) ; Movement
	(- ?n 1 ?n-1)                                       ; Arithmetic
	(nStepsTowards ?n-1 ?x2 ?y2 ?destx ?desty ?finalx ?finaly)) ; Movement

; Meaning: Take one step toward destination only if you won't run into hero.
(<= (singleUnobstructedStep ?startx ?starty ?destx ?desty ?nextx ?nexty)
	(oneStepTowards ?startx ?starty ?destx ?desty ?nextx ?nexty)
	(traversible ?nextx ?nexty)
	(nextHeroLocation ?x ?y)
	(not (sameLocation ?nextx ?nexty ?x ?y)))

; Meaning: Stay put and attack the hero.
(<= (singleUnobstructedStep ?startx ?starty ?destx ?desty ?startx ?starty)
	(oneStepTowards ?startx ?starty ?destx ?desty ?nextx ?nexty)
	(nextHeroLocation ?x ?y)
	(sameLocation ?nextx ?nexty ?x ?y))

; Meaning: Move one step towards destination (diagonally)
;(<= (oneStepTowards ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)
;	(diagonalDirection ?dir ?x2 ?y2 ?x1 ?y1)         ; Movement
;	(nCell 1 ?dir ?x1 ?y1 ?x3 ?y3))                  ; Movement

;;; Rather than having horizontal or vertical movement preferences
;;; as in mummymaze, we alternate each turn.

; Meaning: Move one step (horizontally or vertically) towards destination
(<= (oneStepTowards ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)
	(not (diagonal ?x1 ?y1 ?x2 ?y2))
	(cardinalRelation ?cdir ?x2 ?y2 ?x1 ?y1)         ; Movement
	(nCell 1 ?cdir ?x1 ?y1 ?x3 ?y3))                 ; Movement

(<= (oneStepTowards ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)
	(not (diagonal ?x1 ?y1 ?x2 ?y2))
	(cardinalRelation ?cdir ?x2 ?y2 ?x1 ?y1)         ; Movement
	(nCell 1 ?cdir ?x1 ?y1 ?x3 ?y3))                 ; Movement

(<= (oneStepTowards ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)
	(diagonal ?x1 ?y1 ?x2 ?y2)
	(true odd)
	(verticalDir ?cdir)
	(cardinalRelation ?cdir ?x2 ?y2 ?x1 ?y1)         ; Movement
	(nCell 1 ?cdir ?x1 ?y1 ?x3 ?y3))                 ; Movement

(<= (oneStepTowards ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)
	(diagonal ?x1 ?y1 ?x2 ?y2)
	(true even)
	(horizontalDir ?cdir)
	(cardinalRelation ?cdir ?x2 ?y2 ?x1 ?y1)         ; Movement
	(nCell 1 ?cdir ?x1 ?y1 ?x3 ?y3))                 ; Movement


; Meaning: Direction-based relationships between coordinates
; - Read: ?n cells ?direction of ?x1 ?y1 is ?x2 ?y2
(<= (nCell ?n north ?x ?y1 ?x ?y2)
    (int ?x)
	(int ?n)                   ; Arithmetic
	(- ?y1 ?n ?y2))            ; Arithmetic
(<= (nCell ?n south ?x ?y1 ?x ?y2)
    (int ?x)
	(int ?n)                   ; Arithmetic
	(+ ?y1 ?n ?y2))            ; Arithmetic
(<= (nCell ?n east ?x1 ?y ?x2 ?y)
	(int ?n)                   ; Arithmetic
	(int ?y)                   ; Arithmetic
	(+ ?x1 ?n ?x2))            ; Arithmetic
(<= (nCell ?n west ?x1 ?y ?x2 ?y)
	(int ?n)                   ; Arithmetic
	(int ?y)                   ; Arithmetic
	(- ?x1 ?n ?x2))            ; Arithmetic
;(<= (nCell ?n ne ?x1 ?y1 ?x2 ?y2)
;	(int ?n)                   ; Arithmetic
;	(+ ?x1 ?n ?x2)             ; Arithmetic
;	(- ?y1 ?n ?y2))            ; Arithmetic
;(<= (nCell ?n nw ?x1 ?y1 ?x2 ?y2)
;	(int ?n)                   ; Arithmetic
;	(- ?x1 ?n ?x2)             ; Arithmetic
;	(- ?y1 ?n ?y2))            ; Arithmetic
;(<= (nCell ?n se ?x1 ?y1 ?x2 ?y2)
;	(int ?n)                   ; Arithmetic
;	(+ ?x1 ?n ?x2)             ; Arithmetic
;	(+ ?y1 ?n ?y2))            ; Arithmetic
;(<= (nCell ?n sw ?x1 ?y1 ?x2 ?y2)
;	(int ?n)                   ; Arithmetic
;	(- ?x1 ?n ?x2)             ; Arithmetic
;	(+ ?y1 ?n ?y2))            ; Arithmetic

; Meaning: Direction-based relationships between coordinates
; - Read: ?x1 ?y1 is ?dir of ?x2 ?y2
; *** Do we need int statements on all coordinates?
(<= (cardinalRelation north ?x1 ?y1 ?x2 ?y2)
	(> ?y2 ?y1))                  ; Arithmetic
(<= (cardinalRelation south ?x1 ?y1 ?x2 ?y2)
	(> ?y1 ?y2))                  ; Arithmetic
(<= (cardinalRelation east ?x1 ?y1 ?x2 ?y2)
	(> ?x1 ?x2))                  ; Arithmetic
(<= (cardinalRelation west ?x1 ?y1 ?x2 ?y2)
	(> ?x2 ?x1))                  ; Arithmetic
	
(<= (diagonal ?x1 ?y1 ?x2 ?y2)
	(distinct ?x1 ?x2)
	(distinct ?y2 ?y1))

;;; Omit diagonal movement for now:
;(<= (diagonalDirection ne ?x1 ?y1 ?x2 ?y2)
;	(> ?y2 ?y1)                   ; Arithmetic
;	(> ?x1 ?x2))                  ; Arithmetic
;(<= (diagonalDirection nw ?x1 ?y1 ?x2 ?y2)
;	(> ?y2 ?y1)                   ; Arithmetic
;	(> ?x2 ?x1))                  ; Arithmetic
;(<= (diagonalDirection se ?x1 ?y1 ?x2 ?y2)
;	(> ?y1 ?y2)                   ; Arithmetic
;	(> ?x1 ?x2))                  ; Arithmetic
;(<= (diagonalDirection sw ?x1 ?y1 ?x2 ?y2)
;	(> ?y1 ?y2)                   ; Arithmetic
;	(> ?x2 ?x1))                  ; Arithmetic

;----------------------------------------------------------------------
; Generic spatial relations
;----------------------------------------------------------------------

; Roomlocation: <x y>  (derived from "room")
(<= (roomlocation ?x ?y)  
	(room ?tlx ?tly ?brx ?bry)
	(between ?tlx ?x ?brx)        ; Generic spatial
	(between ?tly ?y ?bry))       ; Generic spatial

; Meaning: ?a <= ?b <= ?c (note: permits them all to be equal)
(<= (between ?a ?b ?c)
	(>= ?b ?a)                    ; Arithmetic
	(>= ?c ?b))              ; Arithmetic

; Meaning: <x,y> can be traversed by a character (or hold an item)
; We actually don't need this for monster movement because so long
; as they only pursue hero within a room, they'll never try to 
; go through walls or cut corners.  If we change monster behavior
; or have non-rectangular rooms, then oneStepTowards will have to
; check.
(<= (traversible ?x ?y)
	(roomlocation ?x ?y))   ; Generic spatial
(<= (traversible ?x ?y)
	(corridor ?x ?y))     ; Generic spatial

; Meaning: Eight possible directions of movement
; Do we add up and down sometime?
(direction north)
(direction south)
(direction east)
(direction west)
;(direction ne)
;(direction nw)
;(direction se)
;(direction sw)

(verticalDir north)
(verticalDir south)
(horizontalDir east)
(horizontalDir west)


(<= (inRoom ?e ?x1 ?y1 ?x2 ?y2)
    (true (location ?e ?x ?y))
    (room ?x1 ?y1 ?x2 ?y2)
    (between ?x1 ?x ?x2)
    (between ?y1 ?y ?y2))

(<= (inSameRoom ?e1 ?e2)
    (inRoom ?e1 ?x1 ?y1 ?x2 ?y2)
    (inRoom ?e2 ?x1 ?y1 ?x2 ?y2))

; Meaning: Same location
(sameLocation ?x ?y ?x ?y)

; Meaning: Adjacent locations
(<= (adjacent ?x ?y1 ?x ?y2)
	(diff ?y1 ?y2 1))
(<= (adjacent ?x1 ?y ?x2 ?y)
	(diff ?x1 ?x2 1))
(<= (adjacent ?x1 ?y1 ?x2 ?y2)
	(diff ?x1 ?x2 1)
	(diff ?y1 ?y2 1))

;----------------------------------------------------------------------
; Generic monster relations
;----------------------------------------------------------------------


(initialHealth   bat 3)
(monsterStrength bat 1)
(monsterSpeed    bat 1)

(initialHealth hobgoblin 5)
(monsterStrength hobgoblin 2)
(monsterSpeed hobgoblin 1)

(initialHealth kestral 2)
(monsterStrength kestral 1)
(monsterSpeed kestral 2)

(initialHealth snake 4)
(monsterStrength snake 1)
(monsterSpeed snake 0.5)

; Meaning: Monsters attack when in the same room as the hero.
; If they're asleep, they wake up when hero attacks them or
; throws an arrow at them.
(<= (provoked ?monster)
	(monster ?monster ?monster-type)           ; Generic monster
	(not (currentlyAsleep ?monster))
	(inSameRoom hero ?monster))                ; Generic spatial

(<= (provoked ?monster)
	(monster ?monster ?monster-type)
	(true (asleep ?monster ?n))
	(monsterAttacked ?monster))

(<= (provoked ?monster)
	(monster ?monster ?monster-type)
	(true (asleep ?monster ?n))
	(hitByArrow ?monster))

;----------------------------------------------------------------------
; Generic item relations
;----------------------------------------------------------------------

; Armor strength (defense)
(armorstrength ringmail 2)
(armorstrength bandedmail 3)

; Weapon strength (offense)
(weaponStrength mace 2)
(weaponStrength sword 3)
(weaponStrength bow 1)


;----------------------------------------------------------------------
; Endgame relations
;----------------------------------------------------------------------

; Mearning: True if the hero is at the same location as the exit
(<= atExit 
	(true (location hero ?x ?y))
	(true (location exit ?x ?y)))

;----------------------------------------------------------------------
; Arithmetic relations
;----------------------------------------------------------------------

; Meaning: Absolute value of difference
(<= (diff ?n1 ?n2 ?diff)
    (> ?n1 ?n2)
    (- ?n1 ?n2 ?diff))                              ; Arithmetic
    
(<= (diff ?n1 ?n2 ?diff)
    (>= ?n2 ?n1)
	(- ?n2 ?n1 ?diff))                              ; Arithmetic


; Meaning: Minimum
(<= (min ?n1 ?n2 ?n2)
	(> ?n1 ?n2))                              ; Arithmetic
(<= (min ?n1 ?n2 ?n1)
	(>= ?n2 ?n1))                             ; Arithmetic


; Meaning: Defines a (finite) succesor relation
(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)

; Meaning: The integers in [0,7]; Used in nCell and plus, only
(int 0)
(int 1)
(int 2)
(int 3)
(int 4)
(int 5)
(int 6)
(int 7)

;---------------------------------------------------------------------------
; Action relations
;---------------------------------------------------------------------------

; Meaning: Hero moves one cell in direction ?dir from ?x1 ?y1
; - If <x1,y1> is not currently occupied by a monster.
(<= (nextHeroLocation ?x2 ?y2)
	(true (location hero ?x1 ?y1))
	(does hero (move ?dir))
	(nCell 1 ?dir ?x1 ?y1 ?x2 ?y2)                       ; Movement
	(not (monsterAt ?x2 ?y2)))                           ; Init

; Meaning: Hero's location is unchanged if attacking a monster
(<= (nextHeroLocation ?x1 ?y1)
	(true (location hero ?x1 ?y1))
	(does hero (move ?dir))
	(nCell 1 ?dir ?x1 ?y1 ?x2 ?y2)                       ; Movement
	(monsterAt ?x2 ?y2))

; Meaning: Non-moving hero actions
(<= (nextHeroLocation ?x ?y)
	(true (location hero ?x ?y))
	(not (does hero (move ?dir))))

; ToDo: Handle staircasedown when there are multiple levels.

; Meaning: There's a monster currently at ?x ?y
(<= (monsterAt ?x ?y)
    (true (location ?monster ?x ?y))
    (monster ?monster ?monster-type))

; Meaning: Move provoked monsters towards hero according to ?speed
; - BUT: Cannot stop on same location as hero's location
;   We'll make nStepsTowards stop when the next location will be
;   occupied by the hero.
(<= (nextMonsterLocation ?monster ?x2 ?y2)
	(not (currentlyInvisible hero))
	(provoked ?monster)                              ; Generic monster
	(true (location ?monster ?x1 ?y1))
	(true (location hero ?herox ?heroy))
	(monster ?monster ?monster-type)
	(monsterSpeed ?monster-type ?base-speed)
	(relativeSpeed ?base-speed ?speed)
	(nStepsTowards ?speed ?x1 ?y1 ?herox ?heroy ?x2 ?y2)) ; Movement

; Meaning: Unprovoked monsters don't move
(<= (nextMonsterLocation ?monster ?x ?y)
    (monster ?monster ?monster-type)
	(not (provoked ?monster))
	(true (location ?monster ?x ?y)))

(<= (nextMonsterLocation ?monster ?x ?y)
    (monster ?monster ?monster-type)
	(true (invisible hero ?n))                      ; invisible for n more turns
	(true (location ?monster ?x ?y)))


; Meaning: How many steps can monster take this turn?
; We slow all monsters down when hero has enhancedSpeed
(<= (relativeSpeed 0.5 ?speed)
	(not (currentlyEnhancedSpeed hero))
	(halfSpeed ?speed))
(<= (relativeSpeed 0.5 ?speed)
	(true (enhancedSpeed hero ?n))
	(quarterSpeed ?speed))
(<= (relativeSpeed ?base-speed ?base-speed)
	(distinct ?base-speed 0.5)
	(not (currentlyEnhancedSpeed hero)))
(<= (relativeSpeed 1 ?speed)
	(true (enhancedSpeed hero ?n))
	(halfSpeed ?speed))
(<= (relativeSpeed 2 1)
	(true (enhancedSpeed hero ?n)))

; Anything that moves slower than Hero is handled by 
; flipping speed from 1 to 0 every other turn
; (or every fourth turn for 1/4 speed monsters)
(<= (halfSpeed 1)
	(true odd))
(<= (halfSpeed 0)
	(true even))
(<= (quarterSpeed 1)
	(true (slomo-time 4)))
(<= (quarterSpeed 0)
	(true (slomo-time ?n))
	(distinct ?n 4))

(<= (healthRestoredByScroll)
	(does hero (read ?scroll))
	(scroll ?scroll restoreHealth)
	(true (health hero ?health))
	(maxHealth hero ?maxHealth)
	(distinct ?health ?maxHealth))

;----------------------------------------------------------------------
; Object-interaction relations
;----------------------------------------------------------------------

; Meaning: The monster is hit by an arrow (unlimited range)
(<= (hitByArrow ?monster)
	(monster ?monster ?monster-type)               ; Generic monster
	(does hero (throw arrow ?dir))
	(true (location hero ?x1 ?y1))
	(true (location ?monster ?x2 ?y2))             ; Action
	(nCell ?dist ?dir ?x1 ?y1 ?x2 ?y2))            ; Movement

; Meaning: Hero adds item to sack if on it at end of time step 
; - And it wasn't dropped in this time step
(<= (pickedUp ?item)
	(item ?item)                         ; Generic item
	(distinct ?item gold)
	(true (burden ?burden))
	(maxBurden ?max-burden)              ; Generic sack
	(> ?max-burden ?burden)              ; Arithmetic
	(true (location hero ?x1 ?y1))
	(nextHeroLocation ?x2 ?y2)           ; Action
	(not (sameLocation ?x1 ?y1 ?x2 ?y2)) ; Don't re-pick up an item after dropping it
	(true (location ?item ?x2 ?y2)))

; Handle gold specially since it is not limited by maxBurden
(<= (pickedUp gold)
	(nextHeroLocation ?x ?y)
	(true (location gold ?x ?y)))

(<= (decrementsBurden ?item)
	(use ?item))

(<= (decrementsBurden ?item)
	(does hero (drop ?item)))

; Note: gold doesn't contribute to burden.
(<= burdenChanged
	(pickedUp ?item)                    ; acquired new item
	(distinct ?item gold))

(<= burdenChanged
	(decrementsBurden ?item)            ; successful drop or use
	(distinct ?item gold))

(<= (changeWeapon ?weapon)
	(does hero (wield ?otherweapon))
	(distinct ?weapon ?otherweapon))

(<= arrowThrown
    (direction ?dir)
	(does hero (throw arrow ?dir)))

; Meaning: hero is invisible at the moment.
; helper predicate to avoid unbound vars in negative statement
(<= (currentlyInvisible hero)
	(true (invisible hero ?n)))

(<= (currentlyAsleep ?agent)
	(true (asleep ?agent ?n)))

(<= (currentlyEnhancedArmor hero)
	(true (enhancedArmor hero ?n)))

(<= (currentlyEnhancedStrength hero)
	(true (enhancedStrength hero ?n)))

(<= (currentlyEnhancedSpeed hero)
	(true (enhancedSpeed hero ?n)))

(<= (armored hero)
	(true (wearing ?armor))
	(armor ?armor ?armor-type))

(<= (armed hero)
	(true (wielding ?weapon))
	(weapon ?weapon ?weapon-type))

; Meaning: Hero quaffs a potion currently in the sack
; This is just a convenience relation that indicates an item has been 
; used up and no longer counts as a burden.  The effect of the
; quaff or read must be handled in a next rule.
(<= (use ?potion)
	(does hero (quaff ?potion)))

; Meaning: Hero reads a scroll currently in the sack
(<= (use ?scroll)
	(does hero (read ?scroll)))


;;; End of file

