sp {propose*complete-task
   (state <s> ^name sorts
              ^planning <p>)
   (<p> ^execution-buffer.task <ct>)
   (<ct> ^completed)
-->
   (<s> ^operator <o> +)
   (<o> ^name planning-complete-task
        ^task <ct>)}

sp {apply*complete-task
   (state <s> ^name sorts
              ^planning <p>
              ^io.input-link.view-frame <vf>
              ^operator <o>)
   (<p> ^execution-buffer <eb>
        ^completed <c>)
   (<o> ^name planning-complete-task
        ^task <t>)
   #(<t> ^name <n> ^plan <pl>)
   (<t> ^name <n>)
-->
   (<c> ^task <ct>)
   (<ct> ^name <n>
        # ^plan <pl>
         ^completion-frame <vf>)
   (<eb> ^task <t> -)}

sp {propose*resolve-dep
   (state <s> ^name sorts
              ^planning <pl>)
   (<pl> ^retrieval-buffer.task <rt>
         ^completed <c>)
   (<rt> ^dep <d>)
   (<c> ^task.name <d>)
-->
   (<s> ^operator <o> +)
   (<o> ^name planning-resolve-dep
        ^task <rt>
        ^dep <d>)}

sp {apply*resolve-dep
   (state <s> ^name sorts
              ^operator <o>)
   (<o> ^name planning-resolve-dep
        ^task <t>
        ^dep <p>)
-->
   (<t> ^dep <p> -)}

sp {propose*resolve-start-dep
   (state <s> ^name sorts
              ^planning <pl>)
   (<pl> ^retrieval-buffer.task <rt>
         ^execution-buffer.task <et>)
   (<rt> ^start-dep <d>)
   (<et> ^name <d>
         ^started)
-->
   (<s> ^operator <o> +)
   (<o> ^name planning-resolve-dep
        ^task <rt>
        ^start-dep <d>)}

sp {apply*resolve-start-dep
   (state <s> ^name sorts
              ^operator <o>)
   (<o> ^name planning-resolve-dep
        ^task <t>
        ^start-dep <p>)
-->
   (<t> ^start-dep <p> -)}


sp {propose*execute-task
   (state <s> ^name sorts
              ^planning <p>)
   (<p> ^retrieval-buffer.task <rt>
        ^execution-buffer <eb>)
   (<rt> -^dep
         -^start-dep)
-->
   (<s> ^operator <o> +)
   (<o> ^name planning-execute-task
        ^task <rt>)}

sp {apply*execute-task
   (state <s> ^name sorts
              ^planning <p>
              ^operator <o>)
   (<p> ^retrieval-buffer <rb>
        ^execution-buffer <eb>)
   (<o> ^name planning-execute-task
        ^task <t>)
-->
   (<eb> ^task <t>)
   (<rb> ^task <t> -)}