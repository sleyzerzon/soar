sp {clean-house*propose*record-block
   (state <s> ^name robot
              ^parameters.objects-held-in << wm smem >>
              ^io.input-link.objects.object <obj>)
   (<s> ^blocks <b>)
  -(<b> ^block.id <id>)
   (<obj> ^id <id>
          ^visible true)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name record-block
         ^block <obj>)
}

sp {apply*record-block*wm
   (state <s> ^operator <op>
              ^parameters.objects-held-in wm
              ^current-location <area>
              ^blocks <b>)
   (<op> ^name record-block
         ^block <obj>)
   (<obj> ^x <x> ^y <y>
          ^id <id>
          ^type <type>)
-->
   (<b> ^block <nb>)
   (<nb> ^x <x> ^y <y>
         ^id <id>
         ^in-storage-room no
         ^area <area>
         ^class block
         ^type <type>)
}

## Test to see if already know block exists
sp {apply*record-block*smem*query
   (state <s> ^smem.command <cmd>
              ^parameters.objects-held-in smem
              ^operator <op>)
   (<op> ^name record-block
         ^block <obj>)
   (<obj> ^id <id>
          ^type { <> player <type> })
-->
   (<cmd> ^query <q>)
   (<q> ^type <type>
        ^in-storage-room no
        ^class block
        ^id <id>)
}

sp {apply*record-block*retrieve*smem
   (state <s> ^parameters.objects-held-in smem
              ^blocks <blocks>
              ^smem <smem>
              ^operator <op>)
   (<op> ^name record-block
         ^block <obj>)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^retrieved <cp>
          ^success <q>)
   (<q> ^id <id>
        ^type <type>)
-->
  #(write (crlf) |Retrieved block | <cp> | id | <id> | type | <type>)
   (<blocks> ^block <cp>)}

sp {apply*record-block*failed-query*store-new
   (state <s> ^operator <op>
              ^current-location <area>
              ^parameters.objects-held-in smem
              ^blocks <blocks>
              ^smem <smem>)
   (<op> ^name record-block
         ^block <obj>)
   (<obj> ^x <x> ^y <y>
          ^id <id>
          ^type <type>)
   (<smem> ^result.failure <q>
           ^command <cmd>)
   (<cmd> ^query <q>)
   (<q> ^id <id>)
-->
  #(write (crlf) |Stored object | <block> | id | <id> | type | <type>)
   (<blocks> ^block <block>)
   (<cmd> ^store <block>)
   (<block> ^x <x> ^y <y>
            ^area <area>
            ^in-storage-room no
            ^id <id>
            ^class block
            ^type <type>)
}