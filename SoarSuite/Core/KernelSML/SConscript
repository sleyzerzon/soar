#!/usr/bin/python
# Project: Soar <http://soar.googlecode.com>
# Author: Jonathan Voigt <voigtjr@gmail.com>
#

Import('env')
env = env.Clone()

env.Prepend(CPPPATH = ['src', '#SoarKernel/src', '#CLI/src', '#ElementXML/src', '#ConnectionSML/src'])

libadd = None
if not env['STATIC_LINKED']:
	libadd = ['ElementXML', 'CommandLineInterface', 'ConnectionSML', 'SoarKernel', 'sqlite3']

if env['SCU']:
	if env['STATIC_LINKED']:
		SoarKernelSML = env.Library('SoarKernelSML', 'KernelSML.cxx')
	else:
		SoarKernelSML = env.SharedLibrary('SoarKernelSML', 'KernelSML.cxx', LIBS = libadd, LIBPATH = '$PREFIX/lib')
else:
	if env['STATIC_LINKED']:
		SoarKernelSML = env.Library('SoarKernelSML', Glob('src/*.cpp'))
	else:
		SoarKernelSML = env.SharedLibrary('SoarKernelSML', Glob('src/*.cpp'), LIBS = libadd, LIBPATH = '$PREFIX/lib')

env.Install('$PREFIX/lib', SoarKernelSML)

# Install resources
Import('InstallDir')
InstallDir(env, '$PREFIX/share/soar/Demos', '#Demos')
InstallDir(env, '$PREFIX/share/soar/Documentation', '#Documentation')

env.Clean(SoarKernelSML, env.FindInstalledFiles())

