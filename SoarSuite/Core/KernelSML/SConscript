#!/usr/bin/python

Import('env')
env = env.Clone()

env.Prepend(CPPPATH = ['src', '#SoarKernel/src', '#CLI/src', '#ElementXML/src', '#ConnectionSML/src'])

libadd = None
if not env['STATIC_LINKED']:
	libadd = ['ElementXML', 'CommandLineInterface', 'ConnectionSML', 'SoarKernel', 'sqlite3']

if env['SCU']:
	if env['STATIC_LINKED']:
		SoarKernelSML = env.Library('SoarKernelSML', 'KernelSML.cxx')
	else:
		SoarKernelSML = env.SharedLibrary('SoarKernelSML', 'KernelSML.cxx', LIBS = libadd, LIBPATH = '$PREFIX/lib')
else:
	if env['STATIC_LINKED']:
		SoarKernelSML = env.Library('SoarKernelSML', Glob('src/*.cpp'))
	else:
		SoarKernelSML = env.SharedLibrary('SoarKernelSML', Glob('src/*.cpp'), LIBS = libadd, LIBPATH = '$PREFIX/lib')

env.Install('$PREFIX/lib', SoarKernelSML)

# Install resources
demoSources = Glob('#Demos/*')
demoTargetDir = '$PREFIX/share/soar/Demos'
env.Install(target = demoTargetDir,
            source = demoSources)

docTargetDir = '$PREFIX/share/soar/Documentation'
env.Install(target = docTargetDir,
            source = '#Documentation')

env.Clean('$PREFIX/share/soar', [demoTargetDir, docTargetDir])

