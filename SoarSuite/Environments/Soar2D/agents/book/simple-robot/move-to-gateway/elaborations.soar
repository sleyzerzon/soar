sp {move-to-gateway*elaborate*substate
   (state <s> ^name move-to-gateway
              ^superstate.operator <so>)
   (<so> ^name move-to-gateway
         ^gateway <g>)
-->
   (<s> ^gateway <g>)
}

sp {move-to-gateway*elaborate*state*angle-too-large
   (state <s> ^name move-to-gateway
              ^gateway.center.angle-off > 0.2)
-->
   (<s> ^need-to-turn true)
}

sp {move-to-gateway*elaborate*state*angle-too-small
   (state <s> ^name move-to-gateway
              ^gateway.center.angle-off < -0.2)
-->
   (<s> ^need-to-turn true)
}

sp {move-to-gateway*elaborate*state*moving
   (state <s> ^name move-to-gateway
              ^io.output-link.move.status executing
             -^io.output-link.move.status complete)
-->
   (<s> ^moving true)
}

sp {move-to-gateway*elaborate*state*rotating
   (state <s> ^name move-to-gateway
              ^io.output-link.rotate-relative.status executing
             -^io.output-link.rotate-relative.status complete)
-->
   (<s> ^rotating true)
}