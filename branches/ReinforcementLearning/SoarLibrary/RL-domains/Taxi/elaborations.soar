sp {elaborate*taxi
  (state <s> ^superstate nil)
  -->
  (<s> ^task taxi)}
  
sp {propose*initialize
  (state <s> ^task taxi -^info-link)
  -->
  (<s> ^operator <o> +)
  (<o> ^name initialize)}
  
sp {apply*initialize
  (state <s> ^operator <o>)
  (<o> ^name initialize)
 -->
  (<s> ^info-link <i>)
  (<i> ^x-coor 0 ^y-coor 0
       ^passenger zoo ^destination library)}
  
sp {propose*get
  (state <s> ^task taxi ^info-link <i>)
  (<i> ^passenger <> taxi)
 -->
  (<s> ^operator <o> + =)
  (<o> ^name get)}
  
sp {propose*put
   (state <s> ^task taxi ^info-link <i>)
   (<i> ^passenger taxi)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name put)}
   
 sp {elaborate*state
   (state <s> ^impasse no-change ^attribute operator ^superstate <s1>)
   (<s1> ^operator <o>)
   -->
   (<s> ^superoperator <o>)}
   

sp {propose*pickup
  (state <s> ^superoperator.name get)
  -->
  (<s> ^operator <o> + =)
  (<o> ^name pickup)}
  
sp {apply*pickup*success
  (state <s> ^operator.name pickup ^info-link <i>)
  (<i> ^location <x> ^passenger <x>)
 -->
  (<i> ^passenger <x> -)
  (<i> ^passenger taxi)}
  
sp {apply*pickup*failure*no*location
  (state <s> ^operator <o> ^info-link <i>)
  (<i> -^location)
  (<o> ^name pickup)
 -->
  (<o> ^result failure)}
  
sp {apply*pickup*failure*wrong*location
  (state <s> ^operator <o> ^info-link <i>)
  (<i> ^location <x> ^passenger <> <x>)
  (<o> ^name pickup)
  -->
  (<o> ^result failure)}

sp {propose*putdown
  (state <s> ^superoperator.name put)
 -->
  (<s> ^operator <o> + =)
  (<o> ^name putdown)}
  
sp {apply*putdown*success
  (state <s> ^operator.name putdown ^info-link <i>)
  (<i> ^location <x> ^destination <x>)
  -->
   (<i> ^passenger taxi -)
   (<i> ^passenger <x>)}
   
sp {apply*putdown*failure*no*location
  (state <s> ^operator <o> ^info-link <i>)
  (<i> -^location)
  (<o> ^name putdown)
 -->
  (<o> ^result failure)}
  
sp {apply*putdown*failure*wrong*location
  (state <s> ^operator <o> ^info-link <i>)
  (<i> ^location <x> ^destination <> <x>)
  (<o> ^name putdown)
 -->
  (<o> ^result failure)}

sp {propose*navigate*get
  (state <s> ^superoperator.name get ^info-link <i>)
  (<i> ^passenger <p> -^location <p>)
 -->
   (<s> ^operator <o> + =)
   (<o> ^name navigate ^to <p>)}

sp {propose*navigate*put
  (state <s> ^superoperator.name put ^info-link <i>)
  (<i> ^destination <x> -^location <x>)
  -->
  (<s> ^operator <o> + =)
  (<o> ^name navigate ^to <x>)}
  
sp {elaborate*at*zoo
  (state <s> ^info-link <i>)
  (<i> ^x-coor 4 ^y-coor 4)
  -->
  (<i> ^location zoo)}
  
sp {elaborate*at*library
  (state <s> ^info-link <i>)
  (<i> ^x-coor 3 ^y-coor 0)
  -->
  (<i> ^location library)}
  
sp {elaborate*at*hospital
  (state <s> ^info-link <i>)
  (<i> ^x-coor 0 ^y-coor 0)
-->
  (<i> ^location hospital)}
  
sp {elaborate*at*airport
  (state <s> ^info-link <i>)
  (<i> ^x-coor 0 ^y-coor 4)
-->
  (<i> ^location airport)}
  
sp {state*copy
  (state <s> ^superstate <s1>)
  (<s1> ^info-link <i>)
  -->
  (<s> ^info-link <i>)}