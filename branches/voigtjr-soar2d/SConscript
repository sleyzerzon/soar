#!/usr/bin/python
import glob
import shutil
import SoarSCons
import sys

Import('env')
env = env.Clone()

if sys.platform == 'cygwin':
	SoarSCons.SetJavaPaths(env, '"Core/ClientSMLSWIG/Java/sml.jar;SoarLibrary/bin/swt.jar;SoarLibrary/bin/tosca.jar;Environments/Soar2D/jdom.jar;Environments/Soar2D/log4j-1.2.15.jar"')
else:
	SoarSCons.SetJavaPaths(env, 'Core/ClientSMLSWIG/Java/sml.jar:SoarLibrary/bin/swt.jar:SoarLibrary/bin/tosca.jar:Environments/Soar2D/jdom.jar:Environments/Soar2D/log4j-1.2.15.jar')

javacStep = env.Java(target = 'classes', source = 'src')

copySources = []
copySources.extend(glob.glob('src/*.cnf'))
copySources.extend(glob.glob('src/images/book/*'))
copySources.extend(glob.glob('src/images/tanksoar/*'))

copyTargets = map(lambda x: x.replace('src', 'classes'), copySources)
	
copyStep = env.Command(copyTargets, copySources, SoarSCons.DoCopy)
jarStep = env.Command('#Environments/Soar2D/Soar2D.jar', [copySources, 'classes'], 'jar cfm Environments/Soar2D/Soar2D.jar Environments/Soar2D/JarManifest -C Environments/Soar2D/classes .')

env.Depends(jarStep, copyStep)
env.Depends(copyStep, javacStep)

env.Clean('classes', 'classes')
