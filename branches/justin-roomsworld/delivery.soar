# PARAMETERS

# areas-held-in
# default-storage-area-id
# delete-old-areas
# look-ahead-planning
# method-ecological-doors
# method-ecological-entry
# mission
# objects-held-in
# search-control-go-to-gateway
# tasks-held-in

# TASKS

sp {interrupt
   (state <s> ^operator.name record-area-wm
              ^io.input-link.area-description.id 15)
-->
   #(interrupt)
}

# DELIVERY PROBLEM SPACE

sp {propose*init-agent
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-agent)
}

sp {apply*init-agent*parameters
   (state <s> ^operator.name init-agent)
-->
   (<s> ^name delivery
        ^tasks <tasks>
        ^areas <areas>
        ^objects <objects>)
}

sp {delivery*elaborate*parameters
   (state <s> ^name delivery)
-->
   (<s> ^parameters <params>)
   (<params> ^distance-tolerance 1.0
             ^heading-tolerance 1.5
             ^linear-velocity 0.7)
}

sp {delivery*elaborate*constants
   (state <s> ^name delivery
              ^parameters <params>)
   (<params> ^distance-tolerance <distance-tol>)
-->
   (<s> ^constants <constants>)
   (<constants> ^direction-delta <dir-delta>
                ^direction-opposite <dir-op>)
   (<dir-delta> ^north <dir-delta-north>
                ^south <dir-delta-south>
                ^east <dir-delta-east>
                ^west <dir-delta-west>)
   (<dir-delta-north> ^x 0.0
                      ^y (+ 0.5 <distance-tol>))
   (<dir-delta-south> ^x 0.0
                      ^y (- -0.5 <distance-tol>))
   (<dir-delta-east> ^x (+ 0.5 <distance-tol>)
                     ^y 0.0)
   (<dir-delta-west> ^x (- -0.5 <distance-tol>)
                     ^y 0.0)
   (<dir-op> ^north south
             ^south north
             ^east west
             ^west east)
}

# ELABORATIONS

# default

sp {elaborate*topstate*topstate
   (state <s> ^superstate nil)
-->
   (<s> ^topstate <s>)
}

sp {elaborate*state*inherited*defaults
   (state <s> ^superstate.{<attr> << io topstate >>} <val>)
-->
   (<s> ^<attr> <val>)
}

sp {elaborate*state*state-name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

# domain-specific inherited attributes

sp {elaborate*state*inherited*domain
   (state <s> ^superstate.{<attr> << parameters constants areas objects tasks >>} <val>)

-->
   (<s> ^<attr> <val>)
}

# navigation

sp {elaborate*state*current-area
   (state <s> ^io.input-link.area-description.id <id>
              ^areas.area <area>)
   (<area> ^id <id>
           ^stored yes)
-->
   (<s> ^current-area <area>)
}

sp {elaboroate*state*waypoint
   (state <s> ^name <name>
              ^io.input-link.waypoints.waypoint <waypoint>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
   (<waypoint> ^id <name>
               ^x <x>
               ^y <y>)
-->
   (<destination> ^waypoint <waypoint>)
}

sp {elaborate*state*arrived
   (state <s> ^name <name>
              ^parameters.distance-tolerance <distance-tol>
              ^destination.waypoint.distance {<= <distance-tol>})
-->
   (<s> ^arrived yes)
}

# PREFERENCES

sp {elaborate*operator*movement
   (state <s> ^operator <o> +)
   (<o> ^name << patrol go-to-adjacent-area go-to-coord turn-to-coord drive-to-coord >>)
-->
   (<o> ^type movement)
}

sp {elaborate*operator*record
   (state <s> ^operator <o> +)
   (<o> ^name << record-area-wm >>)
-->
   (<o> ^type record)
}

sp {delivery*compare*record*movement
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^type record)
   (<o2> ^type movement)
-->
   (<s> ^operator <o1> > <o2>)
}

# OPERATORS

# record-area-wm

sp {delivery*propose*record-area-wm
   (state <s> ^name delivery
             -^current-area
              ^parameters.areas-held-in wm)
-->
   (<s> ^operator <o> +)
   (<o> ^name record-area-wm)
}

sp {record-area-wm*propose*create-neighbor-area
   (state <s> ^name record-area-wm
              ^io.input-link.area-description <description>
             -^areas.area.id <to>)
   (<description> ^id <id>
                  ^gateway.to {<to> <> <id>})
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-neighbor-area
        ^area-id <to>)
}

sp {record-area-wm*apply*create-neighbor-area
   (state <s> ^name record-area-wm
              ^operator <o>
              ^areas <areas>)
   (<o> ^name create-neighbor-area
        ^area-id <id>)
-->
   (<areas> ^area.id <id>)
}

sp {record-area-wm*propose*copy-curent-area
   (state <s> ^name record-area-wm
              ^io.input-link.area-description <description>
              ^areas <areas>)
   (<description> ^id <id>)
  -{ (<description> ^gateway.to {<to> <> <id>})
    -(<areas> ^area.id <to>) }
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-current-area)
}

sp {record-area-wm*apply*copy-current-area*create-area
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description <description>
              ^areas <areas>)
   (<description> ^id <id>
                  ^type <type>)
  -(<areas> ^area.id <id>)
-->
   (<s> ^area <area>)
   (<areas> ^area <area>)
   (<area> ^id <id>
           ^type <type>
           ^class area)
}

sp {record-area-wm*apply*copy-current-area*link-area
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description <description>
              ^areas.area <area>)
   (<description> ^id <id>
                  ^type <type>)
   (<area> ^id <id>)
-->
   (<s> ^area <area>)
   (<area> ^type <type>
           ^class area)
}

sp {record-area-wm*apply*copy-current-area*gateway
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description.gateway <cgateway>
              ^areas.area <to-area>
              ^area <area>
              ^constants.direction-opposite.<direction> <opposite>)
   (<area> ^id <id>
          -^<direction>.id <gid>)
   (<to-area> ^id <to>)
   (<cgateway> ^id <gid>
               ^to {<to> <> <id>}
               ^direction <direction>
               ^x <x>
               ^y <y>)
-->
   (<area> ^<direction> <gateway>)
   (<to-area> ^<opposite> <gateway>)
   (<gateway> ^id <gid>
              ^class coord
              ^type gateway
              ^to <area>
              ^to <to-area>
              ^x <x>
              ^y <y>)
}

sp {record-area-wm*apply*copy-current-area*done
   (state <s> ^name record-area-wm
              ^operator.name copy-current-area
              ^io.input-link.area-description <description>
              ^area <area>)
   (<area> ^id <id>)
  -{ (<description> ^gateway <gateway>)
     (<gateway> ^to {<to> <> <id>}
                ^direction <direction>)
    -(<area> ^<direction>.to.id <to>) }
-->
   (<area> ^stored yes)
}

# patrol

sp {delivery*propose*patrol
   (state <s> ^name delivery)
-->
   (<s> ^operator <o> +)
   (<o> ^name patrol
        ^area <a01> <a02> <a03> <a04> <a05> <a06> <a07> <a08> <a09> <a10> <a11> <a12>)
   (<a01> ^next <a02>
          ^id 0)
   (<a02> ^next <a03>
          ^id 5)
   (<a03> ^next <a04>
          ^id 10)
   (<a04> ^next <a05>
          ^id 15)
   (<a05> ^next <a06>
          ^id 20)
   (<a06> ^next <a07>
          ^id 25)
   (<a07> ^next <a08>
          ^id 30)
   (<a08> ^next <a09>
          ^id 35)
   (<a09> ^next <a10>
          ^id 40)
   (<a10> ^next <a11>
          ^id 45)
   (<a11> ^next <a12>
          ^id 50)
   (<a12> ^next <a01>
          ^id 55)
} 

sp {patrol*propose*go-to-adjacent-area
   (state <s> ^name patrol
              ^superstate.operator.area <area>
              ^current-area.id <id>)
   (<area> ^id <id>
           ^next <next-area>)
   (<next-area> ^id <next-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-adjacent-area
        ^area-id <next-id>)
}

# go-to-adjacent-area

sp {go-to-adjacent-area*elaborate*destination
   # TODO this can cause multiple destinations to be specified
   # if there are two gateways to the same room
   (state <s> ^name go-to-adjacent-area
              ^superstate.operator.area-id <next-id>
              ^current-area.<< north south east west >> <gateway>)
   (<gateway> ^to.id <next-id>
              ^x <x>
              ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {go-to-adjacent-area*propose*go-to-gateway
   (state <s> ^name go-to-adjacent-area
              ^destination.waypoint <waypoint>
             -^arrived)
   (<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x <x>
        ^y <y>)
}

sp {go-to-adjacent-area*propose*enter-area
   (state <s> ^name go-to-adjacent-area
              ^superstate.operator.area-id <next-id>
              ^arrived yes
              ^current-area.{<direction> << north south east west >>}.to.id <next-id>
              ^destination.waypoint <waypoint>
              ^constants.direction-delta.<direction> <dir-delta>)
   (<waypoint> ^x <x>
               ^y <y>)
   (<dir-delta> ^x <delta-x>
                ^y <delta-y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-coord
        ^x (+ <x> <delta-x>)
        ^y (+ <y> <delta-y>))
}

# go-to-coord

sp {go-to-coord*elaborate*destination
   (state <s> ^name go-to-coord
              ^superstate.operator <so>)
   (<so> ^x <x>
         ^y <y>)
-->
   (<s> ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
}

sp {go-to-coord*propose*turn-to-waypoint
   (state <s> ^name go-to-coord
              ^parameters.heading-tolerance <heading-tol>
              ^destination.waypoint <waypoint>)
   (<waypoint> ^yaw <yaw>
               ^abs-relative-bearing {> <heading-tol>})
-->
   (<s> ^operator <o> +)
   (<o> ^name turn-to-waypoint
        ^actions.set-heading.yaw <yaw>)
}

sp {go-to-coord*propose*drive-to-waypoint
   (state <s> ^name go-to-coord
              ^parameters <params>
              ^destination.waypoint <waypoint>)
   (<params> ^distance-tolerance <distance-tol>
             ^linear-velocity <velocity>
             ^heading-tolerance <heading-tol>)
   (<waypoint> ^abs-relative-bearing {<= <heading-tol>}
               ^distance {<remaining> > <distance-tol>})
-->
   (<s> ^operator <o> +)
   (<o> ^name drive-to-waypoint
        ^actions.set-linear-velocity.linear-velocity <velocity>)
}

# waypoints

sp {propose*add-waypoint
   (state <s> ^name <name>
             -^io.input-link.waypoints.waypoint.id <name>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-waypoint
        ^actions.add-waypoint <add-waypoint>)
   (<add-waypoint> ^id <name>
                   ^x <x>
                   ^y <y>)
}

sp {propose*remove-waypoint
   (state <s> ^name <name>
              ^io.input-link.waypoints.waypoint <waypoint>
              ^destination <destination>)
   (<destination> ^x <x>
                  ^y <y>)
   (<waypoint> ^id <name>)
  -(<waypoint> ^x <x>
               ^y <y>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name remove-waypoint
        ^actions.remove-waypoint.id <name>)
}

# output commands

sp {apply*operator*create-output
   (state <s> ^operator.actions.<attr> <val>
              ^io.output-link <ol>)
-->
   (<ol> ^<attr> <val>)
}

sp {apply*operator*remove-output
   (state <s> ^superstate nil
              ^operator <o>
              ^io.output-link <ol>)
   (<ol> ^<attr> <val>)
   (<val> ^status << interrupted complete error >>)
-->
   (<ol> ^<attr> <val> -)
}

# wait

sp {propose*wait
   (state <s> ^attribute state
              ^choices none
             -^operator.name wait)
-->
   (<s> ^operator <o> +)
   (<o> ^name wait)
}
