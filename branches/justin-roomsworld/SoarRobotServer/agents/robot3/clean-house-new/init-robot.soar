sp {clean-house*propose*init-robot
   (state <s> ^name robot
             -^problem-space)
-->
   (<s> ^operator <op> !)
   (<op> ^name init-robot)
}

sp {robot*apply*init-robot*directions
   (state <s> ^name robot
              ^operator.name init-robot
              ^parameters.default-storage-area-id <dsta>)
-->
   (<s> ^problem-space <ps>
        ^desired <d>
        ^objects <b>
        ^path-cost 0)
   (<d> ^storage-area-id <dsta>)
   (<ps> ^name clean-house
         ^default-state-copy yes
         ^one-level-attributes areas current-location destination-area type visited-area)
}

sp {robot*apply*init-robot*areas
   (state <s> ^name robot
              ^parameters.areas-held-in wm
              ^io.input-link.area-description <ad>
              ^operator.name init-robot)
   (<ad> ^id <id>
         ^type <type>)
-->
   (<s> ^areas.area <area>)
   (<area> ^id <id>
           ^type <type>)
}

sp {apply*init-robot*delivery
   (state <s> ^operator <op>
              ^parameters.mission delivery)
   (<op> ^name init-robot)
-->
   (<s> ^tasks <tasks>
        ^missions <mis>)
   (<mis> ^mission <m1>)
   (<m1> ^name delivery
         ^status urgent)
}

#### Default mission is to clean-rooms of objects
sp {apply*init-robot
   (state <s> ^operator <op>
              ^parameters.mission {<> delivery <mission>})
   (<op> ^name init-robot)
-->
   (<s> ^missions <mis>)
   (<mis> ^mission <m1> <m2>)
   (<m1> ^name <mission>
         ^type get-object
         ^status urgent
         ^object <obj>)
   (<obj> ^type block
          ^color green)
   (<m2> ^name random-walk
         ^status proposed)
}
