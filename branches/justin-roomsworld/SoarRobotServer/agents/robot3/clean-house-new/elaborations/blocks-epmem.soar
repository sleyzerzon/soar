sp {clean-house*elaborate*block
   (state <s> ^name robot
              ^parameters.objects-held-in epmem
              ^missions.mission.object <mobj>
              ^current-location <area>
             -^current-location.storage-area yes
              ^io.input-link.objects.object <obj>)
   (<mobj> ^color <color>
           ^type <type>)
   (<s> ^objects <b>)
   (<obj> ^<attr> <val>)
-->
   (<b> ^object <nb>)
   (<nb> ^<attr> <val>
         ^in-storage-room no
         ^area <area>
         ^class object)
}

sp {clean-house*elaborate*block*storage
   (state <s> ^name robot
              ^current-location.storage-area yes
              ^current-location <area>
              ^parameters.objects-held-in epmem
              ^io.input-link.objects.object <obj>)
   (<s> ^objects <b>)
   (<obj> ^<attr> <val>)
-->
   (<b> ^object <nb>)
   (<nb> ^<attr> <val>
         ^area <area>
         ^in-storage-room yes
         ^class object)
}

## Hack
sp {apply*block*save-object-in-storage-area
   :o-support
   (state <s> ^superstate nil
              ^parameters.objects-held-in  << epmem smem >>
              ^storage-area <st>
              ^objects.object <obj>
              ^current-location <ca>)
   (<ca> ^storage-area yes)
   (<obj> ^id <id>)
-->
   (<st> ^object-id <id>)
}
