#IF I haven't retrieved any epmems to do the evaluation yet
#THEN propose doing so
sp {evaluate-op*propose*recall-combat-result
   (state <s> ^name evaluate-op
             -^epmem.retrieved.<any>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name recall-combat)
}

#IF the operator was selected
#THEN try to retrieve a similar situation in
#     which I took the to-be-evaluated action
sp {apply*recall-combat-result
   (state <s> ^operator <op>
              ^epmem <epmem>
              ^io.input-link <il>)
   (<epmem> ^query <query>)
   (<op> ^name recall-combat)
-->
   (<query> ^io <qio>)
   (<qio> ^input-link <il>
          ^output-link <qol>)
}

#If a query has been created with an output-link,
#elaborate it with the eval-item's actions
sp {apply*query*item-action-command
   (state <s> ^operator.name recall-combat
              ^eval-item <item>
              ^epmem.query.io.output-link <qol>)
   (<item> ^actions <act>)
   (<act> ^<att> <value>)
   (<value> ^<att2> <value2>)
-->
   (<qol> ^<att> <value3>)
   (<value3> ^<att2> <value2>)
}

#Prohibit epmems that have a bad meta-evaluation
#%%%NOTE:  Hard-coded threshold
sp {apply*query*prohibit-epmems-with-poor-meta-evaluation
   (state <s> ^operator.name recall-combat
              ^top-state.meta-evaluations.<any> <me>
              ^epmem.query <query>)
   (<query> ^io)
   (<me> ^mem-id <mem-id>
         ^value <= -3)
-->
   (<query> ^command <cmd>)
   (<cmd> ^name prohibit
          ^argument <mem-id>)
   (write (crlf) |Prohibiting epmem #| <mem-id> | due to poor meta-evaluation.|)
#   (interrupt)
}


