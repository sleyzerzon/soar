##These are the elaborations that the tank uses 
##to query epmem


####ENERGY LEVEL
sp {elaborate*state*energy-level*none
   (state <s> ^name tanksoar
              ^io.input-link.energy 0)
-->
   (<s> ^energy none)
}

sp {elaborate*state*energy-level*low
   (state <s> ^name tanksoar
              ^io.input-link.energy <= 200
              ^io.input-link.energy > 0)
-->
   (<s> ^energy low
)
}

sp {elaborate*state*energy-level*moderate
   (state <s> ^name tanksoar
              ^io.input-link.energy < 500
              ^io.input-link.energy > 200)
-->
   (<s> ^energy moderate)
}

sp {elaborate*state*energy-level*high
   (state <s> ^name tanksoar
              ^io.input-link.energy >= 500)
-->
   (<s> ^energy high)
}


#####MISSILES
sp {elaborate*state*missile-stock*none
   (state <s> ^name tanksoar
              ^io.input-link.missiles 0)
-->
   (<s> ^missiles none)
}

sp {elaborate*state*missile-stock*low
   (state <s> ^name tanksoar
              ^io.input-link.missiles > 0
              ^io.input-link.missiles <= 5)
-->
   (<s> ^missiles low)
}

sp {elaborate*state*missile-stock*moderate
   (state <s> ^name tanksoar
              ^io.input-link.missiles < 10
              ^io.input-link.missiles > 5)
-->
   (<s> ^missiles moderate)
}

sp {elaborate*state*missile-stock*high
   (state <s> ^name tanksoar
              ^io.input-link.missiles >= 10)
-->
   (<s> ^missiles high)
}


###########HEALTH LEVEL
sp {elaborate*state*health*low
   (state <s> ^name tanksoar
              ^io.input-link.health <= 200)
-->
   (<s> ^health low)
}

sp {elaborate*state*health*moderate
   (state <s> ^name tanksoar
              ^io.input-link.health < 700
              ^io.input-link.health > 200)
-->
   (<s> ^health moderate)
}

sp {elaborate*state*health*high
   (state <s> ^name tanksoar
              ^io.input-link.health >= 700)
-->
   (<s> ^health high)
}


##########TANK ON RADAR
sp {elaborate*state*tank-on-radar
   (state <s> ^name tanksoar
              ^io.input-link.radar.tank.position <pos>)
-->
   (<s> ^tank-on-radar <pos>)
}

sp {elaborate*state*radar-on*no-tank
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^radar-status on
         ^radar <radar>)
   (<radar> -^tank)
-->
   (<s> ^tank-on-radar no)
}

sp {elaborate*state*radar-off*no-tank
   (state <s> ^name tanksoar
              ^io.input-link.radar-status off)
-->
   (<s> ^tank-on-radar no)
}

##########DISTANCE TO ENEMY
sp {elaborate*state*enemy-distance*adjacent
   (state <s> ^name tanksoar
              ^io.input-link.smell.distance <= 1)
-->
   (<s> ^enemy-distance adjacent)
}

sp {elaborate*state*enemy-distance*close
   (state <s> ^name tanksoar
              ^io.input-link.smell.distance > 1
              ^io.input-link.smell.distance <= 3)
-->
   (<s> ^enemy-distance close)
}

sp {elaborate*state*enemy-distance*moderate
   (state <s> ^name tanksoar
              ^io.input-link.smell.distance > 3
              ^io.input-link.smell.distance <= 6)
-->
   (<s> ^enemy-distance moderate)
}

sp {elaborate*state*enemy-distance*far
   (state <s> ^name tanksoar
              ^io.input-link.smell.distance > 6)
-->
   (<s> ^enemy-distance far)
}

######DODGE DIRECTION
sp {elaborate*state*dodge-direction*not-adjacent
   (state <s> ^enemy-direction <dir>
              ^side-direction.<dir> <ndir>
             -^enemy-direction <ndir>
             -^enemy-distance adjacent
              ^io.input-link.blocked.<ndir> no)
-->
   (<s> ^dodge-direction <ndir>)
}

sp {elaborate*state*dodge-direction*adjacent*left-right
   (state <s> ^io.input-link <il>)
   (<il> ^radar.tank <tank>
         ^blocked.{ <ndir> << forward backward >> } no)
   (<tank> ^distance 0
           ^position <dir>)
-->
   (<s> ^dodge-direction <ndir>)
}

sp {elaborate*state*dodge-direction*adjacent*forward
   (state <s> ^io.input-link <il>)
   (<il> ^radar.tank <tank>
         ^blocked.{ <ndir> << left right >> } no)
   (<tank> ^distance 1
           ^position center)
-->
   (<s> ^dodge-direction <ndir>)
}

####INCOMING MISSILES
sp {elaborate*state*incoming-missiles
   (state <s> ^name tanksoar
              ^io.input-link.incoming.<dir> yes)
-->
   (<s> ^incoming yes)
}

sp {elaborate*state*no-incoming-missiles
   (state <s> ^name tanksoar
              ^io.input-link.incoming <inc>)
   (<inc> -^<dir> yes)
-->
   (<s> ^incoming no)
}
