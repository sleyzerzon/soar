#If the same action has been proposed multiple times
#they're always equal
sp {attack*compare*tie*same-move-turn-equal
   (state <s> ^name attack
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name <action>
          ^direction <dir>)
   (<op2> ^name <action>
          ^direction <dir>)
-->
   (<s> ^operator <op1> = <op2>)
}

#Elaborate move and turn actions with their evaluations
sp {attack*elaborate*operator*evaluation
   (state <s> ^name attack
              ^top-state.evaluations.eval <eval>
              ^operator <op> +)
   (<op> ^name <name>
         ^direction <dir>)
   (<eval> ^name <name>
           ^direction <dir>
           ^value <val>)
-->
   (<op> ^value <val>)
}

#Prefer actions with better evaluations
sp {attack*compare*recalled-result*prefer-higher-score
   (state <s> ^name attack
              ^operator <op1> +
                        <op2> +
              ^top-state.evaluations <evals>)
   (<op1> ^name <action1>
          ^direction <dir1>
          ^value <val>)
   (<op2> ^name <action2>
          ^direction <dir2>
          ^value <less-val> < <val>)
-->
   (<s> ^operator <op1> > <op2>)
   (write (crlf) |Prefer | <action1> | | <dir1> | with score | <val>|  over | <action2> | | <dir2> | (|<less-val>|).|)
#   (interrupt)
}

#Actions with identical evaluations are equal
sp {attack*compare*recalled-result*equal-evals-are-equal
   (state <s> ^name attack
              ^operator <op1> +
                        <op2> +
              ^top-state.evaluations <evals>)
   (<op1> ^name <action1>
          ^direction <dir1>
          ^value <val>)
   (<op2> ^name <action2>
          ^direction <dir2>
          ^value <val>)
-->
   (<s> ^operator <op1> = <op2>)
}


