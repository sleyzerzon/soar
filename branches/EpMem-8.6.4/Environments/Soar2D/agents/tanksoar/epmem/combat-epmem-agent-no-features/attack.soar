echo
echo Attack

### Propose attack operator
#  If the state is the top state, and there is a tank on radar, and health and energy are not low, then 
#  propose the attack operator.
sp {propose*attack
   (state <s> ^name tanksoar
              ^io.input-link.radar.tank
             -^missiles-energy low)
-->
   (<s> ^operator <o> + =)
   (<o> ^name attack)
}


#IF a sub-operator was selected in the attack
#   subgoal that takes an action in the world
#AND the action was selected due to epmem
#AND the epmem gave the selected operator 
#    an evaluation greater than zero (i.e., good)
#THEN save the info about the epmem that
#     led to this evaluation for future
#     meta-evaluation
sp {apply*attack-op*store-meta-evaluation
   (state <s> ^name attack
              ^operator <o>
              ^top-state <ts>
              ^io.input-link <il>)
   (<o> ^name <name>
        ^direction <dir>)
   (<ts> ^evaluations.eval <eval>
         ^pending-meta-evals <pmes>)
   (<eval> ^name <name>
           ^direction <dir>
           ^value > 0
           ^mem-id <mem-id>)
   (<il> ^clock <clock>
         ^my-color <my-color>
         ^smell.color <enemy-color>
         ^current-score <cs>)
   (<cs> ^<enemy-color> <enemy-score>
         ^<my-color> <my-score>)
-->
   (<pmes> ^pme <meta-eval>)
   (<meta-eval> ^mem-id <mem-id>
                ^clock <clock>
                ^my-score <my-score>
                ^enemy-score <enemy-score>)
}

