#IF the retrieved epmem is too old
#THEN propose adding a command to retrieve
#     an epmem within the prescribed range
#AND  prohibit the current memory
sp {init-meta-evals*propose*refine-query-recency
   (state <s> ^name init-meta-evals
              ^epmem.recency >= 50)
-->
   (<s> ^operator <op> + =)
   (<op> ^name refine-query-recency)
}

sp {apply*refine-query-recency
   (state <s> ^operator.name refine-query-recency
              ^epmem <epmem>)
   (<epmem> ^present-id <now-id>
            ^memory-id <mem-id>
            ^query <query>)
-->
   (<query> ^command <cmd1>
            ^command <cmd2>)
   (<cmd1> ^name after
          ^argument (- <now-id> 50))
   (<cmd2> ^name prohibit
           ^argument <mem-id>)
   (write (crlf) |Prohibiting epmem #| <mem-id>)
}

