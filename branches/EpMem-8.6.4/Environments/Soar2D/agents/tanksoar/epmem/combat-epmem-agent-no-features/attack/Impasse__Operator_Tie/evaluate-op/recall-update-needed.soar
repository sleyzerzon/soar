#IF I have retrieved an epmem
#AND the scores in it have changed from the last epmem
#THEN note status needs to be recorded
sp {evaluate-op*elaborate*new-mem*no-scores
   (state <s> ^name evaluate-op
             -^recalled-status
              ^epmem.retrieved.io <rio>
             -^score-record up-to-date)
-->
   (<s> ^recall-update needed)
}

#IF I've been resurrected
#THEN note that a status needs to be recorded
sp {evaluate-op*elaborate*new-mem*resurrected
   (state <s> ^name evaluate-op
             -^recalled-status
              ^epmem.retrieved.io.input-link.resurrect yes)
-->
   (<s> ^recall-update needed)
}

#IF The enemy's score has changed significantly
#THEN note that a status needs to be recorded
sp {evaluate-op*elaborate*new-mem*definitive-enemy-score-change
   (state <s> ^enemy-score.change <enemy-change>  >= 5)
-->
   (<s> ^recall-update needed)
}

#IF My score has changed significantly
#THEN note that a status needs to be recorded
sp {evaluate-op*elaborate*new-mem*definitive-my-score-change
   (state <s> ^my-score.change <my-change>  >= 5)
-->
   (<s> ^recall-update needed)
}


#IF I've retrieved 10 or more epmems
#THEN note that a status needs to be recorded
sp {evaluate-op*elaborate*new-mem*out-of-combat
   (state <s> ^name evaluate-op
             -^recalled-status
              ^epmem.retrieval-count >= 10)
-->
   (<s> ^recall-update needed)
}



