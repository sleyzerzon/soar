#IF retrieved enough evaluations
#   (NOTE: %%%hardcoded threshold)
#THEN report the value
sp {evaluate-op*propose*report-final-evaluation*success
   (state <s> ^name evaluate-op
              ^evaluation.num-evals >= 1)
-->
   (<s> ^operator <op> + =)
   (<op> ^name report-final-evaluation)
}

#This operator trumps any further reporting
sp {evaluate-op*compare*report-final-evaluation
   (state <s> ^name evaluate-op
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name report-final-evaluation)
   (<op2> ^name record-recalled-combat-status)
-->
   (<s> ^operator <op1> > <op2>)
}


#IF I've prohibited too many epmems
#   (%%%NOTE:  hardcoded threshold)
#THEN report the partial evaluation that I have
sp {evaluate-op*propose*report-final-evaluation*too-many-prohibits
   (state <s> ^name evaluate-op
              ^prohibit-count >= 3)
-->
   (<s> ^operator <op> + =)
   (<op> ^name report-final-evaluation)
}

#IF the episodic retrieval failed
#THEN report the partial evaluation that I have 
sp {evaluate-op*propose*report-final-evaluation*no-retrieval
   (state <s> ^name evaluate-op
              ^epmem.result.retrieved no-memory)
-->
   (<s> ^operator <op> + =)
   (<op> ^name report-final-evaluation)
   (write (crlf) |Epmem retrieval failed|)
}

#IF the operator has been selected
#AND an evaluation is available
#THEN report it 
sp {apply*report-final-evaluation
   (state <s> ^operator.name report-final-evaluation
              ^top-state.evaluations <evals>
              ^eval-name <eval-name>
              ^eval-direction <eval-dir>
              ^evaluation <local-eval>)
   (<local-eval> ^mem-id <mem-id>
                 ^avg-value <value>)
-->
   (<evals> ^eval <new-eval>)
   (<new-eval> ^name <eval-name>
               ^direction <eval-dir>
               ^value <value>
               ^mem-id <mem-id>)
   (write (crlf) |FINAL EVALUATION of | <eval-name> | | <eval-dir> |  = | <value>)
   (write (crlf) |==================================================================|)
}


#IF the operator has been selected
#AND there is no evaluation to report
#THEN report a default evaluation
sp {apply*report-final-evaluation*default-evaluation
   (state <s> ^operator.name report-final-evaluation
              ^top-state.evaluations <evals>
              ^eval-name <eval-name>
              ^eval-direction <eval-dir>
             -^evaluation)
-->
   (<evals> ^eval <new-eval>)
   (<new-eval> ^name <eval-name>
               ^direction <eval-dir>
               ^value 0.0001)
   (write (crlf) |FINAL EVALUATION of | <eval-name> | | <eval-dir> |  = 0.0001 (default)|)
   (write (crlf) |==================================================================|)
}

