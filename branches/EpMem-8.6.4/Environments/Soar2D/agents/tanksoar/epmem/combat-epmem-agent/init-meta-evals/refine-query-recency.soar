#IF the retrieved epmem is too old
#THEN propose adding a command to retrieve
#     an epmem within the prescribed range
#AND  prohibit the current memory
sp {init-meta-evals*propose*refine-query-recency
   (state <s> ^name init-meta-evals
              ^epmem.result.recency >= 50)
-->
   (<s> ^operator <op> + =)
   (<op> ^name refine-query-recency)
}

sp {apply*refine-query-recency
   (state <s> ^operator.name refine-query-recency
              ^epmem <epmem>)
   (<epmem> ^result <ep-res>
            ^command <ep-cmd>)
   (<ep-cmd> ^query <query>)
   (<ep-res> ^present-id <now-id>
            ^memory-id <mem-id>)
-->
   (<ep-cmd> ^after (- <now-id> 50)
             ^prohibit <mem-id>)
   (write (crlf) |Prohibiting epmem #| <mem-id>)
}

