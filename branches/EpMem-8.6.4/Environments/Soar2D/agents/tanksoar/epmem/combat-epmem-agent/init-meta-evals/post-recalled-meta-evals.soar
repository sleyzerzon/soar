#IF a memory has been retrieved
#AND it is sufficiently recent
#    (Note: %%%hardcoded threshold)
#THEN propose harvesting the meta-evals from
#     this epmem
sp {init-meta-evals*propose*post-recalled-meta-evals
   (state <s> ^name init-meta-evals
              ^epmem.result.recency < 50)
-->
   (<s> ^operator <op> + =)
   (<op> ^name post-recalled-meta-evals)
}

#IF the operator has been selected
#THEN create the meta-evals link on
#     the top-state
sp {apply*post-recalled-meta-evals*create-mes-link
   (state <s> ^operator.name post-recalled-meta-evals
              ^top-state <ts>)
-->
   (<ts> ^meta-evaluations <mes>)
}

#IF the operator has been selected
#AND the retrieved epmem contains a
#    meta-evaluation with a strong negative 
#    evaluation ("strong" is hardcoded to -3)
#AND the top-state has a meta-evalutions link
#THEN copy it to the top-state
sp {apply*post-recalled-meta-evals*harvest-meta-eval
   (state <s> ^operator.name post-recalled-meta-evals
              ^epmem.result.retrieved.meta-evaluations.<any> <ret-me>
              ^top-state.meta-evaluations <mes>)
   (<ret-me> ^mem-id <mem-id>
             ^value <val> <= -3)
-->
   (<mes> ^(make-constant-symbol) <me>)
   (<me> ^mem-id <mem-id>
         ^value <val>)
   (write (crlf) |Recalling meta-evaluation of epmem #| <mem-id> | with value | <val>)
}

#IF the operator has been selected
#AND the mes link exists
#THEN create the pending-meta-evals link on
#     the top-state
sp {apply*post-recalled-meta-evals*create-pmes-link
   (state <s> ^operator.name post-recalled-meta-evals
              ^top-state <ts>)
   (<ts> ^meta-evaluations)
-->
   (<ts> ^pending-meta-evals <pmes>)
}

