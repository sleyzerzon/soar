##%%%12 Sep 06:
##Added a 10% chance of non-default action to
##avoid infinite loops

#If I can dodge out of the way
#Propose doing so 90% of the time
sp {retreat*propose*move
   (state <s> ^name retreat
              ^enemy-direction <dir>
              ^top-state.side-direction.<dir> <ndir>
             -^enemy-direction <ndir>
             -^avoid-direction <ndir>
              ^io.input-link <il>)
   (<il> ^random >= 0.1
         ^blocked.<ndir> no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction <ndir>)}

#If I'd normally move to one side
#propose turning instead 10% of the time
sp {retreat*propose*turn*random
   (state <s> ^name retreat
              ^enemy-direction <dir>
              ^top-state.side-direction.<dir> <ndir>
             -^enemy-direction <ndir>
             -^avoid-direction <ndir>
              ^io.input-link <il>)
   (<il> ^random < 0.1
         ^blocked.<ndir> no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name random-turn
        ^actions.rotate.direction left)
}

#If I can't move to avoid the missiles
#propose moving backward 10% of the time
sp {retreat*propose*random-turn*no-wait
   (state <s> ^name retreat
              ^io.input-link <il>)
  -{(<s> ^operator <o> +)
    (<o> ^name move)}
   (<il> ^random < 0.1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name random-turn
        ^actions.rotate.direction left)
}

#If I can't move to avoid the missiles
#propose moving anyway 10% of the time
sp {retreat*propose*random-move*no-wait
   (state <s> ^name retreat
              ^io.input-link <il>)
  -{(<s> ^operator <o> +)
    (<o> ^name move)}
   (<il> ^random > 0.9
         ^blocked.<dir> no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name random-move
        ^actions.move.direction <dir>)
}
                                

