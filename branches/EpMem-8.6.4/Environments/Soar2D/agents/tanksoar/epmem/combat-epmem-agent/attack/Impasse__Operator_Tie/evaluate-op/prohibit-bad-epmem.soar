#IF a first retrieved epmem does not match the 
#   to-be-evaluated operator
#THEN prohibit the epmem to force
#     a new retrieval
sp {evaluate-op*propose*prohibit-bad-epmem*bad-match-on-retrieved-action
   (state <s> ^name evaluate-op
             -^epmem-op-match true
              ^epmem <epmem>)
   (<epmem> ^result <ep-res>
            ^command <ep-cmd>)
   (<ep-res> ^memory-id <mem-id>
             ^retrieval-count 1)
   (<ep-cmd> -^prohibit <mem-id>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name prohibit-bad-epmem
         ^mem-id <mem-id>
         ^reason |does not match to-be-evaluated operator|)
}

#IF a first retrieved epmem does not contain
#   a score for me and my enemy
#THEN prohibit the epmem to force
#     a new retrieval
sp {evaluate-op*propose*prohibit-bad-epmem*bad-match-on-retrieved-scores
   (state <s> ^name evaluate-op
              ^io.input-link <il>
              ^epmem.result <ep-res>)
   (<ep-res> ^memory-id <mem-id>
             ^retrieval-count 1
             ^retrieved.io.input-link.current-score <ret-cs>)
   (<il> ^my-color <my-color>
         ^smell.color <enemy-color>)
  -{(<ret-cs> ^<my-color>
              ^<enemy-color>)}
-->
   (<s> ^operator <op> + =)
   (<op> ^name prohibit-bad-epmem
         ^mem-id <mem-id>
         ^reason |does not contain score information|)
}


#IF I have an uncertain outcome on a
#   a series of epmems
#THEN prohibit the epmem to force
#     a new retrieval
sp {evaluate-op*propose*prohibit-bad-epmem*uncertain-outcome*with-lne
   (state <s> ^name evaluate-op
              ^recalled-status uncertain
             -^recalled-status certain
              ^epmem.command.query <query>
              ^last-new-epmem <lne>)
   (<lne> ^id <mem-id>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name prohibit-bad-epmem
         ^mem-id <mem-id>
         ^reason |led to uncertain outcome|)
}

#IF I have an uncertain outcome on a
#   a first epmem retrieved
#THEN prohibit the epmem to force
#     a new retrieval
sp {evaluate-op*propose*prohibit-bad-epmem*uncertain-outcome*no-lne
   (state <s> ^name evaluate-op
              ^recalled-status uncertain
             -^recalled-status certain
              ^epmem <epmem>
             -^last-new-epmem)
   (<epmem> ^command.query <query>
            ^result.memory-id <mem-id>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name prohibit-bad-epmem
         ^mem-id <mem-id>
         ^reason |led to uncertain outcome|)
}

#Prohibiting an epmem trumps recording info from an epmem
sp {evaluate-op*compare*prohibit-bad-epmem*limit-not-reached
   (state <s> ^name evaluate-op
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name prohibit-bad-epmem)
   (<op2> ^name record-recalled-combat-status)
-->
   (<s> ^operator <op1> > <op2>)
}

#Add the prohibit command (for first epmems)
sp {apply*prohibit-bad-epmem*prohibit-command*first-epmems
   (state <s> ^operator <op>
              ^epmem.command <ep-cmd>)
   (<op> ^name prohibit-bad-epmem
         ^mem-id <mem-id>
         ^reason <reason>)
-->
   (<ep-cmd> ^prohibit <mem-id>)
   (write (crlf) |Prohibiting epmem #| <mem-id> | because it | <reason> |.|)
}

#Remove any last-new-epmem info
sp {apply*prohibit-bad-epmem*remove-old-lne
   (state <s> ^operator.name prohibit-bad-epmem
              ^last-new-epmem <lne>)
-->
   (<s> ^last-new-epmem <lne> -)
}

#Initialize the prohibit count
#(this is a count of how many prohibit commands
# have been issued so far)
sp {apply*prohibit-bad-epmem*init-prohibit-count
   (state <s> ^operator <op>
             -^prohibit-count)
   (<op> ^name prohibit-bad-epmem)
-->
   (<s> ^prohibit-count 1)
   (write (crlf) |prohibit count = 1|)
}

#Increment the prohibit count
sp {apply*prohibit-bad-epmem*increment-prohibit-count
   (state <s> ^operator <op>
              ^prohibit-count <n> < 3)
   (<op> ^name prohibit-bad-epmem)
-->
   (<s> ^prohibit-count <n> -
        ^prohibit-count (+ 1 <n>))
   (write (crlf) |prohibit count = | (+ 1 <n>))
}
