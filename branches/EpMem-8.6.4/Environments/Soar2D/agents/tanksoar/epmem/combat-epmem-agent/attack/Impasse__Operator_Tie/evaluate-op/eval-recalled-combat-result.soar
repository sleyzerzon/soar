#IF I have determined with certainty my status
#   at the end of a recalled combat
#AND I haven't reported an recalled result on
#    this action action
#THEN report a recalled result
sp {evaluate-op*propose*eval-recalled-combat-result
   (state <s> ^name evaluate-op
              ^recalled-status certain
              ^eval-name <name>
              ^eval-direction <dir>
              ^last-new-epmem.id)
-->
   (<s> ^operator <op> + =)
   (<op> ^name eval-recalled-combat-result)
}


#This operator trumps recording more info
sp {evaluate-op*compare*eval-recalled-combat-result
   (state <s> ^name evaluate-op
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name eval-recalled-combat-result)
   (<op2> ^name record-recalled-combat-status)
-->
   (<s> ^operator <op1> > <op2>)
}

#IF the operator has been selected
#AND no aggregate evaluation exists
#THEN create an initial evaluation
sp {apply*eval-recalled-combat-result*initial-eval
   (state <s> ^operator.name eval-recalled-combat-result
             -^evaluation)
-->
   (<s> ^evaluation <eval>)
   (<eval> ^value-sum 0
           ^num-evals 0
           ^avg-value 0)
}

#IF the operator has been selected
#AND I've recorded a certain result
#THEN report it
#AND prohibit future use of the memory that
#    it came from
#AND remove the record of having retrieved it
sp {apply*eval-recalled-combat-result*flight
   (state <s> ^operator.name eval-recalled-combat-result
              ^recalled-status certain
              ^recalled-score-change <score-change>
              ^recalled-evaluation <value>
              ^eval-name <name>
              ^eval-direction <dir>
              ^epmem.command <ep-cmd>
              ^last-new-epmem <lne>
              ^evaluation <eval>)
   (<lne> ^id <mem-id>
          ^cardinality <card>)
   (<eval> ^value-sum <old-val>
           ^num-evals <old-num>)
-->
   (<s> ^evaluation <eval> -
        ^evaluation <new-eval>)
   (<new-eval> ^value-sum (+ <old-val> (* <card> <value>))
               ^num-evals (+ <old-num> 1)
               ^avg-value (/ (+ <old-val> (* <card> <value>)) (+ <old-num> 1))
               ^mem-id <mem-id>)
   (<ep-cmd> ^prohibit <mem-id>)
   (<s> ^last-new-epmem <lne> -
        ^recalled-status certain -
        ^recalled-score-change <score-change> -
        ^recalled-evaluation <value> -)
   (write (crlf) |EVALUATION of | <name> | | <dir> |:  | <value>)
   (write (crlf) |----------------------------------------------|)
}

#IF the operator is selected
#THEN cleanup old data structures
sp {apply*eval-recalled-combat-result*cleanup-enemy-score
   (state <s> ^operator.name eval-recalled-combat-result
              ^enemy-score <score>)
-->
   (<s> ^enemy-score <score> -)
}

#Ibid
sp {apply*eval-recalled-combat-result*cleanup-my-score
   (state <s> ^operator.name eval-recalled-combat-result
              ^my-score <score>)
-->
   (<s> ^my-score <score> -)
}



