#Note the recency of any retrieved memory
sp {epmem*elaborate-recency
   (state <s> ^epmem.result <ep-res>)
   (<ep-res> ^memory-id <mem-id>
            ^present-id <now-id>)
-->
   (<ep-res> ^recency (- <now-id> <mem-id>))
}


#Note which clock tick predicted the
#current situation
sp {elaborate*meta-eval-time
   (state <s> ^name tanksoar
              ^io.input-link.clock <n>)
-->
   (<s> ^meta-eval-time (- <n> 10))
}

##Elaborate pending meta-evaluations with
#the change in score since the prediction
sp {elaborate*pending-meta-eval*curr-value
   (state <s> ^pending-meta-evals.pme <pme>
              ^io.input-link <il>)
   (<il> ^my-color <my-color>
         ^smell.color <enemy-color>
         ^current-score <cs>)
   (<cs> ^<enemy-color> <enemy-score>
         ^<my-color> <my-score>)
   (<pme> ^my-score <my-prev-score>
          ^enemy-score <enemy-prev-score>)
-->
   (<pme> ^curr-value (+ (- <my-score> <my-prev-score>)
                         (- <enemy-prev-score> <enemy-score>)))
}


