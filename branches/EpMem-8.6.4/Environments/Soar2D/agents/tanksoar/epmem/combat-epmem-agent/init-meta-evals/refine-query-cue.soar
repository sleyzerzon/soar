#IF the retrieval failed
#THEN propose trying a different cue
sp {init-meta-evals*propose*refine-query-cue
   (state <s> ^name init-meta-evals
              ^epmem <epmem>)
   (<epmem> ^command.query.{ <cue-attr> << health energy missiles >> } <qval>
            ^result.retrieved no-memory)
-->
   (<s> ^operator <op> + =)
   (<op> ^name refine-query-cue
         ^<cue-attr> <qval>)
   (write (crlf) |No luck with ^| <cue-attr> | | <qval>)
}

sp {apply*refine-query-cue*high-to-moderate
   (state <s> ^operator <op>
              ^epmem.command.query <query>)
   (<op> ^name refine-query-cue
         ^<cue-attr> high)
   (<query> ^<cue-attr> high)
-->
   (<query> ^<cue-attr> high -
            ^<cue-attr> moderate)
}

sp {apply*refine-query-cue*moderate-to-low
   (state <s> ^operator <op>
              ^epmem.command.query <query>)
   (<op> ^name refine-query-cue
         ^<cue-attr> moderate)
   (<query> ^<cue-attr> moderate)
-->
   (<query> ^<cue-attr> moderate -
            ^<cue-attr> low)
}

sp {apply*refine-query-cue*low-health-to-high-energy
   (state <s> ^operator <op>
              ^epmem.command.query <query>)
   (<op> ^name refine-query-cue
         ^health low)
   (<query> ^health low)
-->
   (<query> ^health low -
            ^energy high)
}

sp {apply*refine-query-cue*low-energy-to-high-missiles
   (state <s> ^operator <op>
              ^epmem.command.query <query>)
   (<op> ^name refine-query-cue
         ^energy low)
   (<query> ^energy low)
-->
   (<query> ^energy low -
            ^missiles high)
}

sp {apply*abort-on-missiles-low
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name refine-query-cue
         ^missiles low)
-->
   (<ts> ^meta-evaluations <mes>
         ^pending-meta-evals <pmes>)
}


