################################
### Propose Turn operator if blocked
################################

sp {wander*propose*turn
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^ { << left right >> <dir> } no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn 
        ^actions <a>)
   (<a> ^rotate.direction <dir>
        ^radar.switch on
        ^radar-power.setting 13)}

sp {wander*propose*turn*backward
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^left yes ^right yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name turn 
        ^actions.rotate.direction left)}


#%%%12 Sep 06
#To avoid infinite loops w/ other agent I've added
#a 10% chance of turning instead
sp {wander*propose*turn*random
   (state <s> ^name wander
              ^io.input-link <il>)
   (<il> ^random < 0.1
         ^blocked.forward no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name random-turn 
        ^actions <a>)
   (<a> ^rotate.direction left
        ^radar.switch on
        ^radar-power.setting 13)
}
