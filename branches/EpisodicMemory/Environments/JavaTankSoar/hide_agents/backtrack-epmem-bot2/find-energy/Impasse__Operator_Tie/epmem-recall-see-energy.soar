#IF I have not attempted to retrieve a memory
#   and I have not found the energy charger
#THEN find out if I've seen the charger

sp {recall-energy-path*propose*epmem-recall-see-energy
   (state <s> ^name recall-energy-path
              ^io.input-link.energyrecharger no
              ^epmem.retrieved <ret>)
   (<ret> -^no-retrieval true
          -^io)
-->
   (<s> ^operator <op> + =)
   (<op> ^name epmem-recall-see-energy)
}

#It's better to retrieve an epmem before
#deciding where to go.
sp {recall-energy-path*compare*epmem-recall-see-energy
   (state <s> ^name recall-energy-path
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name epmem-recall-see-energy)
   (<op2> ^name move)
-->
   (<s> ^operator <op1> > <op2>)
}

#IF the operator has been selected
#THEN cue for a memory of seeing the energy
#     charger from this position before.
sp {apply*epmem-recall-see-energy
   (state <s> ^operator <op>
              ^epmem.query <query>
              ^io.input-link <il>)
   (<op> ^name epmem-recall-see-energy)
   (<il> ^x <x>
         ^y <y>)
-->
   (<query> ^io.input-link <qil>)
   (<qil> 
          ^x <x>
          ^y <y>
          ^radar.energy.position center
#          ^radar.energy.position left
#          ^radar.energy.position right
                                 )
}



