echo "\nLoading attack/slide.soar "
## Under attack so move out of the way and turn off shields

sp {attack*propose*slide-out-of-attack
   (state <s> ^idirection <dir>
              ^side-direction.<dir> <ndir>
             -^idirection <ndir>
              ^io.input-link.blocked.<ndir> no)
   -->
   (write (crlf) |Move out of the way of an attack: | <ndir>)
   (<s> ^operator <o> + =, >)
   (<o> ^name slide-out-of-attack
        ^attack-direction <dir>
        ^direction <ndir>
        ^actions <a>)
   (<a> ^move.direction <ndir>
        ^shields.switch off
        ^radar.switch off)}

sp {attack*apply*slide-out-of-attack
   (state <s> ^operator <o>
              ^io.input-link.radar <r>)
   (<r> ^tank <t>)
   (<t> ^position center
        ^distance <d>)
 -{(<r> ^obstacle <ob>)
   (<ob> ^position <pos>
         ^distance <= <d>)}
   (<o> ^name slide-out-of-attack
        ^attack-direction forward
        ^actions <a>
        ^direction <pos>)
   -->
   (write (crlf) |Shoot missile on slide.|)
   (<a> ^fire.weapon missile)}

sp {attack*apply*slide-out-of-attack2
   (state <s> ^operator <o>
              ^square <square>
              ^opposite-direction.<d> <od>)
   (<o> ^name slide-out-of-attack
        ^attack-direction <attack>
        ^direction <d>)
   -->
   (write (crlf) |Slide away for later pop back to re-attack: | <od> | Attack dir:| <attack>)
   (<s> ^saved-slide-direction <x>)
   (<x> ^direction <od>
        ^avoid-square <square>
        ^attack-direction <attack>)}

sp {attack*died*remove*saved-slide-direction
   :o-support
   (state <s> ^name tanksoar
              ^saved-slide-direction <x>
              ^io.input-link.resurrect yes)
-->
   (<s> ^saved-slide-direction <x> -)}  

sp {attack*apply*slide-out-of-attack*radar*distance
   (state <s> ^operator <o>
              ^io.input-link.radar.tank.distance <dist>
              ^saved-slide-direction <x>)
   (<o> ^name slide-out-of-attack
        ^attack-direction forward)
  -(<x> ^distance)
   -->
   (<x> ^distance <dist>)}

sp {attack*apply*slide-out-of-attack*smell*distance
   (state <s> ^operator <o>
             -^io.input-link.radar.tank
              ^io.input-link.smell.distance <dist>
              ^saved-slide-direction <x>)
   (<o> ^name slide-out-of-attack)
  -(<x> ^distance)
   -->
   (<x> ^distance <dist>)}

sp {attack*apply*slide-out-of-attack*smell*distance*not-forward
   (state <s> ^operator <o>
              ^io.input-link.smell.distance <dist>
              ^saved-slide-direction <x>)
   (<o> ^name slide-out-of-attack
        ^attack-direction <> forward)
  -(<x> ^distance)
   -->
   (<x> ^distance <dist>)}

sp {attack*apply*slide-out-of-attack*time-to-hit
   (state <s> ^operator <o>
              ^io.input-link.clock <clock>
              ^saved-slide-direction <x>)
   (<o> ^name slide-out-of-attack)
   (<x> ^distance <dist>
       -^hit-time)
    -->
   (write (crlf) |Hit Time |  (+ <clock> (/ <dist> 1.4)) | Clock: | <clock>)
   (<x> ^hit-time  (+ <clock> (/ <dist> 1.4))
        ^remove-hit-time (+ 1 (+ <clock> (/ <dist> 1.4))))}

sp {attack*select*slide-out-of-attack
   (state <s> ^operator <o1> + <o2> +)
   (<o1> ^name << slide-out-of-attack move-forward-fire move-fire-to-opening >>)
   (<o2> ^name fire-missile)
   -->
   (<s> ^operator <o1> > <o2>)}


############ TURN TO EARLIER SLIDE THREAT

################################
### Propose Turn operator if rwaves is not from front
################################
sp {attack*propose*turn-after-slide*lr*forward
   (state <s> ^saved-slide-direction <ssd>
              ^opposite-direction.<tdir> <odir>)
   (<ssd> ^attack-direction { << left right >> <tdir>}
          ^direction forward)
   -->
   (write (crlf) |Turn to face: | <tdir> )
   (<s> ^operator <o> + =, >)
   (<o> ^name turn-after-slide
        ^new-direction <odir>
        ^actions.rotate.direction <tdir>)}

sp {attack*propose*turn-after-slide*lr*backward
   (state <s> ^saved-slide-direction <ssd>)
   (<ssd> ^attack-direction { << left right >> <tdir>}
          ^direction backward)
   -->
   (write (crlf) |Turn to face: | <tdir> )
   (<s> ^operator <o> + =, >)
   (<o> ^name turn-after-slide
        ^new-direction <tdir>
        ^actions.rotate.direction <tdir>)}

sp {attack*propose*turn-after-slide*backward
   (state <s> ^saved-slide-direction <ssd>)
   (<ssd> ^attack-direction backward
          ^direction <odir>)
   -->
   (write (crlf) |Turn to face: | <odir>)
   (<s> ^operator <o> + =, >)
   (<o> ^name turn-after-slide
        ^new-direction forward
        ^actions.rotate.direction <odir>)}

sp {attack*apply*turn*from-attack*correct
   (state <s> ^saved-slide-direction <ssd>
              ^operator <o>)
   (<ssd> ^direction <odir>)
   (<o> ^name turn-after-slide
        ^new-direction <ndir>)
   -->
   (write (crlf) |Correct saved-direction | <odir> | --> | <ndir>)
   (<ssd> ^direction <odir> -
          ^direction <ndir>)}


########## SLIDE AROUND CORNER OR BACK INTO FIGHT

sp {attack*propose*slide-at-corner
   (state <s> ^saved-slide-direction <x>
              ^io.input-link <il>)
   (<il> ^blocked.<d> no
         ^clock <clock>)
   (<x> ^direction <d>
       -^hit-time > <clock>)
   -->
   (write (crlf) |Slide back : | <d>)
   (<s> ^operator <o> + =, >)
   (<o> ^name slide-attack
        ^actions <a>)
   (<a> ^fire.weapon missile
        ^move.direction <d>
        ^radar.switch on
        ^shields.switch on)}

sp {attack*remove*saved-slide-direction*old
   :o-support
   (state <s> ^saved-slide-direction <x>
              ^io.input-link <il>)
   (<il> ^clock <clock>)
   (<x> ^remove-hit-time < <clock>)
   -->
   (write |Remove old slide structure.|)
   (<s> ^saved-slide-direction <x> -)}

sp {attack*select*slide-at-corner
   (state <s> ^operator <o1> + <o2> +)
   (<o1> ^actions.fire)
   (<o2> ^name record-sound)
   -->
   (<s> ^operator <o1> > <o2>)}

# Remove saved-slide-direction
sp {attack*apply*slide-at-corner  
   (state <s> ^saved-slide-direction <d>
              ^operator.name slide-attack)
   -->
   (write (crlf) |Remove slide direction.|)
   (<s> ^saved-slide-direction <d> -)}

########### DO NOT MOVE INTO SLIDE AREA ##########
sp {attack*select*avoid-move*slide-square
   (state <s> ^saved-slide-direction.square <sq>
              ^operator <o>
              ^dir.<dir>.<tdir> <ndir>
              ^square.<ndir> <sq>
              ^io.input-link.direction <dir>)
   (<o> ^actions.move.direction <tdir>)
   -->
   (write |*** AVOID MOVING INTO DANGER AREA |)
   (<s> ^operator <o> -)}

########RETREAT
sp {attack*propose*slide-at-corner*retreat
   (state <s> ^saved-slide-direction <x>
              ^opposite-direction.<d> <od>
              ^io.input-link <il>)
   (<il> ^blocked.<od> no
         ^energy 0)
   (<x> ^direction <d>)
   -->
   (write (crlf) |Retreat Slide away : | <od>)
   (<s> ^operator <o> + =, >)
   (<o> ^name slide-away
        ^actions <a>)
   (<a> ^move.direction <od>)}

####################################

sp {attack*propose*slide-away-from-death
   (state <s> ^about-to-die *yes*
              ^io.input-link <il>
	      ^about-to-die *yes*
              ^square.<dir>.open *yes* 
              ^side-direction.forward <ndir>)
   (<il> ^direction <dir>
         ^smell.distance 1
         ^blocked <b>)
   (<b> ^forward yes
        ^<ndir> no)
   -->
   (write (crlf) |Move out of way before death.|)
   (<s> ^operator <o> + =, >)
   (<o> ^name slide-from-death
        ^actions <a>)
   (<a> ^move.direction <ndir>)}

sp {attack*apply*slide-from-death
   (state <s> ^operator <o>
              ^io.input-link.missiles > 0)
   (<o> ^name slide-from-death
        ^actions <a>)
   -->
   (write (crlf) |Shoot missile on slide.|)
   (<a> ^fire.weapon missile)}
