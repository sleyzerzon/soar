echo "\nLoading all/radar.soar "

################################
### Turn radar directly operator
################################

sp {all*radar-off
   (state <s> ^name tanksoar
             -^sound
             -^tank-detected yes
              ^io.input-link.radar-status on
              ^io.output-link <ol>)
  -{(<s> ^io.output-link.radar <r>)
    (<r> -^status complete ^switch on)}
-->
   (<ol> ^radar.switch off)}

### Set Radar based on prior history
sp {all*radar-power*distance-to-obstacle*turn
   (state <s> ^operator <o>
              ^square.radar-distances.<nd> <x>)
   (<o> ^new-direction <nd>
        ^actions <a>)
   (<a> ^rotate)
-->
   (write (crlf) | Direction | <nd> |, | <x>)
   (<a> ^radar-power.setting <x>
        ^radar.switch on)
}

sp {all*radar-power*distance-to-obstacle*move
   (state <s> ^operator <o>
              ^io.input-link.direction <dir>
              ^dir.<dir>.<tdir> <ndir>
              ^square.<ndir>.radar-distances.<nd> <x>)
   (<o> ^new-direction <nd>
        ^actions <a>)
   (<a> ^move.direction <tdir>)
-->
   (write (crlf) | Direction | <nd> |, | <x>)
   (<a> ^radar-power.setting <x>
        ^radar.switch on)
}

######  SET RADAR POWER BASED ON DISTANCE TO WALL

sp {all*radar-power*distance-to-edge*east
   (state <s> ^operator <o>
             -^square.radar-distances.east
             -^square.east.obstacle *yes*
              ^io.input-link.x <x>)
   (<o> ^new-direction east
        ^actions <a>)
   (<a> ^rotate)
-->
   (<a> ^radar-power.setting (- 14 <x>)
        ^radar.switch on)
}

sp {all*radar-power*distance-to-edge*west
   (state <s> ^operator <o>
             -^square.radar-distances.west
             -^square.west.obstacle *yes*
              ^io.input-link.x <x>)
   (<o> ^new-direction west
        ^actions <a>)
   (<a> ^rotate)
-->
   (<a> ^radar-power.setting (- <x> 1)
        ^radar.switch on)
}

sp {all*radar-power*distance-to-edge*south
   (state <s> ^operator <o>
             -^square.radar-distances.south
             -^square.south.obstacle *yes*
              ^io.input-link.y <y>)
   (<o> ^new-direction south
        ^actions <a>)
   (<a> ^rotate)
-->
   (<a> ^radar-power.setting (- 14 <y>)
        ^radar.switch on)
}

sp {all*radar-power*distance-to-edge*north
   (state <s> ^operator <o>
             -^square.radar-distances.north
             -^square.north.obstacle *yes*
              ^io.input-link.y <y>)
   (<o> ^new-direction north
        ^actions <a>)
   (<a> ^rotate)
-->
   (<a> ^radar-power.setting (- <y> 1)
        ^radar.switch on)
}

#############################

sp {all*radar-power*move-to-edge*east
   (state <s> ^operator <o>
              ^dir.east.<tdir> <ndir>
              ^square.<ndir> <ns>)
   (<ns> -^radar-distances.east
          ^x <x>
         -^east.obstacle *yes*)
   (<o> ^new-direction east
        ^actions <a>)
   (<a> ^move.direction <tdir>)
-->
   (<a> ^radar-power.setting (- 14 <x>)
        ^radar.switch on)
}

sp {all*radar-power*move-to-edge*west
   (state <s> ^operator <o>
              ^dir.west.<tdir> <ndir>
              ^square.<ndir> <ns>)
   (<ns> -^radar-distances.west
          ^x <x>
         -^west.obstacle *yes*)
   (<o> ^new-direction west
        ^actions <a>)
   (<a> ^move.direction <tdir>)
-->
   (<a> ^radar-power.setting (- <x> 1)
        ^radar.switch on)
}


sp {all*radar-power*move-to-edge*north
   (state <s> ^operator <o>
              ^dir.north.<tdir> <ndir>
              ^square.<ndir> <ns>)
   (<ns> -^radar-distances.north
          ^y <y>
         -^north.obstacle *yes*)
   (<o> ^new-direction north
        ^actions <a>)
   (<a> ^move.direction <tdir>)
-->
   (<a> ^radar-power.setting (- <y> 1)
        ^radar.switch on)
}


sp {all*radar-power*move-to-edge*south
   (state <s> ^operator <o>
              ^dir.south.<tdir> <ndir>
              ^square.<ndir> <ns>)
   (<ns> -^radar-distances.south
          ^y <y>
         -^south.obstacle *yes*)
   (<o> ^new-direction south
        ^actions <a>)
   (<a> ^move.direction <tdir>)
-->
   (<a> ^radar-power.setting (- 14 <y>)
        ^radar.switch on)
}


sp {monitor-radar
   (state <s> ^io.input-link.radar-setting <x>)
-->
   (write (crlf) |Radar set to: | <x>)
   }




