echo "\nLoading attack/fire-missile.soar "

###Propose Fire Operator
##  If the state is attack and there is a tank on radar in the center,
##  then propose the fire missile operator. 

sp {attack*propose*fire-missile
   (state <s> ^name attack
              ^io.input-link <il>)
   (<il> ^radar.tank.position center
         ^missiles <m>)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name fire-missile
        ^actions.fire.weapon missile)}

sp {attack*propose*fire-missile*mongo
   (state <s> ^name attack
              ^blocking-obstacle <dir>
             -^operator.name fire-missile
              ^superstate.missile-number mongo
             -^opening *yes*)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name fire-missile
        ^actions.fire.weapon missile)}

sp {attack*propose*fire-missile*lots
   (state <s> ^name attack
              ^blocking-obstacle <dir>
              ^superstate.missile-number lots
              ^io.input-link.random > .5
             -^operator.name fire-missile
             -^opening *yes*)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name fire-missile
        ^actions.fire.weapon missile)}
