echo "\nLoading attack/elaborations.soar"


sp {attack*elaborate*blocking*obstacle
   (state <s> ^name attack
              ^io.input-link.radar <r>)
   (<r> ^tank <t>
       -^tank.position center
        ^obstacle <ob>)        
   (<t> ^position { << left right >> <dir> }
        ^distance <d>) 
   (<ob> ^position <dir>
         ^distance < <d>)
   -->
   (<s> ^blocking-obstacle <dir>)}

sp {attack*elaborate*opening*ahead
   (state <s> ^name attack
              ^io.input-link.radar <r>)
   (<r> ^tank.distance <d>
        ^open <op>)   
   (<op> ^position << left right >>
         ^distance { > 0 < <d> })
   -->
   (<s> ^opening *yes*)}

sp {attack*elaborate*shields-on
   :i-support
   (state <s> ^name attack
              ^operator.actions <a>
              ^io.input-link.shield-status off)
   -->
   (<a> ^shields.switch on)}


