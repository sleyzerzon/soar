echo "\nLoading complex/elaborations.soar"

sp {elaborate*state*top-state
   (state <s> ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)}

sp {elaborate*state*io
   (state <s> ^superstate.io <io>)
-->
   (<s> ^io <io>)}

sp {elaborate*state*operator*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)}

## These are used in retreat/move.soar
sp {elaborate*sidestep-directions
   (state <s> ^name tanksoar)
   -->
   (<s> ^side-direction <sd>)
   (<sd> ^forward right left 
         ^backward right left
         ^right forward backward 
         ^left forward backward)}

sp {elaborate*directions
    (state <s> ^superstate nil)
    -->
    (<s> ^direction-map <tns> 
         ^radar-map <dirp> 
         ^opposite-direction <opp>
         ^maze-size 14)
    (<tns> ^north <north>
           ^south <south>
           ^west <west>
           ^east <east>)
    (<north> ^right east  ^left west  ^backward south ^forward north)
    (<south> ^right west  ^left east  ^backward north ^forward south)
    (<west>  ^right north ^left south ^backward east  ^forward west)
    (<east>  ^right south ^left north ^backward west  ^forward east)
    (<dirp> ^north <northp>
            ^south <southp>
            ^west <westp>
            ^east <eastp>)
    (<northp> ^center <cr> ^right <nr> ^left <nl> ^sx 0 ^sy -1)
    (<southp> ^center <cr> ^right <sr> ^left <sl> ^sx 0 ^sy 1)
    (<westp>  ^center <cr> ^right <wr> ^left <wl> ^sx -1 ^sy 0)
    (<eastp>  ^center <cr> ^right <er> ^left <el> ^sx 1 ^sy 0)
    (<cr>  ^x   0 ^y  0)
    (<nr>  ^x   1 ^y  0)
    (<nl>  ^x  -1 ^y  0)
    (<sr>  ^x  -1 ^y  0)
    (<sl>  ^x   1 ^y  0)
    (<wr>  ^x   0 ^y  -1)
    (<wl>  ^x   0 ^y   1)
    (<er>  ^x   0 ^y   1)
    (<el>  ^x   0 ^y  -1)
    (<opp> ^forward backward
           ^backward forward
           ^left right
           ^right left)
}

############# Sensor Data #################

sp {elaborate*state*under-attack*direction
   (state <s> ^name tanksoar
              ^io.input-link.incoming.<dir> yes)
   -->
   (<s> ^idirection <dir>)}

sp {elaborate*state*radiated
   (state <s> ^name tanksoar
              ^io.input-link.rwaves.<dir> yes)
   -->
   (<s> ^radiated *yes*)}

##### Classify missile numbers

sp {elaborate*state*mongo-missiles
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^missiles < 22)
   -->
   (<s> ^missile-number mongo)}

sp {elaborate*state*lots-o-missiles
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^missiles < 8)
   -->
   (<s> ^missile-number lots)}

sp {elaborate*state*some-missiles
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^missiles >= 8
         -^missiles 0)
   -->
   (<s> ^missile-number some)}

sp {elaborate*state*none
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^missiles 0)
   -->
   (<s> ^missile-number none)}

sp {elaborate*state*about-to-die
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^health > 600
         -^energy >= 50)
   -->
   (write (crlf) |******ABOUT TO DIE********|)
   (<s> ^about-to-die *yes*)}

#### Detect if on charger and need to charge

sp {elaborate*state*healthcharger-ready
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^health >= 970
          ^healthrecharger yes)
   -->
   (<s> ^charger-ready *yes*)}

sp {elaborate*state*energycharger-ready
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^energy >= 970
          ^energyrecharger yes)
   -->
   (<s> ^charger-ready *yes*)}

#### Enemy Tank Detected

sp {elaborate*tank-detected
   (state <s> ^name tanksoar
              ^io.input-link.radar.tank)
   -->
   (<s> ^tank-detected *yes*)}

sp {elaborate*tank-detected*smell
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^smell.distance 1)
   -->
   (<s> ^tank-detected *yes*)}

###### Chaseable Enemy tank

sp {elaborate*chaseable
   (state <s> ^name tanksoar
             -^really-need-energy *yes*
             -^missile-number none
             -^tank-detected *yes*)
  -->
  (<s> ^chaseable *yes*)}