#IF I have recorded path to the energy charger
#AND I haven't performed this query already along
#     the path's axis (x or y)
#AND the ^epmem.queue link is empty (ready for a new query)
#THEN Attempt to retrieve an epmem of me seeing
#     the partial path's target position from here
sp {recall-energy-path*propose*epmem-recall-see-energy-path
   (state <s> ^name recall-energy-path
              ^top-state.energy-path <path>
              ^io.input-link <il>
             -^tried-path <path>
             -^epmem.query.<anything>)
   (<il> ^x <x>
         ^y <y>)
   (<path> ^x <x>
           ^y <path-y> <> <y>
           ^distance <dist>
           ^distance-from-here <dfh>)
-->
   (<s> ^operator <op> +)
   (<op> ^name epmem-recall-see-energy-path
         ^path <path>
         ^distance <dfh>
         ^total-distance (+ <dfh> <dist>))
}

#as above, except y-axis
sp {recall-energy-path*propose*epmem-recall-see-energy-path*y-axis
   (state <s> ^name recall-energy-path
              ^top-state.energy-path <path>
              ^io.input-link <il>
             -^tried-path <path>
             -^epmem.query.<anything>)
   (<il> ^x <x>
         ^y <y>)
   (<path> ^x <path-x> <> <x>
           ^y <y>
           ^distance <dist>
           ^distance-from-here <dfh>)
-->
   (<s> ^operator <op> +)
   (<op> ^name epmem-recall-see-energy-path
         ^path <path>
         ^distance <dfh>
         ^total-distance (+ <dfh> <dist>))
}

#IF the operator has been proposed
#AND it's for a location East of here
#THEN elaborate the path with that direction
sp {epmem-recall-see-energy-path*elaborate*direction*east
   (state <s> ^operator <op> +
              ^io.input-link <il>)
   (<op> ^name epmem-recall-see-energy-path
        ^path <path>)
   (<il> ^x <x>
         ^y <y>)
   (<path> ^x > <x>
           ^y <y>)
-->
   (<op> ^direction east)
}

#As above, except West
sp {epmem-recall-see-energy-path*elaborate*direction*west
   (state <s> ^operator <op> +
              ^io.input-link <il>)
   (<op> ^name epmem-recall-see-energy-path
        ^path <path>)
   (<il> ^x <x>
         ^y <y>)
   (<path> ^x < <x>
           ^y <y>)
-->
   (<op> ^direction west)
}

#As above, except South
sp {epmem-recall-see-energy-path*elaborate*direction*south
   (state <s> ^operator <op> +
              ^io.input-link <il>)
   (<op> ^name epmem-recall-see-energy-path
        ^path <path>)
   (<il> ^x <x>
         ^y <y>)
   (<path> ^x <x>
           ^y > <y>)
-->
   (<op> ^direction south)
}

#As above, except North
sp {epmem-recall-see-energy-path*elaborate*direction*north
   (state <s> ^operator <op> +
              ^io.input-link <il>)
   (<op> ^name epmem-recall-see-energy-path
        ^path <path>)
   (<il> ^x <x>
         ^y <y>)
   (<path> ^x <x>
           ^y < <y>)
-->
   (<op> ^direction north)
}

#IF the query would result in a path that doubles
#   back on itself 
#THEN reject it
sp {recall-energy-path*compare*epmem-recall-see-energy-path*reject-backtrack
   (state <s> ^name recall-energy-path
              ^top-state.opposite.<dir> <opp-dir>
              ^operator <op> +)
   (<op> ^name epmem-recall-see-energy-path
         ^direction <dir>
         ^path.abs-dir <opp-dir>)
-->
   (<s> ^operator <op> -)
}

#IF multiple paths queries have been proposed
#AND neither of them is for the opposite direction
#THEN prefer the one with the shortest overall distance
sp {recall-energy-path*compare*epmem-recall-see-energy-path*prefer-short-dist
   (state <s> ^name recall-energy-path
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name epmem-recall-see-energy-path
          ^total-distance <dist>)
   (<op2> ^name epmem-recall-see-energy-path
          ^total-distance <dist2> > <dist>)
-->
   (<s> ^operator <op1> > <op2>)
}

#IF multiple paths queries have been proposed
#THEN paths with equal overall distance are of equal value
sp {recall-energy-path*compare*epmem-recall-see-energy-path*equal-dist
   (state <s> ^name recall-energy-path
              ^operator <op1> +
                        <op2> + <> <op1> +)
   (<op1> ^name epmem-recall-see-energy-path
          ^total-distance <dist>)
   (<op2> ^name epmem-recall-see-energy-path
          ^total-distance <dist>)
-->
   (<s> ^operator <op1> = <op2>)
}


#IF the operator has been selected
#THEN cue for a memory of seeing the previously 
#     path's position from this position
sp {apply*epmem-recall-see-energy-path
   (state <s> ^operator <op>
              ^io.input-link <il>
              ^epmem.query <query>)
   (<il> ^x <x>
         ^y <y>)
   (<op> ^name epmem-recall-see-energy-path
         ^direction <abs-dir>
         ^distance <dist>)
-->
   (<query> ^io.input-link <qil>)
   (<qil> ^x <x>
          ^y <y>
          ^direction <abs-dir>
          ^radar-status on
          ^radar-setting 13)
   (write (crlf) |QUERY:  Can I see the position |<dist>| spaces |<abs-dir>| of me?|)
}

#IF the operator has been selected
#THEN note that I've attempted this query already
sp {apply*epmem-recall-see-energy-path*note-complete
   (state <s> ^operator <op>)
   (<op> ^name epmem-recall-see-energy-path
        ^path <path>)
-->
   (<s> ^tried-path <path>)
}

#IF the operator has been selected
#THEN note which path the current epmem 
#     query refers to
sp {apply*epmem-recall-see-energy-path*note-lastest-path
   (state <s> ^operator <op>
              -^latest-path <old-path>)
   (<op> ^name epmem-recall-see-energy-path
         ^path <path>)
-->
   (<s> ^latest-path <path>)
}

sp {apply*epmem-recall-see-energy-path*replace-lastest-path
   (state <s> ^operator <op>
              ^latest-path <old-path>)
   (<op> ^name epmem-recall-see-energy-path
         ^path <path> <> <old-path>)
-->
   (<s> ^latest-path <old-path> -
        ^latest-path <path>)
}

