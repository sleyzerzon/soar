#IF I have not attempted to retrieve a memory of
#   seeing the energy charger from a position 
#   nearby along the x-axis
#AND there isn't a current query in place
#THEN propose performing that retrieval
sp {recall-energy-path*propose*epmem-recall-see-energy-nearby*x-axis
   (state <s> ^name recall-energy-path
             -^epmem.query.io
              ^io.input-link.x <x>
              -^epmem-recall-see-energy-nearby x-complete)
-->
   (<s> ^operator <op> +)
   (<op> ^name epmem-recall-see-energy-nearby
         ^axis x
         ^x <x>)
}

#as above except y-axis
sp {recall-energy-path*propose*epmem-recall-see-energy-nearby*y-axis
   (state <s> ^name recall-energy-path
             -^epmem.query.io
              ^io.input-link.y <y>
             -^epmem-recall-see-energy-nearby y-complete)
-->
   (<s> ^operator <op> +)
   (<op> ^name epmem-recall-see-energy-nearby
         ^axis y
         ^y <y>)
}

sp {recall-energy-path*compare*epmem-recall-see-energy-nearby
   (state <s> ^name recall-energy-path
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name epmem-recall-see-energy-nearby)
   (<op2> ^name epmem-recall-see-energy-nearby)
-->
   (<s> ^operator <op1> = <op2>)
}


#IF the operator has been selected
#THEN cue for a memory of seeing the energy
#     charger from this position before.
sp {apply*epmem-recall-see-energy-nearby
   (state <s> ^operator <op>
              ^epmem.query <query>
              ^io.input-link <il>)
   (<op> ^name epmem-recall-see-energy-nearby
         ^axis <axis>
         ^<axis> <val>)
   (<il> ^x <x>
         ^y <y>)
-->
   (<query> ^io.input-link <qil>)
   (<qil> 
          ^<axis> <val>
          ^radar.energy.position center
#          ^radar.energy.position left
#          ^radar.energy.position right
                                 )
}


#IF the operator has been selected
#THEN note that I've attempted this query already
sp {apply*epmem-recall-see-energy-nearby*note-complete*x-axis
   (state <s> ^operator <o>)
   (<o> ^name epmem-recall-see-energy-nearby
        ^axis x)
-->
   (<s> ^epmem-recall-see-energy-nearby x-complete)
}

#as above except y-axis
sp {apply*epmem-recall-see-energy-nearby*note-complete*y-axis
   (state <s> ^operator <o>)
   (<o> ^name epmem-recall-see-energy-nearby
        ^axis y)
-->
   (<s> ^epmem-recall-see-energy-nearby y-complete)
}
