#IF I have not attempted to retrieve a memory
#   of seeing the energy charger from this position
#AND there isn't a current query in place
#THEN propose performing that retrieval
sp {recall-energy-path*propose*epmem-recall-see-energy
   (state <s> ^name recall-energy-path
              -^epmem-recall-see-energy complete
              -^epmem.query.io)
-->
   (<s> ^operator <op> +)
   (<op> ^name epmem-recall-see-energy)
}

#It's better to retrieve an epmem of a direct
#path than a roundabout path.
sp {recall-energy-path*compare*epmem-recall-see-energy
   (state <s> ^name recall-energy-path
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name epmem-recall-see-energy)
   (<op2> ^name << epmem-recall-see-energy-path epmem-recall-see-energy-nearby >>)
-->
   (<s> ^operator <op1> > <op2>)
}

#IF the operator has been selected
#THEN cue for a memory of seeing the energy
#     charger from this position before.
sp {apply*epmem-recall-see-energy
   (state <s> ^operator <op>
              ^epmem.query <query>
              ^io.input-link <il>)
   (<op> ^name epmem-recall-see-energy)
   (<il> ^x <x>
         ^y <y>)
-->
   (<query> ^io.input-link <qil>)
   (<qil> 
          ^x <x>
          ^y <y>
          ^radar.energy.position center
#          ^radar.energy.position left
#          ^radar.energy.position right
                                 )
}

#IF the operator has been selected
#THEN note that I've attempted this query already
sp {apply*epmem-recall-see-energy*note-complete
   (state <s> ^operator.name epmem-recall-see-energy)
-->
   (<s> ^epmem-recall-see-energy complete)
}

