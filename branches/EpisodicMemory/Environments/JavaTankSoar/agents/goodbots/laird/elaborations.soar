echo "\nLoading destroyer/elaborations.soar"

sp {elaborate*task*tanksoar
   (state <s> ^superstate nil)
   -->
   (<s> ^name tanksoar)}

## These are used in retreat/move.soar
sp {elaborate*sidestep-directions
   (state <s> ^name tanksoar)
   -->
   (<s> ^side-direction <sd>
        ^opposite-direction <od>)
   (<sd> ^forward right left ^backward right left
         ^right forward backward ^left forward backward)
   (<od> ^forward backward ^backward forward
         ^right left ^left right)}

sp {elaborate*directions
    (state <s> ^superstate nil)
    -->
    (<s> ^dirlr right left 
         ^dirfb forward backward 
         ^dir <tns> 
         ^dirp <dirp> 
         ^opposite <opp>
         ^maze-size 14)
    (<tns> ^north <north>
           ^south <south>
           ^west <west>
           ^east <east>)
    (<north> ^right east  ^left west  ^backward south ^forward north)
    (<south> ^right west  ^left east  ^backward north ^forward south)
    (<west>  ^right north ^left south ^backward east  ^forward west)
    (<east>  ^right south ^left north ^backward west  ^forward east)
    (<dirp> ^north <northp>
           ^south <southp>
           ^west <westp>
           ^east <eastp>)
    (<northp> ^center <cr> ^right <nr> ^left <nl> ^sx 0 ^sy -1)
    (<southp> ^center <cr> ^right <sr> ^left <sl> ^sx 0 ^sy 1)
    (<westp>  ^center <cr> ^right <wr> ^left <wl> ^sx -1 ^sy 0)
    (<eastp>  ^center <cr> ^right <er> ^left <el> ^sx 1 ^sy 0)
    (<cr>  ^x   0 ^y  0)
    (<nr>  ^x   1 ^y  0)
    (<nl>  ^x  -1 ^y  0)
    (<sr>  ^x  -1 ^y  0)
    (<sl>  ^x   1 ^y  0)
    (<wr>  ^x   0 ^y  -1)
    (<wl>  ^x   0 ^y   1)
    (<er>  ^x   0 ^y   1)
    (<el>  ^x   0 ^y  -1)
    (<opp> ^yes no 
           ^no yes
           ^on off 
           ^off on
           ^forward backward
           ^backward forward
           ^left right
           ^right left)
}

sp {elaborate*state*under-attack*direction
   (state <s> ^name tanksoar
              ^io.input-link.incoming.<dir> yes)
   -->
   (<s> ^idirection <dir>)}

sp {elaborate*state*radiated
   (state <s> ^name tanksoar
              ^io.input-link.rwaves.<dir> yes)
   -->
   (<s> ^radiated yes)}
##### Classify missiles

sp {elaborate*state*mongo-missiles
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^missiles < 22)
   -->
   (<s> ^missile-number mongo)}

sp {elaborate*state*lots-o-missiles
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^missiles < 8)
   -->
   (<s> ^missile-number lots)}

sp {elaborate*state*some-missiles
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^missiles >= 8
         -^missiles 0)
   -->
   (<s> ^missile-number some)}

sp {elaborate*state*none
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^missiles 0)
   -->
   (<s> ^missile-number none)}

###ABOUT TO DIE

sp {elaborate*state*about-to-die
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^health > 600
         -^energy >= 50)
   -->
   (write (crlf) |******ABOUT TO DIE********|)
   (<s> ^about-to-die *yes*)}

#### Detect if on charger and need to charge

sp {elaborate*state*healthcharger-ready
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^health >= 970
          ^healthrecharger yes)
   -->
   (<s> ^charger-ready yes)}

sp {elaborate*state*energycharger-ready
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> -^energy >= 970
          ^energyrecharger yes)
   -->
   (<s> ^charger-ready yes)}

sp {elaborate*tank-detected
   (state <s> ^name tanksoar
              ^io.input-link.radar.tank)
   -->
   (<s> ^tank-detected yes)}

sp {elaborate*tank-detected*smell
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^smell.distance 1)
   -->
   (<s> ^tank-detected yes)}

sp {top-ps*monitor*radar-outputlink
   (state <s> ^name tanksoar
              ^io.output-link.radar <r>)
   (<r> ^switch <x>)
-->
   (write (crlf) | Radar Switch: | <r> |: | <x> )
}


sp {top-ps*monitor*resurrected
   (state <s> ^name tanksoar
              ^io.input-link.resurrect yes)
-->
   (write (crlf) |******** JUST DIED ********|)}  


