echo "\nLoading attack/target-directly-behind.soar"


## if enemy is behind you, first try and move to left or right, out of missile range.
sp {propose*attack*target-directly-behind*sidestep
   (state <s> ^name attack*target-directly-behind
              ^io.input-link.blocked <b>)
   (<b> ^{ << left right >> <dir> } no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name sidestep-rear-enemy
        ^actions <a>)
   (<a> ^move.direction <dir>)
}


## if enemy is behind you, first try and move to left or right, out of missile range.
sp {propose*attack*target-directly-behind*turn
   (state <s> ^name attack*target-directly-behind)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn-towards-rear-enemy
        ^actions <a>)
   (<a> ^rotate.direction left)          ## default direction
}



sp {select*attack*target-directly-behind*prefer-slide
   (state <s> ^operator <oA> +
              ^operator <oB> +)
   (<oA> ^name turn-towards-rear-enemy)
   (<oB> ^name sidestep-rear-enemy)
-->
   (<s> ^operator  <oA>  <  <oB>)
}
