#IF an evaluation structure for an out of date 
#x-y position is still on the top-state.
#THEN propose removing it
sp {find-energy*propose*remove-stale-evaluation*x
   (state <s> ^name find-energy
              ^top-state.evaluation <eval>
              ^io.input-link.x <x>)
   (<eval> -^x <x>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name remove-stale-evaluation
         ^evaluation <eval>)
}

sp {find-energy*propose*remove-stale-evaluation*y
   (state <s> ^name find-energy
              ^top-state.evaluation <eval>
              ^io.input-link.y <y>)
   (<eval> -^y <y>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name remove-stale-evaluation
         ^evaluation <eval>)
}


#It's better to remove stale evaluations before 
#generating new ones or moving in any direction
sp {find-energy*compare*remove-stale-evaluation
   (state <s> ^name find-energy
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name remove-stale-evaluation)
   (<op2> ^name << move gather-info >>)
-->
   (<s> ^operator <op1> > <op2>)
}

#remove the stale evaluation
sp {apply*remove-stale-evaluation
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name remove-stale-evaluation
         ^evaluation <eval>)
   (<ts> ^evaluation <eval>)
-->
   (<ts> ^evaluation <eval> -)
}

