#!/usr/bin/python
# Project: Soar <http://soar.googlecode.com>
# Author: Jonathan Voigt <voigtjr@gmail.com>
#
import distutils.sysconfig
import sys
import os
import SCons.Script

Import('env')
clone = env.Clone()

# Targets, sources, other constants
pythonsml_wrap = env.File('#ClientSMLSWIG/Python/Python_sml_ClientInterface_wrap.cpp')
pythonsml_i = env.File('#ClientSMLSWIG/Python/Python_sml_ClientInterface.i')
pythonsml_py = env.File('#ClientSMLSWIG/Python/Python_sml_ClientInterface.py')
swig_cmdline = 'swig -o ClientSMLSWIG/Python/Python_sml_ClientInterface_wrap.cpp -c++ -python -Wall -IClientSML/src -IElementXML/src -IConnectionSML/src ClientSMLSWIG/Python/Python_sml_ClientInterface.i'
pythonsml_shlib = 'Python_sml_ClientInterface'

# Scanner for dependencies in .i files
SWIGScanner = SCons.Scanner.ClassicCPP("SWIGScan",".i","CPPPATH",'^[ \t]*[%,#][ \t]*(?:include|import)[ \t]*(<|")([^>"]+)(>|")')
clone.Append(SCANNERS=[SWIGScanner])

# Create the wrap cpp and python file
env.Command(pythonsml_wrap, pythonsml_i, swig_cmdline)
env.SideEffect(pythonsml_py, pythonsml_wrap)

# Install python file to lib
env.Install('$PREFIX/lib', pythonsml_py)

# Prepare environment to build shared library
clone.Prepend(CPPPATH = Split('#ClientSML/src #ElementXML/src #ConnectionSML/src'))
clone.Prepend(CPPPATH = distutils.sysconfig.get_python_inc())

if os.name == 'posix':
	clone.Append(CXXFLAGS = Split('-Wno-unused -fno-strict-aliasing'))

clone.Append(LIBS = Split('SoarClient SoarKernel'))
clone.Append(LIBPATH = ['$PREFIX/lib'])

if sys.platform == 'darwin':
	clone.Replace(SHLINKFLAGS = Split('$LINKFLAGS -bundle -flat_namespace -undefined suppress'))
	clone.Replace(SHLIBSUFFIX = ['.so'])

clone.Append(CPPFLAGS = ['-w'])

shlib = clone.SharedLibrary(pythonsml_shlib, pythonsml_wrap) 

# libPython_sml_ClientInterface.so is not the correct naming convention for python dynamic code
# it must be named, in this case, _Python_sml_ClientInterface.so
env.InstallAs('$PREFIX/lib/_Python_sml_ClientInterface.so', shlib)

