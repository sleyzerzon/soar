###
### FILE               : waterjug.soar (Soar 8)
###
### AUTHOR             : John E. Laird
###
### Last Modified:     : June 19, 2003
###
### ABSTRACT.
### These Soar productions implement the waterjug task.
### This set does not involve planning or chunking.
###
### PROBLEM STATEMENT.
### You are given two empty jugs. One holds five gallons of water and
### the other holds three gallons. There is a well that has unlimited
### water in it that you can use to completely fill the jugs. You can
### also empty a jug or pour water from one jug to another. There are no
### marks for intermediate levels on the jugs. The goal is to fill the
### three-gallon jug with one gallon of water.
###
### A PSYCHOLOGICAL NOTE. The present set of productions could be used
### as a starting point to study models of and issues associated with
### (variant) water jug tasks. Relevant references are listed below.
###
### REFERENCES.
### [1] Atwood, M. E., & Polson, P. G. (1976). A process model for water
### jug problems. Cognitive Psychology, 8, 191-216.
### [2] Ernst, G. W., & Newell, A. (1969). GPS: A case study in
### generality and problem solving. New York: Academic Press.
### [3] Mortensen, U. (1973). Models for some elementary problem solving
### processes. In A. Elithorn & D. Jones (Eds.), Artificial and human
### thinking. San Francisco, CA: Jossey-Bass.
###
### DESCRIPTION.
### The task problem space has three operators: empty, fill, and
### pour. Empty empties a jug into the well. Fill fills up a jug from
### the well. Pour pours some or all of the contents from one jug into
### the other jug. Pour can only pour out the contents of the jug until
### the source is empty or the destination is full.
###
### State Structure:
### The state has augmentations for each jug.
### Each jug has attributes to record its capacity [volume],
###                the amount of water it contains [contents],
###                     and the capacity available [free].
###    (free = the volume minus the contents)
###
###   (state s1 ^jug j1)
###   (jug j1 ^volume v1 ^contents c1 ^free f1),
###         where v1, c1, and f1 are numbers.
###

multi-attributes jug 2
learn -off

###
### WATERJUG:
### TASK NAME AND
### INITIAL STATE AND### DESIRED STATE
###

sp {waterjug*elaborate*state*name
   (state <s> ^superstate nil)
   -->
   (<s> ^name waterjug)}

sp {waterjug*propose*initialize
   (state <s> ^name waterjug
             -^jug)
   -->
   (<s> ^operator <o> +)
   (<o> ^name initialize)}

sp {waterjug*apply*initialize
   (state <s> ^operator.name initialize)
   -->
   (<s> ^jug <i> <j>)
   (<i> ^volume 3
        ^contents 0) 
   (<j> ^volume 5
        ^contents 0)}

#######
sp {waterjug*elaborate*free
   (state <s> ^jug <j>)
   (<j> ^contents <c>
        ^volume <v>)
   -->
   (<j> ^free (- <v> <c>))}

###
### WATERJUG TASK OPERATORS:
### FILL, EMPTY, AND POUR
###

sp {waterjug*propose*fill
   (state <s> ^name waterjug
              ^jug <j>)
   (<j> ^free > 0)
   --> 
   (<s> ^operator <o> + =)
   (<o> ^name fill 
        ^jug <j>)}

sp {waterjug*propose*empty 
   (state <s> ^name waterjug
              ^jug <j>)
   (<j> ^contents > 0)
   -->
   (<s> ^operator <o> + =) 
   (<o> ^name empty
        ^jug <j>)}

sp {waterjug*propose*pour
   (state <s> ^name waterjug
              ^jug <i> { <> <i> <j> }) 
   (<i> ^contents > 0 )
   (<j> ^free > 0)
   --> 
   (<s> ^operator <o> + =)
   (<o> ^name pour 
        ^jug <i> 
        ^into <j>)}

###
### WATERJUG:
### OPERATOR APPLICATION
###

sp {apply*fill 
   (state <s> ^operator <o>
              ^jug <j>)
   (<o> ^name fill 
        ^jug <j>)
   (<j> ^volume <volume> 
        ^contents <contents>
        ^free <free>)
   --> 
   (<j> ^contents <volume> <contents> - 
        ^free 0 <free> - )}

sp {apply*empty
   (state <s> ^operator <o>
              ^jug <j>)
   (<o> ^name empty 
        ^jug <j>)
   (<j> ^volume <volume> 
        ^contents <contents> 
        ^free <free>)
   --> 
   (<j> ^contents 0 <contents> - 
        ^free <volume> <free> - )}

## Apply pour when contents of source jug i is less than or equal to the
## capacity available of the destination jug j.

sp {apply*pour*not-empty-source
   (state <s> ^operator <o>)
   (<o> ^name pour 
        ^jug <i> 
        ^into <j>)
   (<j> ^volume <jvol> 
        ^contents <jcon> 
        ^free <jfree>)
   (<i> ^volume <ivol> 
        ^contents <icon> <= <jfree> 
        ^free <ifree>)
   --> 
   (<i> ^contents 0 <icon> - 
        ^free <ivol> <ifree> - )
   (<j> ^contents (+ <jcon> <icon>) <jcon> - 
        ^free (- <jfree> <icon>) <jfree> - )}

## Implement pour when contents of source jug i is more than the
## capacity available of the destination jug j.

sp {apply*pour*empty-source
   (state <s> ^operator <o>)
   (<o> ^name pour 
        ^jug <i> 
        ^into <j>)
   (<i> ^volume <ivol> 
        ^contents <icon> > <jfree> 
        ^free <ifree>)
   (<j> ^volume <jvol> 
        ^contents <jcon> 
        ^free <jfree>)
   --> 
   (<i> ^contents (- <icon> <jfree>) <icon> - 
        ^free (+ <ifree> <jfree>) <ifree> - )
   (<j> ^contents <jvol> <jcon> - 
        ^free 0 <jfree> - )}

###
### EVALUATION:
### STATE SUCCESS/GOAL TEST
###

sp {waterjug*detect*goal*achieved
   (state <s> ^name waterjug
              ^jug <j>)
   (<j> ^volume 3 ^contents 1)
   --> 
   (write (crlf) |Success!|)
   (halt)}

### eof of waterjug.soar (Version Type: Soar8)



