waitsnc -e

sp {init
	(state <s> ^superstate nil)
-->
	(<s> ^time 0)
}

sp {make-model
	(state <s> ^superstate nil ^svs.command <c>)
-->
	(<c> ^model <mdl>)
	(<mdl> ^name model1 
	       ^type lwr
	       ^num-neighbors 20)
}

sp {gen-distractor
	(state <s>
		^superstate nil
		^svs.command <c>)
-->
	(<c> ^generate <g>)
	(<g> ^parent world
	     ^node <n>)
	(<n> ^type gen
	     ^name distractor
	     ^points.type singleton
	     ^pos <pos>)
	(<pos> ^x 0.0 ^y 0.0 ^z 0.0)
}

sp {gen-target
	(state <s>
		^superstate nil
		^svs.command <c>)
-->
	(<c> ^generate <g>)
	(<g> ^parent world
	     ^node <n>)
	(<n> ^type gen
	     ^name target
	     ^points.type singleton
	     ^pos <pos>)
	(<pos> ^type vec3
	       ^x 5.0
	       ^y -5.0
	       ^z 0.0)
}

sp {cursor-target-int
	(state <s> 
		^superstate nil
		^svs (
			^command <c>
			^spatial-scene (
				^child.id splinter
				^child.id target
			)
		)
	)
-->
	(<c> ^extract <e>)
	(<e> ^type bbox_int
	     ^a <a>
	     ^b <b>)
	(<a> ^type node ^name splinter)
	(<b> ^type node ^name target)
}

sp {explore
	(state <s>
		^superstate nil
		^svs.command <c>
	)
	-(<s> ^time >= 100)
-->
	(<c> ^control <ctl>)
	(<ctl> ^type random
	        ^outputs <out>)
	(<out> ^left <left> ^right <right>)
	(<left>  ^min -1.0 ^max 1.0 ^inc 1.0)
	(<right> ^min -1.0 ^max 1.0 ^inc 1.0)
}

sp {propose*count-time
	(state <s> ^superstate nil ^time <t>)
-->
	(<s> ^operator <o> +)
	(<o> ^name count-time)
}

sp {apply*count-time
	(state <s>
		^superstate nil
		^time <t>
		^operator.name count-time
	)
-->
	(<s> ^time <t> - (+ <t> 1))
}

sp {seek-target
	(state <s>
		^superstate nil
		^svs (
			^command <c>
			^spatial-scene (
				^child.id splinter
				^child.id target
			)
		)
	)
	-(<s> ^time < 100)
-->
	(<c> ^control <ctl>)
	(<ctl> ^type simplex
	       ^depth 5
	       ^outputs <out>
	       ^objective <obj>
	       ^model model1)
	(<out> ^left <left> ^right <right>)
	(<left>  ^min -1.0 ^max 1.0 ^inc 1.0)
	(<right> ^min -1.0 ^max 1.0 ^inc 1.0)
	(<obj> ^name euclidean
	       ^a splinter
	       ^b target)
}
