sp {move-to-box
   (state <s> ^superstate nil
              ^auto-control t
              ^svs <svs>)
   (<svs> ^command <c>
          ^spatial-scene <scn>)
   (<scn> ^child.id splinter
          ^child.id target1)
   -{(<s> ^intersect <i>)
     (<i> ^a splinter ^b box1)}
-->
   (<c> ^seek <sk>)
   (<sk> ^type tree
         ^depth 300
         ^outputs <out>
         ^objective <obj1>)
   (<out> ^left <left> ^right <right>)
   (<left>  ^min -0.1 ^max 1.0 ^inc 1.0)
   (<right> ^min -0.1 ^max 1.0 ^inc 1.0)
   (<obj1> ^name behind
           ^a box1
           ^b target1
           ^c splinter
           ^next <obj2>)
   (<obj2> ^name collinear
           ^a splinter
           ^b box1
           ^c target1
           ^next <obj3>)
   (<obj3> ^name align_facing
           ^a splinter
           ^b box1
           ^c target1
           ^next <obj4>)
   (<obj4> ^name euclidean
           ^a splinter
           ^b box1)}

sp {push-box-to-target
   (state <s> ^superstate nil
              ^auto-control t
              ^svs <svs>
              ^intersect <int>)
   (<svs> ^command <c>
          ^spatial-scene <scn>)
   (<scn> ^child.id splinter
		  ^child.id box1
		  ^child.id target1)
   (<int> ^a splinter ^b box1)
-->
   (<c> ^seek <sk>)
   (<sk> ^type simplex
         ^depth 50
         ^outputs <out>
         ^objective <obj>)
   (<out> ^left <left> ^right <right>)
   (<left>  ^min -0.1 ^max 1.0 ^inc 1.0)
   (<right> ^min -0.1 ^max 1.0 ^inc 1.0)
   (<obj> ^name euclidean
          ^a box1
          ^b target1)}

sp {manual
   (state <s> ^superstate nil 
              ^svs.command <c>
             -^auto-control t)
-->
   (<c> ^manual_control.outputs <out>)
   (<out> ^left <left> ^right <right>)
   (<left>  ^min 0.0 ^max 1.0 ^inc 1.0)
   (<right> ^min 0.0 ^max 1.0 ^inc 1.0)}
