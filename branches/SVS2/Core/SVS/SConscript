#!/usr/bin/python
# Project: Soar <http://soar.googlecode.com>
# Author: Jonathan Voigt <voigtjr@gmail.com>
#

Import('env')
clone = env.Clone()
clone.Prepend(CPPPATH = Split('src src/filters src/models #SoarKernel/src'))

src = ['#SVS/src/filter_factory.cpp'] + Glob('src/*.cpp') + Glob('src/filters/*.cpp') + Glob('src/commands/*.cpp') + Glob('src/models/*.cpp')
Command('#SVS/src/filter_factory.cpp', Glob('src/filters/*.cpp'), './gen_filter_factory', chdir='SVS')
svs = clone.StaticLibrary('svs', src)
env.Install('$PREFIX/lib', svs)

clone.Prepend(LIBS = ['armadillo'])

lwrtest = clone.Program('lwrtest', ['src/models/lwr.cpp', '#SVS/tests/lwrtest.cpp'])
env.Install('#SVS/tests', lwrtest)

splintersim = clone.Program('splintersim', ['#SVS/demos/robot/splintersim.cpp', 'src/linalg.cpp'])
env.Install('#SVS/demos/robot', splintersim)
