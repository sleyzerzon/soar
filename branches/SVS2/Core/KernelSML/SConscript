#!/usr/bin/python
# Project: Soar <http://soar.googlecode.com>
# Author: Jonathan Voigt <voigtjr@gmail.com>
#
Import('env')
clone = env.Clone()

clone.Prepend(CPPPATH = Split('src #SoarKernel/src #CLI/src #ElementXML/src #ConnectionSML/src #SVS/src'))

if not env['STATIC_LINKED']:
	clone.Append(LIBS = Split('ElementXML CommandLineInterface ConnectionSML SoarKernel sqlite3 svs'))
	clone.Append(LIBPATH = ['$PREFIX/lib'])

if env['SCU']:
	srcs = 'KernelSML.cxx'
else:
	srcs = Glob('src/*.cpp')

if env['STATIC_LINKED']:
	SoarKernelSML = clone.Library('SoarKernelSML', srcs)
else:
	SoarKernelSML = clone.SharedLibrary('SoarKernelSML', srcs)

env.Install('$PREFIX/lib', SoarKernelSML)

# Install resources
Import('InstallDir')
InstallDir(SoarKernelSML, '$PREFIX/share/soar/Demos', '#Demos')
InstallDir(SoarKernelSML, '$PREFIX/share/soar/Documentation', '#Documentation')

