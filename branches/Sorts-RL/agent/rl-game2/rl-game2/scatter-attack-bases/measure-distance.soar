sp {scatter-attack-bases*propose*measure-distance
   (state <s> ^name scatter-attack-bases
              ^memory <m>
              ^my-pid <mpid>
              ^enemy-pid <epid>
              ^io.input-link.groups <gs>)
   (<gs> ^group <my-tanks>
         ^group <enemy-base>)
   (<my-tanks> ^owner <mpid>
               ^type tank
               ^id <mtid>)
   (<enemy-base> ^owner <epid>
                 ^type |controlCenter|
                 ^id <ebid>)
  -{(<m> ^distance-measure <dm>)
    (<dm> ^my-tank-id <mtid>
          ^enemy-base-id <ebid>)}
-->
   (<s> ^operator <o> + > =)
   (<o> ^name measure-distance
        ^my-tanks <my-tanks>
        ^enemy-base <enemy-base>)}

sp {scatter-attack-bases*apply*measure-distance
   (state <s> ^name scatter-attack-bases
              ^memory <m>
              ^my-pid <mpid>
              ^enemy-pid <epid>
              ^operator <o>)
   (<o> ^name measure-distance
        ^my-tanks <my-tanks>
        ^enemy-base <enemy-base>)
   (<my-tanks> ^id <mtid>
               ^x-pos <mtx>
               ^y-pos <mty>)
   (<enemy-base> ^id <ebid>
                 ^x-pos <ebx>
                 ^y-pos <eby>)
-->
   (<m> ^distance-measure <dm>)
   (<dm> ^my-tank-id <mtid>
         ^enemy-base-id <ebid>
         ^distance (+ (* (- <mtx> <ebx>) (- <mtx> <ebx>)) (* (- <mty> <eby>) (- <mty> <eby>))))}
   