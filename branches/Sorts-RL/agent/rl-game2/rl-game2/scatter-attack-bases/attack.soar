sp {scatter-attack-bases*propose*attack
   (state <s> ^name scatter-attack-bases
              ^my-pid <mpid>
              ^enemy-pid <epid>
              ^io.input-link.groups <gs>)
   (<gs> ^group <my-tanks>
         ^group <enemy-base>)
   (<my-tanks> ^owner <mpid>
               ^type tank
               ^id <mtid>)
   (<enemy-base> ^owner <epid>
                 ^type |controlCenter|
                 ^id <ebid>)
   (<s> ^distance-measure <dm>)
   (<dm> ^my-tank-id <mtid>
         ^enemy-base-id <ebid>
         ^distance <d>)
  -{(<gs> ^group <closer-target>)
    (<closer-target> ^owner <epid>
                     ^type |controlCenter|
                     ^id <ctid>)
    (<s> ^distance-measure <dm1>)
    (<dm1> ^my-tank-id <mtid>
           ^enemy-base-id <ctid>
           ^distance < <d>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name attack
        ^my-tanks <my-tanks>
        ^enemy-base <enemy-base>)}

sp {scatter-attack-bases*apply*attack
   (state <s> ^name scatter-attack-bases
              ^io.output-link.sorts <cmd>
              ^operator <o>)
   (<o> ^name attack
        ^my-tanks <my-tanks>
        ^enemy-base <enemy-base>)
   (<my-tanks> ^id <mtid>)
   (<enemy-base> ^id <ebid>)
-->
   (<cmd> ^attack <a>)
   (<a> ^group0 <mtid>
        ^group1 <ebid>)}