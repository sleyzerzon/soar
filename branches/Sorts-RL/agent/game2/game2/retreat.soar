sp {elaborate*needs-to-retreat
   (state <s> ^name game2
              ^situation normal
              ^io.input-link.groups <gs>
              ^attack-memory <am>)
   (<am> ^target <tid>
         ^double-unit-count <duc>
        -^needs-to-retreat)
   (<gs> ^group <tg>
         ^group <backup>)
   (<tg> ^id <tid>
         ^num_members > <duc>)
-->
   (<am> ^needs-to-retreat 1)}

sp {elaborate*retreat-distance
   (state <s> ^name game2
              ^my-id <pid>
              ^io.input-link.groups <gs>
              ^attack-memory <am>)
   (<am> ^attacker <aid>
         ^needs-to-retreat)
   (<gs> ^group <retreating>
         ^group <backup>)
   (<retreating> ^id <aid>
                 ^x-pos <x1>
                 ^y-pos <y1>)
   (<backup> ^id <bid>
             ^owner <pid>
             ^type tank
             ^speed 0 # we don't want to chase
            -^enemy-intersect
             ^x-pos <x2>
             ^y-pos <y2>)
-->
   (<am> ^possible-retreat <pr>)
   (<pr> ^retreater <aid>
         ^backup <backup>
         ^distance (+ (* (- <x1> <x2>) (- <x1> <x2>)) (* (- <y1> <y2>) (- <y1> <y2>))))}

sp {propose*retreat
   (state <s> ^name game2
              ^io.input-link.groups <gs>
              ^attack-memory <am>)
   (<am> ^attacker <aid>
         ^possible-retreat <pr>)
   (<pr> ^retreater <aid>
         ^backup <backup>
         ^distance <d>)
  -{(<am> ^possible-retreat <alt>)
    (<alt> ^distance < <d>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name retreat
        ^retreating <aid>
        ^backup <backup>)}

sp {apply*retreat
   (state <s> ^name game2
              ^my-pid <pid>
              ^enemy <eid>
              ^io.output-link <out>
              ^operator <o>)
   (<o> ^name retreat
        ^retreating <rid>
        ^backup <backup>)
   (<backup> ^x-pos <x>
             ^y-pos <y>)
-->
   (<out> ^move <m>)
   (<m> ^group0 <rid>
        ^param0 <x>
        ^param1 <y>)}