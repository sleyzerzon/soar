#IF I can see the energy charger on radar
#AND there is no energy path from here
#THEN create an energy path
sp {recall-energy-path*propose*record-energy-path*on-radar
   (state <s> ^name recall-energy-path
              ^io.input-link <il>
             -^top-state.available-energy-path)
   (<il> ^radar <radar>
         ^x <x>
         ^y <y>
         ^direction <dir>)
   (<radar> ^energy)
-->
   (<s> ^operator <op> + =)
   (<op> ^name record-energy-path
         ^x <x>
         ^y <y>
         ^abs-dir <dir>)
   (write (crlf) |RADAR: I can see energy | <dir> | of here.|)
}

#IF I have retrieved an epmem of seeing the
#   energy character from this position
#AND there is no energy path from here
#THEN create a path to it
sp {recall-energy-path*propose*record-energy-path*direct-epmem
   (state <s> ^name recall-energy-path
              ^io.input-link <il>
              ^epmem.retrieved.io.input-link <ril>
             -^top-state.available-energy-path)
   (<il> ^x <x>
         ^y <y>)
   (<ril> ^x <x>
          ^y <y>
          ^radar.energy
          ^direction <dir>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name record-energy-path
         ^x <x>
         ^y <y>
         ^abs-dir <dir>)
   (write (crlf) |EPMEM: I last saw energy | <dir> | of here.|)
}

#IF a record-energy-path operator has been selected
#THEN create an energy-path on the top-state
sp {apply*record-energy-path
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name record-energy-path
         ^x <x>
         ^y <y>
         ^abs-dir <abs-dir>)
-->
   (<ts> ^energy-path <ep>)
   (<ep> ^x <x>
         ^y <y>
         ^abs-dir <abs-dir>)
   (write (crlf) |Creating energy path from here.|)
}
