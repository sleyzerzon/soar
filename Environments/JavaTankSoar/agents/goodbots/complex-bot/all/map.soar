#echo "\nLoading all/map.soar"

##### compute current square 

sp {all*map*curent-square
   (state <s> ^io.input-link <il>
              ^map.square <cs>)
   (<il> ^x <x>
         ^y <y>)
   (<cs> ^x <x>
         ^y <y>)
-->
   (<s> ^square <cs>)}

## Mark visited
sp {map*visited*square
   :o-support
   (state <s> ^square <cs>)
  -(<cs> ^visited *yes*)
-->
   (<cs> ^visited *yes*)}

#Map radar-distance

sp {map*mark-radar-distance
   :o-support
   (state <s> ^io.input-link <io>
              ^square.radar-distances <rd>)
   (<io> ^radar.obstacle <ob>
         ^direction <dir>)
   (<ob> ^distance <d>
         ^position center)
  -(<rd> ^<dir>)
-->
   (<rd> ^<dir> (- <d> 1))}

sp {map*record*square-direction
   :o-support
   (state <s> ^io.input-link <il>
              ^square <cs>)
   (<il> ^direction <dir>
         ^radar-status on)
-->
   (<cs> ^recorded <dir>)}

######################mark using radar

sp {map*mark-open
   (state <s> ^map <m>
              ^io.input-link <io>
              ^square <cs>
              ^radar-map.<dir> <dirr>)
   (<dirr> ^<pos> <pss>
           ^sx <sx>
           ^sy <sy>)
   (<pss> ^x <dx>
          ^y <dy>)
   (<io> ^radar.<< tank energy health missiles flag open >> <ob>
         ^direction <dir>)
   (<ob> ^distance <d>
     ^position <pos>)
   (<cs> ^x <x>
         ^y <y>
        -^recorded <dir>)
-->
;##  (write (crlf) | OPEN: | (+ (+ <x> (* <sx> <d>)) <dx>) |, |
;##                          (+ (+ <y> (* <sy> <d>)) <dy>))
   (<m> ^open <obs>)
   (<obs> ^x (+ (+ <x> (* <sx> <d>)) <dx>)
          ^y (+ (+ <y> (* <sy> <d>)) <dy>))}

sp {map*mark-open*set
   :o-support
   (state <s> ^map <m>)
   (<m> ^open <obs>
        ^square <sq>)
   (<sq> ^x <x> ^y <y>)
   (<obs> ^x <x> ^y <y>)
  -(<sq> ^open *yes*)
-->
   (<sq> ^open *yes*)}

sp {map*mark-open*set*there
   :o-support
   (state <s> ^square <sq>)
   (<sq> -^open *yes*)
-->
   (<sq> ^open *yes*)}

sp {map*mark-energy*set*there
   :o-support
   (state <s> ^io.input-link.energyrecharger yes
              ^square <sq>)
   (<sq> -^energy *yes*)
-->
   (<sq> ^energy *yes*)}

sp {map*mark-health*set*there
   :o-support
   (state <s> ^io.input-link.healthrecharger yes
              ^square <sq>)
   (<sq> -^health *yes*)
-->
   (<sq> ^health *yes*)}


###############OBSTACLES##################
sp {map*mark-obstacle*x0
   :o-support
   (state <s> ^map.square <sq>)
   (<sq> ^x 0)
-->
   (<sq> ^obstacle *yes*)}

sp {map*mark-obstacle*y0
   :o-support
   (state <s> ^map.square <sq>)
   (<sq> ^y 0)
-->
   (<sq> ^obstacle *yes*)}

sp {map*mark-obstacle*x15
   :o-support
   (state <s> ^map.square <sq>)
   (<sq> ^x 15)
-->
   (<sq> ^obstacle *yes*)}

sp {map*mark-obstacle*y15
   :o-support
   (state <s> ^map.square <sq>)
   (<sq> ^y 15)
-->
   (<sq> ^obstacle *yes*)}

sp {map*mark-obstacle
   (state <s> ^map <m>
              ^io.input-link <io>
              ^square <cs>
              ^radar-map.<dir> <dirr>)
   (<dirr> ^<pos> <pss>
           ^sx <sx>
           ^sy <sy>)
   (<pss> ^x <dx>
          ^y <dy>)
   (<io> ^radar.{ <type> << health energy obstacle >> } <ob>
         ^direction <dir>)
   (<ob> ^distance <d>
   ^position <pos>)
   (<cs> ^x <x>
         ^y <y>
        -^recorded <dir>)
-->
   (<m> ^<type> <obs>)
   (<obs> ^x (+ (+ <x> (* <sx> <d>)) <dx>)
          ^y (+ (+ <y> (* <sy> <d>)) <dy>))}

sp {map*mark-obstacle*set
   :o-support
   (state <s> ^map <m>)
   (<m> ^{<type> << obstacle health energy >>} <obs>
        ^square <sq>)
   (<sq> ^x <x> ^y <y>)
   (<obs> ^x <x> ^y <y>)
  -(<sq> ^<type> *yes*)
-->
   (<sq> ^<type> *yes*)}

sp {map*mark-missiles
   (state <s> ^io.input-link <io>
              ^map <m>
              ^square <cs>
              ^radar-map.<dir> <dirr>)
   (<dirr> ^<pos> <pss>
           ^sx <sx>
           ^sy <sy>)
   (<pss> ^x <dx>
          ^y <dy>)
   (<io> ^radar.missiles <ob>
         ^direction <dir>)
   (<ob> ^distance <d>
         ^position <pos>)
   (<cs> ^x <x>
         ^y <y>)
-->
   (<m> ^missiles <obs>)
   (<obs> ^x (+ (+ <x> (* <sx> <d>)) <dx>)
          ^y (+ (+ <y> (* <sy> <d>)) <dy>))}

sp {map*mark-type*set
   :o-support
   (state <s> ^map <m>)
   (<m> ^missiles <obs>
        ^square <sq>)
   (<sq>  ^x <x> 
          ^y <y>)
   (<obs> ^x <x> 
          ^y <y>)
-->
   (<sq> ^missiles *yes*)}

sp {map*clean*missile*occupied
   :o-support
   (state <s> ^io.input-link <il>
              ^square <cs>)
   (<cs> ^missiles *yes*)
-->
   (<cs> ^missiles *yes* -)}

sp {map*clean*missiles
   :o-support
   (state <s> ^map <m>)
   (<m> ^square <sq>
        ^open <obs>)
 -{(<m> ^missiles <mm>)
   (<mm> ^x <x> 
         ^y <y>)}
   (<sq> ^x <x> 
         ^y <y>
         ^missiles *yes*)
   (<obs> ^x <x> 
          ^y <y>)
-->
   (<sq> ^missiles *yes* -)}
