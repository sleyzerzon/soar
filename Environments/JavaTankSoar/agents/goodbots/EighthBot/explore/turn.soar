echo "\nLoading explore/turn.soar "

################################
### Propose Turn operator if blocked
################################

## turn towards least visited direction if blocked in front
sp {explore*propose*turn*not-blocked
   (state <s> ^name explore
              ^io.input-link <il>
              ^status.square <sq>
              ^direction-map <dm>)
   (<il> ^blocked <b>
         ^direction <heading>)
   (<b> ^forward no
        ^{ << left right >> <dirA>} no
        ^{ << left right >> <dirB> <> <dirA>} no)
   (<dm> ^<heading>.<dirA> <headA>
         ^<heading>.<dirB> <headB>)
   (<sq> ^<headA>.visit <vA>
        -^<headB>.visit    <  <vA>
        -^<heading>.visit  <=  <vA>)         ### forward square
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn 
        ^actions <a>)
   (<a> ^rotate.direction <dirA>
        ^radar.switch on
        ^radar-power.setting 13)}


## turn towards least visited direction if blocked in front
sp {explore*propose*turn*blocked-side
   (state <s> ^name explore
              ^io.input-link <il>
              ^status.square <sq>
              ^direction-map <dm>)
   (<il> ^blocked <b>
         ^direction <heading>)
   (<b> ^forward no
        ^{ << left right >> <dirA>} no
        ^{ << left right >> <dirB> <> <dirA>} yes)
   (<dm> ^<heading>.<dirA> <headA>)
   (<sq> ^<headA>.visit <vA>
        -^<heading>.visit  <=  <vA>)         ### forward square
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn 
        ^actions <a>)
   (<a> ^rotate.direction <dirA>
        ^radar.switch on
        ^radar-power.setting 13)}



## turn towards least visited direction if blocked in front
sp {explore*propose*turn*blocked-front
   (state <s> ^name explore
              ^io.input-link <il>
              ^status.square <sq>
              ^direction-map <dm>)
   (<il> ^blocked <b>
         ^direction <heading>)
   (<b> ^forward yes
        ^{ << left right >> <dirA>} no
        ^{ << left right >> <dirB> <> <dirA>} no)
   (<dm> ^<heading>.<dirA> <headA>
         ^<heading>.<dirB> <headB>)
   (<sq> ^<headA>.visit <vA>
        -^<headB>.visit  <  <vA>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn 
        ^actions <a>)
   (<a> ^rotate.direction <dirA>
        ^radar.switch on
        ^radar-power.setting 13)}


## turn if blocked forward and to one side
sp {explore*propose*turn*blocked-front-side
   (state <s> ^name explore
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^ { << left right >> <dir> } no
        ^ << left right >> yes)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn 
        ^actions <a>)
   (<a> ^rotate.direction <dir>
        ^radar.switch on
        ^radar-power.setting 13)}




sp {explore*propose*turn*backward
   (state <s> ^name explore
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^left yes ^right yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name turn 
        ^actions.rotate.direction left)}


