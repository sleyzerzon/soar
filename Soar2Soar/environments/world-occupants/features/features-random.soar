sp {features*propose*features-random
   (state <s> ^name features
              ^feature <f>)
   (<f> ^type random
        ^option <o>)
   (<o> ^chance <chance>)
-->
   (<s> ^operator <op> + = <chance>)
   (<op> ^name features-random
         ^option <o>)
}

sp {apply*features-random*input
   (state <s> ^operator <op>
              ^agent.input <a-i>
              ^feature.name <f-name>)
   (<op> ^name features-random
         ^option.name <f-val>)
-->
   (<a-i> ^<f-name> <f-val>)
}

sp {apply*features-random*next
   (state <s> ^operator <op>
              ^feature <f>)
   (<op> ^name features-random)
   (<f> ^next {<> nil <f-next>})
-->
   (<s> ^feature <f> - <f-next>)
}

sp {apply*features-random*nil
   (state <s> ^operator <op>
              ^feature <f>
              ^agent <a>
              ^todo <a>)
   (<op> ^name features-random)
   (<f> ^next nil)
-->
   (<s> ^feature <f> - nil
        ^agent <a> -
        ^todo <a> -)
}
