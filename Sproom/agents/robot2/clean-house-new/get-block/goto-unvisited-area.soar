sp {clean-house*propose*goto-unvisited-area
   (state <s> ^name get-block
              ^top-state.parameters.areas-held-in wm
              ^current-location.cleaned yes
              ^top-state.areas.area <area>)
   (<area> ^visited no)
-->
   (<s> ^operator <op> + =)
   (<op> ^name goto-unvisited-area
         ^type go-to-area
         ^area <area>)
}

sp {clean-house*compare*go-to-area*connected
   (state <s> ^current-location <ca>
              ^top-state.parameters.search-control-go-to-gateway yes
              ^operator <op1> +
                        <op2> +)
   (<op1> ^type go-to-area
          ^name <name>
          ^area.id <to-id1>)
   (<ca> ^gateway.to <to-id1>)
   (<op2> ^type go-to-area
          ^name <name>
          ^area.id <to-id2>)
  -(<ca> ^gateway.to <to-id2>)
-->
   (<s> ^operator <op1> > <op2>)
}
#############SMEM

sp {get-block*propose*goto-unvisited-area*smem
   (state <s> ^name get-block
             -^top-state.all-visited true
              ^top-state.parameters.objects-held-in smem)
-->
   (<s> ^operator <op> + =, <)
   (<op> ^name goto-unvisited-area
         ^type go-to-area
         ^storage-area no)
}

sp {apply*goto-unvisited-area*smem*query
   (state <s> ^operator.name goto-unvisited-area
              ^top-state.parameters.objects-held-in smem
              ^smem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^visited no)}

sp {apply*goto-unvisited-area*smem*result
 #  :interrupt
   (state <s> ^operator.name goto-unvisited-area
              ^smem <smem>)
   (<smem> ^command.query <q>
           ^result <res>)
   (<res> ^retrieved <cp>
          ^success <q>)
-->
  #(write (crlf) |Retrieved dirty area | <cp> )
   (<s> ^area <cp>)}

sp {apply*goto-unvisited-area*smem*operator
   (state <s> ^operator <o>
              ^area <area>)
   (<o> ^name goto-unvisited-area)
-->
  ##(write (crlf) |Retrieved area | <area> )
   (<o> ^area <area>)}

sp {apply*goto-unvisited*smem*result*failure
   (state <s> ^operator.name goto-unvisited-area
              ^top-state <ts>
              ^smem <smem>)
   (<smem> ^command.query <q>
           ^result.failure <q>)
-->
  #(write (crlf) |Failed to find unvisited area |)
   (<ts> ^all-visited true)}

sp {get-block*compare*goto-unvisited-area
   (state <s> ^name get-block
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name goto-unvisited-area)
   (<op2> ^name goto-not-clean-area)
-->
   (<s> ^operator <op1> > <op2>)
}
