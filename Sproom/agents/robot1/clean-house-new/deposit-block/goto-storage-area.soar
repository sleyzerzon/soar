sp {deposit-block*propose*goto-storage-area
   (state <s> ^name deposit-block
              ^top-state <ts>)
   (<ts> ^storage-area <area>
         ^current-location <> <area>)
-->
 # #(write (crlf) |Going to found storage area: | <id>)
   (<s> ^operator <op> + =, >)
   (<op> ^name goto-storage-area
         ^type go-to-area
         ^area <area>)
}

sp {deposit-block*propose*find-storage-area
   (state <s> ^name deposit-block
              ^top-state <ts>)
   (<ts> -^storage-area 
          ^parameters.objects-held-in wm
          ^current-location { <> <area> <ca> }
          ^areas.area <area>)
   (<area> -^visited yes
            ^id <nid> )
-->
#  #(write (crlf) |Searching for storage area: | <id> | via: | <nid>)
   (<s> ^operator <op> + =)
   (<op> ^name goto-storage-area
         ^type go-to-area
         ^area <area>)
}

############SMEM

sp {deposit-block*propose*find-storage-area*smem
   (state <s> ^name deposit-block
              ^top-state <ts>)
   (<ts> -^storage-area 
          ^parameters.objects-held-in smem)
-->
   (<s> ^operator <op> + =)
   (<op> ^name goto-storage-area
         ^type go-to-area)
}

sp {apply*find-storage-area*smem*query
   (state <s> ^operator.name goto-storage-area
             -^top-state.storage-area
              ^top-state.parameters.objects-held-in smem
              ^smem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^visited no)}

sp {apply*find-storage-area*smem*result
   (state <s> ^operator <o>
              ^smem <smem>)
   (<o> ^name goto-storage-area)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^retrieved <cp>
          ^success <q>)
-->
  #(write (crlf) |Retrieved not visited area | <cp> )
   (<s> ^area <cp>)}

sp {apply*find-storage-area*smem*operator
   (state <s> ^operator <o>
              ^area <area>)
   (<o> ^name goto-storage-area)
-->
  ##(write (crlf) |Retrieved area | <area> )
   (<o> ^area <area>)}
