
## If crossing through a gateway area to get to a room
sp {go-to-area*propose*go-to-gateway*cross-doorway-area
   (state <s> ^type go-to-area
              ^top-state.parameters.search-control-go-to-gateway yes
              ^current-location <ca>)
   (<ca> ^previous-area.id <id>
         ^type door
         ^gateway <gw>)
   (<gw> ^to <> <id>)
-->
   (<s> ^operator <op> +, =, >)
   (<op> ^name go-to
         ^destination <gw>
         ^cross-doorway yes)
}

### IF final destination is the next area
sp {go-to-area*propose*go-to-waypoint*door
   (state <s> ^type go-to-area
              ^top-state.parameters.search-control-go-to-gateway yes
              ^destination-area.id <daid>
              ^current-location <ca>)
   (<ca> ^gateway <gw>
         ^type door)
   (<gw> ^to <daid>)
-->
   (<s> ^operator <op> +, =, >)
   (<op> ^name go-to
         ^destination <gw>
         ^gateway-area-id <daid>
         ^cross-doorway yes)
}
sp {go-to-area*propose*go-to-waypoint
   (state <s> ^type go-to-area
              ^top-state.parameters.search-control-go-to-gateway yes
             -^name evaluate-operator
              ^destination-area.id <daid>
              ^current-location <ca>)
   (<ca> ^type room
         ^gateway <gw>)
   (<gw> ^to <daid>)
-->
   (<s> ^operator <op> +, =, >)
   (<op> ^name go-to
         ^destination <gw>
         ^gateway-area-id <daid>)
}


############### Random
sp {go-to-area*propose*go-to-gateway*cross-doorway-area*no-sc
   (state <s> ^type go-to-area
              ^top-state.parameters.search-control-go-to-gateway no
              ^current-location <ca>)
   (<ca> ^previous-area.id <id>
         ^type door
         ^gateway <gw>)
   (<gw> ^to <> <id>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name go-to
         ^destination <gw>
         ^cross-doorway yes)
}

### IF final destination is the next area
sp {go-to-area*propose*go-to-waypoint*door*no-sc
   (state <s> ^type go-to-area
              ^top-state.parameters.search-control-go-to-gateway no
              ^destination-area.id <daid>
              ^current-location <ca>)
   (<ca> ^gateway <gw>
         ^type door)
   (<gw> ^to <daid>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name go-to
         ^destination <gw>
         ^gateway-area-id <daid>
         ^cross-doorway yes)
}
sp {go-to-area*propose*go-to-waypoint*no-sc
   (state <s> ^type go-to-area
              ^top-state.parameters.search-control-go-to-gateway no
             -^name evaluate-operator
              ^destination-area.id <daid>
              ^current-location <ca>)
   (<ca> ^type room
         ^gateway <gw>)
   (<gw> ^to <daid>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name go-to
         ^destination <gw>
         ^gateway-area-id <daid>)
}