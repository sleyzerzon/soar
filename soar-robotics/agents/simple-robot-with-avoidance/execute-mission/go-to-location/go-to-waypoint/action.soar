# propose the action associated with each unblocked arc
sp {go-to-waypoint*propose*action
   (state <s> ^name go-to-waypoint
              ^superoperator.move-now true
              ^io.input-link.ranges.range <r>)
   (<r> ^blocked false
        ^action <a>
        ^distance <d>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name action
         ^action <a>
         ^distance <d>)
}

# prefer actions associated with closer arcs
sp {go-to-waypoint*compare*action
   (state <s> ^name go-to-waypoint
              ^operator <op1> +
                        <op2> +)
   (<op1> ^distance <d>)
   (<op2> ^distance > <d>)
-->
   (<s> ^operator <op1> > <op2>)
}

# if all arcs are blocked, propose turning left
sp {go-to-waypoint*propose*turn-left
   (state <s> ^name go-to-waypoint
              ^superoperator.move-now true
              ^io.input-link.ranges <rs>)
  -(<rs> ^range.blocked false)
-->
   (<s> ^operator <op> + =)
   (<op> ^name action
         ^action <a>)
   (<a> ^rotate <params>)
   (<params> ^direction left
             ^throttle 1.0)
}

sp {apply*action
   (state <s> ^operator <op>
              ^io.output-link <ol>)
   (<op> ^name action
         ^action <a>)
   (<a> ^<action> <params>)
-->
   (<ol> ^<action> <params>)
}
