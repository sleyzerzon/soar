sp {go-to-location*propose*give-up-on-move-to
   (state <s> ^name go-to-location
              ^top-state.move-to <mt>)
   (<mt> ^give-up true)
-->
   (<s> ^operator <op> +)
   (<op> ^name give-up-on-move-to
         ^move-to <mt>)
}

# if the give-up is marked as immediate, then make this better
# (giving up will be marked as immediate if the agent receives a message
#  that says its destination is unreachable from its source)
sp {go-to-location*compare*give-up-on-move-to-better-than-update-progress
   (state <s> ^name go-to-location
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name give-up-on-move-to
          ^move-to.give-up immediate)
   (<op2> ^name update-progress)
-->
   (<s> ^operator <op1> > <op2>)
}

# don't let low-level movement continue once we've decided to quit
sp {apply*give-up-on-move-to*stop
   (state <s> ^operator <op>
              ^io.output-link <ol>)
   (<op> ^name give-up-on-move-to)
-->
   (<ol> ^stop <x>)
}

# this fires when subgoal is finished
sp {apply*give-up-on-move-to
   (state <s> ^operator <op>
              ^waypoints.at <w>
              ^top-state <ts>)
   (<op> ^name give-up-on-move-to
         ^move-to <mt>)
   (<ts> ^move-to <mt>)
   (<mt> ^map-updated true
         ^source <w>)
-->
   (<ts> ^move-to <mt> -)
}
