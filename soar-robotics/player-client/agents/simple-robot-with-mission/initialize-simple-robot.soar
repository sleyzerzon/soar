sp {propose*initialize-simple-robot
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialize-simple-robot)
}

sp {apply*initialize-simple-robot
   (state <s> ^operator <op>)
   (<op> ^name initialize-simple-robot)
-->
   (<s> ^name simple-robot
        ^entities <e>
        ^waypoint-tolerance 0.85 # how close (in meters) the agent needs to be to a waypoint to be considered "at" it
        ^replan-time 3 # how many seconds (as reported on the input-link) the agent should let the collision avoidance algorithm try to make progress towards the waypoint before replanning
        ^message-update-time 3 # after how many seconds (as reported on the input-link) the agent should send another positition update of an unfriendly entity)
        ^rotation-tolerance 15 # how many degrees the agent's yaw can be off when it has a desired yaw
   )
}

sp {apply*initialize-simple-robot*create-mission
   (state <s> ^operator <op>)
   (<op> ^name initialize-simple-robot)
-->
   (<s> ^mission <m>)
   (<m> ^first <s1>)
   (<s1> ^name start
         ^delay-time 0
         ^next <s2>)
   (<s2> ^name go-to-location
         ^x -6.5 ^y 0
         ^next <s3>)
   (<s3> ^name scout
         ^next nil)
}

#sp {elaborate*initialize-simple-robot*set-desired
#   (state <s> ^waypoints.waypoint <w>)
#   (<w> ^x -2 ^y -1)
#-->
#   (<s> ^desired <w>)
#}
#
#sp {at-goal
#   (state <s> ^superstate nil
#              ^desired <w>
#              ^waypoints.at <w>)
#-->
#   (write (crlf) |At goal|)
#   (halt)
#}
