sp {go-to-location*propose*choose-waypoint
   (state <s> ^name go-to-location
              ^desired
              ^waypoints.at.next <w>
             -^top-state.go-to-location) # not already going to a location
-->
#   (interrupt)
   (<s> ^operator <op> +)
   (<op> ^name choose-waypoint
         ^destination <w>)
}

# create a top-state structure describing the destination
# add a waypoint for the destination
# remove the waypoint that we're "at"
sp {apply*choose-waypoint
   (state <s> ^operator <op>
              ^top-state <ts>
              ^io <io>
              ^waypoints.at <wa>)
   (<io> ^input-link.self.waypoints.waypoint <wr>
         ^output-link <ol>)
   (<op> ^name choose-waypoint
         ^destination <w>)
   (<w> ^id <ida> ^x <x> ^y <y>)
   (<wa> ^id <idr>)
   (<wr> ^id <idr>)
-->
   (<ts> ^go-to-location <gtl>)
   (<gtl> ^waypoint <w>)
   (<ol> ^add-waypoint <aw>
         ^remove-waypoint <rw>)
   (<aw> ^id <ida> ^x <x> ^y <y>)
   (<rw> ^id <idr>)
}

sp {select*choose-waypoint
   (state <s> ^operator <op> +
              ^top-state.mission.current <mission>)
   (<mission> ^id <waypoint>
              ^name go-to-location)
   (<op> ^name choose-waypoint
         ^destination.id <waypoint>)
-->
   (<s> ^operator <op> >)
}
